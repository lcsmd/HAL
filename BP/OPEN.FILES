SUBROUTINE OPEN.FILES
* Opens all data files defined in SCHEMA_INDEX.csv
* Uses dynamic file numbers from schema

COMMON /FILES/ FILES(100)

* Read schema index
INDEX.FILENAME = "SCHEMA/SCHEMA_INDEX.csv"
OPENSEQ INDEX.FILENAME TO F.INDEX ELSE
   PRINT "Error: Cannot open ":INDEX.FILENAME
   STOP
END

* Read all lines
CSV.LINES = ""
LINE.COUNT = 0
LOOP
   READSEQ LINE FROM F.INDEX ELSE EXIT
   LINE.COUNT += 1
   IF LINE.COUNT > 1 THEN
      CSV.LINES<-1> = LINE
   END
REPEAT
CLOSESEQ F.INDEX

* Build arrays of filenames and file numbers
FILENAMES = ""
FILENUMS = ""
FOR I = 1 TO DCOUNT(CSV.LINES, @FM)
   LINE = CSV.LINES<I>
   IF LINE = "" THEN CONTINUE
   
   FILENAME = FIELD(LINE, ",", 1)
   FILE.NUM = FIELD(LINE, ",", 3)
   STATUS = FIELD(LINE, ",", 8)
   
   IF STATUS = "active" THEN
      FILENAMES<-1> = FILENAME
      FILENUMS<-1> = FILE.NUM
   END
NEXT I

* Open all files
FILE.CT = DCOUNT(FILENAMES, @FM)
FAILED.FILES = ""

FOR FILE.X = 1 TO FILE.CT
   FILENAME = FILENAMES<FILE.X>
   FILE.NUM = FILENUMS<FILE.X>
   OPEN FILENAME TO FILES(FILE.NUM) ELSE
      FAILED.FILES<-1> = FILENAME
   END
NEXT FILE.X

* Report failures
IF FAILED.FILES # "" THEN
   FAILED.CT = DCOUNT(FAILED.FILES, @FM)
   PRINT "THE FOLLOWING ":FAILED.CT:" FILES FAILED TO OPEN:"
   FOR FAILED.X = 1 TO FAILED.CT
      PRINT SPACE(5):FAILED.FILES<FAILED.X>
   NEXT FAILED.X
   PRINT ""
END ELSE
   PRINT "All ":FILE.CT:" files opened successfully"
END

RETURN
END

