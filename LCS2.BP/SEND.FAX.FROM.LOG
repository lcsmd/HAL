          PROGRAM SEND.FAX.FROM.LOG

          JOB.ID = TRIM(FIELD(@SENTENCE," ",2))
          ACTION = TRIM(FIELD(@SENTENCE," ",3))
          COPY.OPT = TRIM(FIELD(@SENTENCE," ",4))
          IF COPY.OPT = "" THEN COPY.OPT = 'N'
          IF JOB.ID = "" THEN
               PRINT "SEND.FAX.FROM.JOB <JOB.ID> [<ACTION> <COPY.OPTION>]"
               STOP
          END

          EQU IN.FILE.NAME TO INARGS<1,1>
          EQU IN.ITEM.NAME TO INARGS<1,2>
          EQU USER TO INARGS<3>
          EQU OUT.FILE.NAME TO INARGS<4,1>
          EQU OUT.ITEM.NAME TO INARGS<4,2>

          EQU SEND.TO.DATA TO INARGS<5>
          EQU SEND.TO.NAME TO INARGS<5,1>,SEND.TO.COMPANY TO INARGS<5,2>,SEND.TO.EMAIL TO INARGS<5,3>
          EQU SEND.TO.FAX TO INARGS<5,4>,SEND.TO.TYPE TO INARGS<5,5>,SEND.TO.REF TO INARGS<5,6>

          EQU SEND.FROM.DATA TO INARGS<6>
          EQU SEND.FROM.USER TO INARGS<6,1>, SEND.FROM.EMAIL TO INARGS<6,2>, SEND.FROM.PHONE TO INARGS<6,3>, SEND.FROM.COMPANY TO INARGS<6,4>
          EQU SEND.FROM.FAX TO INARGS<6,5>, SEND.FROM.PHOTO TO INARGS<6,6>, SEND.FROM.TTILE TO INARGS<6,7>, SEND.FROM.DEPT TO INARGS<6,8>

          EQU MESSAGE.BODY TO INARGS<7>
          EQU COMMENTS TO INARGS<8>
          EQU SUBJECT TO INARGS<9>
          EQU COVERPG TO INARGS<10>
          EQU LASER TO INARGS<11>
          EQU COMMENT TO INARGS<12>
          EQU SENDVIA TO INARGS<13>

          OUTARGS = ""
          LM = 1
          INARGS = ""
          ERRORS = ""
          METHOD = ACTION[2,1]

          INARGS<2> = ACTION
          LFCR = CHAR(10):CHAR(13)

          OPEN 'CONTROL' TO CONTROL ELSE PRINT 'CONTROL' ; STOP
          OPEN 'LCS.CONTROL' TO LCS.CONTROL ELSE PRINT 'LCS.CONTROL' ; STOP
          OPEN 'LCS.MAIL' TO LCS.MAIL ELSE PRINT 'LCS.MAIL';STOP
          OPEN 'MASTER' TO MASTER ELSE PRINT 'MASTER' ; STOP
          READV DDI.PATH FROM LCS.CONTROL,"DDI.PATH",1 ELSE DDI.PATH = "C:\U2\UV\ACCOUNTS\DDI\"
          READV LCS.PATH FROM LCS.CONTROL,"LCS.PATH",1 ELSE LCS.PATH = "C:\U2\UV\ACCOUNTS\DDI.LCS\"
          READV LOGS.PATH FROM LCS.CONTROL,"LOGS.PATH",1 ELSE LOGS.PATH = "C:\U2\UV\ACCOUNTS\DDI.LOGS\"
          READV HOME.PATH FROM LCS.CONTROL,"HOME.PATH",1 ELSE HOME.PATH = "\\CE1\Folder Redirection\"
          READ COMP.B FROM CONTROL,'COMPANY' ELSE COMP.B = ''
          *
          READV SMTP.SERVER FROM LCS.CONTROL,"SMTP.SERVER",1 ELSE SMTP.SERVER = "CESMX.CES.LOCAL"
          READV FAX.OUT.PATH FROM LCS.CONTROL,"FAX.OUT.PATH",1 ELSE FAX.OUT.PATH = LCS.PATH:"FAXES\OUT\"
          READV PDF.OUT.PATH FROM LCS.CONTROL,"PDF.OUT.PATH",1 ELSE PDF.OUT.PATH = LCS.PATH:"PDF\OUT\"
          READV LOG.FILEPATH FROM LCS.CONTROL,"LOG.FILEPATH",1 ELSE LOG.FILEPATH = LOGS.PATH:"LOGS\"
          !
          BASE = DDI.PATH
          PCL.BASE = "C:\U2\UV\ACCOUNTS\DDI\PW.PCL"
          PDF.BASE = "C:\U2\UV\ACCOUNTS\DDI\CONSERVE.FAX"
          EMAILSERVER = "cesmx.ces.local"
          EMAILSERVER = "smtp.rcn.com"
          PWLOG.NAME = 'C:\PWLOG-':OCONV(DATE(),'D2-'):'.TXT'

          READ COMP.B FROM CONTROL,'COMPANY' ELSE COMP.B = '' ; PRINT 'COMPANY RECORD MISSING' ; STOP
          IN.FILE.NAME = "PW.PCL"
          OUT.FILE.NAME = "CONSERVE.FAX"
          OPEN IN.FILE.NAME TO F.IN.FILE ELSE PRINT IN.FILE.NAME ; STOP
          OPEN OUT.FILE.NAME TO F.OUT.FILE ELSE PRINT OUT.FILE.NAME ; STOP

          OPEN 'FAX.LOG' TO FAX.LOG ELSE PRINT 'FAX.LOG' ; STOP
          OPEN 'FAX.DET' TO FAX.DET ELSE PRINT 'FAX.DET' ; STOP
          SELECT.STR = 'SELECT FAX.LOG WITH JOB.ID = ':JOB.ID
          IF LM THEN PRINT SELECT.STR
          EXECUTE SELECT.STR CAPTURING CAP
          IF LM THEN PRINT CAP
          READLIST AA THEN
               FAX.LOG.ID = AA<1>
               USER=FIELD(AA,"*",1)
               READ FAX.LOG.B FROM FAX.LOG,AA THEN
                    GOSUB PARSE.FROM.DATA:
                    GOSUB PARSE.TO.DATA:
                    GOSUB PARSE.LOG.DATA:
               END ELSE PRINT "FAX.LOG RECORD MISSING ":AA ; STOP
               JOBNO = FIELD(AA,"*",2)
               DOS.SCRIPT.NAME = "MAIL":JOBNO:'.BAT'
               PS.SCRIPT.NAME = "MAIL":JOBNO:".PS1"
               SUBJECT = "(ID:":JOBNO:") ":SUBJECT
               DOS.SCRIPT.BODY = ""
               CONVERT "." TO "" IN SEND.TO.NAME
               NEW.ITEM.NAME = CHANGE(AA,"*","_")
               PCL.ITEM = NEW.ITEM.NAME:".PCL"
               IN.ITEM.NAME = PCL.ITEM
               PCL.NAME = PCL.BASE:"\":PCL.ITEM

          * CREATE PCL FILE
               READ FAX.B FROM FAX.DET,AA THEN
                    IF LM THEN PRINT "WRITING PCL FILE ":PCL.ITEM
                    WRITE FAX.B ON F.IN.FILE,PCL.ITEM
               END

          *ADD LINE TO BATCH FILE TO CREATE PDF
               PDF.NAME = PDF.BASE:"\":NEW.ITEM.NAME:".PDF"
               PDF.CMD = '"C:\Program Files (x86)\Rasmussen Software, Inc\Print Wizard 4.1\PRINTWIZ.EXE" /PDF ':PCL.NAME:' /Q /LOG=':PWLOG.NAME:' /W /F=':PDF.NAME
               OUT.ITEM.NAME = NEW.ITEM.NAME:".PDF"
               IN.FILE.NAME = "CONSERVE.FAX"
               IF LM THEN PRINT PDF.CMD
               DOS.SCRIPT.BODY<-1> =  PDF.CMD

          *ADD LINE TO EXECUTE POWERSHELL SCRIPT WHCIH WILL DELIVER EMAIL/FAX
               IF (ACTION[1,1] = 'E' ! ACTION[1,1] = "F") THEN
                    INARGS<2> = ACTION
                    INARGS<15> = JOBNO

          *WRITE BATCH FILE
                    WRITE DOS.SCRIPT.BODY ON LCS.MAIL,DOS.SCRIPT.NAME

                    CALL SEND.DOC(INARGS,OUTARGS,ERRORS)
                    READ DOS.SCRIPT.BODY FROM LCS.MAIL,DOS.SCRIPT.NAME ELSE DOS.SCRIPT.BODY = ''
                    IF LM THEN PRINT "READING ":DOS.SCRIPT.NAME
                    IF LM THEN PRINT DOS.SCRIPT.BODY
                    READV TESTS FROM LCS.MAIL,PS.SCRIPT.NAME,1 THEN
                         DOS.SCRIPT.BODY<-1> = 'POWERSHELL.EXE "& C:\U2\UV\ACCOUNTS\DDI\LCS.MAIL\':PS.SCRIPT.NAME:'"'
                    END
               END

          *ADD LINE TO BATCH FILE TO COPY DOCUMENT TO SHAREPOINT SITE
               IF COPY.OPT <> "N" THEN
                    GOSUB COPY.PDF:
               END

          *WRITE BATCH FILE
               WRITE DOS.SCRIPT.BODY ON LCS.MAIL,DOS.SCRIPT.NAME

          *SUBMIT BATCH FILE AS JOB FOR IMMEDIATE EXECUTION
               IF DOS.SCRIPT.BODY > "" THEN
                    DOS.CMD = 'DOS /C C:\U2\UV\ACCOUNTS\DDI\LCS.MAIL\':DOS.SCRIPT.NAME
          *                        DOS.CMD = 'DOS /C SCHTASKS /CREATE /TN ':DOS.SCRIPT.NAME:' /SC ONCE /ST ':OCONV(TIME()+61,'MT'):' /F /TR ':DOS.CMD
                    IF LM THEN PRINT DOS.CMD
                    EXECUTE DOS.CMD CAPTURING CAP
                    IF LM THEN PRINT CAP
                    SLEEP 1
               END

          END


          CLOSE F.IN.FILE
          CLOSE F.OUT.FILE
          PRINT "FINISHED"
          STOP

          *EQU SEND.TO.DATA TO INARGS<5>
          *EQU SEND.TO.NAME TO INARGS<5,1>,SEND.TO.COMPANY TO INARGS<5,2>,SEND.TO.EMAIL TO INARGS<5,3>
          *EQU SEND.TO.FAX TO INARGS<5,4>,SEND.TO.TYPE TO INARGS<5,5>,SEND.TO.REF TO INARGS<5,6>
          *EQU SEND.FROM.DATA TO INARGS<6>
          *EQU SEND.FROM.USER TO INARGS<6,1>, SEND.FROM.EMAIL TO INARGS<6,2>, SEND.FROM.PHONE TO INARGS<6,3>, SEND.FROM.COMPANY TO INARGS<6,4>
          *EQU SEND.FROM.FAX TO INARGS<6,5>, SEND.FROM.PHOTO TO INARGS<6,6>, SEND.FROM.TTILE TO INARGS<6,7>, SEND.FROM.DEPT TO INARGS<6,8>

PARSE.TO.DATA:
          IF NOT(SEND.TO.EMAIL) THEN
               SUBJECT = "*** Error: An request to email was received without a valid 'Email to:' address"
               EMAILBODY = "Please review and resend your message.  No email was sent for this request"
          END
          IF NOT(SEND.TO.NAME) THEN SEND.TO.NAME = SEND.TO.EMAIL
          RETURN

PARSE.FROM.DATA:
          COEMAIL = COMP.B<161>
          IF COEMAIL = '' THEN
               ERRORS = 'Company Email Address is Missing!'
               RETURN
          END
          READ USER.B FROM MASTER, 'SYSTEM.USERS' ELSE USER.B = ''
          LOCATE USER IN USER.B<4> SETTING VM THEN
               USEREMAIL = USER.B<8,VM>
               USERNAME = TRIM(FIELD(USEREMAIL,'@',1))
          END ELSE USEREMAIL = ''
          IF USEREMAIL = '' THEN USEREMAIL = COEMAIL
          SEND.FROM.USER = USERNAME
          SEND.FROM.EMAIL = USEREMAIL
          IF NOT(SEND.FROM.FAX) THEN SEND.FROM.FAX = COMP.B<6>
          RETURN

PARSE.LOG.DATA:
          SENDVIA = FAX.LOG.B<21>
          IF ACTION = "" THEN ACTION = SENDVIA
          SUBJECT = FAX.LOG.B<4>
          FAX.DATE= OCONV(FAX.LOG.B<5>,"D2-")
          FAX.TIME = OCONV(FAX.LOG.B<6>,"MTH")
          IF ACTION[1,1] = 'P' THEN PDF.COPY.NAME = FAX.LOG.B<5,1>
          IF ACTION[1,1] = "E" ! ACTION[1,1] = "F" THEN
               IF SENDVIA = "E" THEN SEND.TO.EMAIL = FAX.LOG.B<1>
               IF SENDVIA = "F" THEN SEND.TO.FAX = FAX.LOG.B<1>
               SEND.TO.NAME = FAX.LOG.B<2>
               SEND.TO.COMPANY = FAX.LOG.B<3>
               SEND.FROM.USER = FAX.LOG.B<14>
               SEND.FROM.COMPANY = FAX.LOG.B<15>
               IF FAX.LOG.B<17> = "Y" THEN COVERPG = "DEFAULT.COV" ELSE COVERPG = FAX.LOG.B<17>
               IF COVERPG ="N" THEN COVERPG = ""
               MESSAGE.BODY = FAX.LOG.B<19>
          END
          LASER = FAX.LOG.B<20>
          RETURN

COPY.PDF:
          GOSUB COPY.PDF.HOME:
          *GOSUB COPY.PDF.USER:
          RETURN



COPY.PDF.HOME:
          TARGET.DIRECTORY = HOME.PATH:USERNAME:'\Documents\_PDF'
          COPY.DOS.CMD = 'COPY ':PDF.NAME:' "':TARGET.DIRECTORY:'" /Y'
          DOS.SCRIPT.BODY<-1> = COPY.DOS.CMD
          RETURN


COPY.PDF.USER:
          TARGET.DIRECTORY = LCS.PATH:'USER_PDF\':USERNAME
          COPY.DOS.CMD = 'COPY ':PDF.NAME:' ':TARGET.DIRECTORY:' /Y'
          EXECUTE 'DOS /C ':COPY.DOS.CMD
          DOS.SCRIPT.BODY<-1> = COPY.DOS.CMD
          RETURN


