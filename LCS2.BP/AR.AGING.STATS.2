SUBROUTINE AR.AGING.STATS.2
!BP AR.AGING.STATS.2                          >AR AGING STATISTICS II
!                                              (Detailed Inquiry &
!                                               Select Items for Letter)
!
!CREATED BY> LEON SHOYKHETBROD           DATE >11/21/94
!CHANGED BY>
!CLIENT    > WAL-RICH CORP.                   >
!
!
!
!-------------------------------------------------------------------------
!
! COMMON CELL BLOCK
!
COMMON CTRL.F, CUST.F, LETT.F, FILES(5), FNAME.B, ATTRIBUTE
COMMON INVLIST, SEL.B
!
READ COMP.B FROM CTRL.F, 'COMPANY' ELSE COMP.B = ''
IF COMP.B<22> = 'D' THEN
HDR = 'DUE-DATE'; ATTR = 8
END ELSE HDR = 'INV-DATE'; ATTR = 2
!
$INCLUDE UTLSBP ASSIGN.VAR
!
VMSG = C22:@(15):DMON:'Select ':DMOFF:'Line Number ':DMON
VMSG = VMSG:'to TAG or UN-TAG, Range ':DMOFF:'(nn-nn)':DMON:', ':DMOFF:'I':DMON
VMSG = VMSG:'nvoice + ':DMOFF:'#'
!
! EXECUTE COMMAND
!
PRINT C21:EOS:FON:'Loading please wait....':FOFF:
CMND = 'SSELECT ':FNAME.B<ATTRIBUTE>:' BY NAME BY INV.DATE'
EXECUTE CMND
EOF = FALSE; INVLIST = ''
LOOP
READNEXT ID THEN INVLIST<-1> = ID ELSE EOF = TRUE
UNTIL EOF DO REPEAT
!
! DISPLAY SCREEN
!
TITLE = 'AR AGING STATISTICS'
CALL SCREENTITLE (TITLE, NEW.TITLE, COL)
SCR = CLS
SCR = SCR:@(COL,0):WIDE:NEW.TITLE
SCR = SCR:@(0,4):DMON:'LINE#  INVOICE#  CUSTOMER-NAME                   '
SCR = SCR:HDR:'  DAYS-OLD  AMOUNT-DUE':DMOFF
!
STL = 1; EDL = 16; PG = 1; SEL.B = ''
ITEMS = DCOUNT(INVLIST,IAMC)
IF ITEMS LE 16 THEN PGS = 1 ELSE
IF ITEMS/16 GT INT(ITEMS/16) THEN PGS = INT(ITEMS/16) + 1 ELSE
PGS = ITEMS/16
END
END
SCR = SCR:@(15,2):'Category ':OCONV(FNAME.B<ATTRIBUTE>,'G1,1')
SCR = SCR:@(35,2):ITEMS:' Invoice(s)'
PRINT SCR
GOSUB 5000 ;* DISPLAY INFO
!
! OPTIONS
!
1000  SEL = ''
LOOP
PRINT VMSG:
XMISC=''
XMISC<10>='23'
XMISC<4>=4:IVM:5:IVM:7:IVM:9
XMISC<5>='Pg Down':IVM:'Pg Up':IVM:'Tag/UnTag All':IVM:'Done'
CALL CIP(0,22,12,'',SEL,ESCFLG,XMISC)
PRINT C22:EOS:
IF XMISC<6>= 7 THEN
START=1
FINISH=ITEMS
GOSUB 2000
END
IF (SEL = 'F' ! XMISC<6>=4) & (EDL + 1 LE ITEMS) THEN
STL = EDL + 1; EDL = STL + 15; PG = PG + 1; GOSUB 5000
END
IF (SEL = 'B' ! XMISC<6>=5) AND STL # 1 THEN
EDL = STL - 1; STL = EDL - 15; PG = PG - 1; GOSUB 5000
END
IF SEL[1,1] = 'I' THEN
SEL = SEL[2,99]
IF NUM(SEL) THEN
SEL = INT(SEL)
IF SEL GT 0 AND SEL LE ITEMS THEN
INVOICE = INVLIST<SEL>
DATA 'INVINQ'
DATA INVOICE
EXECUTE 'OS'
PRINT SCR; GOSUB 5000
END
END
END ELSE
IF INDEX(SEL,'-',1) THEN
START = OCONV(SEL,'G-1'); FINISH = OCONV(SEL,'G1-1')
IF NUM(START) & NUM(FINISH) THEN
START = INT(START); FINISH = INT(FINISH)
IF START GT FINISH ELSE
IF START GT 0 AND START LE ITEMS THEN
IF FINISH GT 0 AND FINISH LE ITEMS THEN
GOSUB 2000
END
END
END
END
END ELSE IF NUM(SEL) THEN
SEL = INT(SEL)
IF SEL GT 0 AND SEL LE ITEMS THEN
START = SEL; FINISH = SEL; GOSUB 2000
END
END
END
UNTIL SEL = 'E' OR XMISC<6> = 9 OR ESCFLG DO
REPEAT
SCT = DCOUNT(SEL.B<1>,IVM)
IF SCT THEN
LOOP
PRINT C22:ULON:SCT:ULOFF:DMON:' Invoice(s) tagged, Print Letters ? (Y)es/(N)o...':DMOFF:
CALL CIP (55, 22, 1, '', RESP, ESCFLG, '')
IF ESCFLG THEN RESP = 'N'
UNTIL RESP = 'Y' ! RESP = 'N' DO REPEAT
PRINT C22:
IF RESP = 'Y' THEN
CALL AR.AGING.STATS.3
PRINT SCR; GOSUB 5000; GO 1000
END
END
RETURN
!
! DISPLAY INFO
!
5000  DISPLAY = @(0,5):EOS; LINE = 4
DISPLAY = DISPLAY:@(55,2):EOL:ULON:'Page ':PG:' of ':PGS:ULOFF
CTR = STL - 1
LOOP
CTR = CTR + 1; LINE = LINE + 1
WHILE CTR LE ITEMS & CTR LE EDL DO
DISPLAY = DISPLAY:@(0,LINE)
LOCATE CTR IN SEL.B<1> SETTING VM THEN DISPLAY = DISPLAY:LON
DISPLAY = DISPLAY:OCONV(CTR,'MD,') 'R#5  '
DISPLAY = DISPLAY:INVLIST<CTR> 'L#8  '
READ OI.B FROM FILES(ATTRIBUTE), INVLIST<CTR> ELSE OI.B = ''
READV NAME FROM CUST.F, OI.B<1>, 1 ELSE NAME = ''
DISPLAY = DISPLAY:NAME 'L#30  '
DISPLAY = DISPLAY:OCONV(OI.B<ATTR>,'D2/') 'L#8  '
DISPLAY = DISPLAY:OCONV(DATE()-OI.B<ATTR>,'MD,') 'R#8  '
DISPLAY = DISPLAY:OCONV(OI.B<6>,'MD2,') 'R#10':LOFF
IF OI.B<41> # "" THEN DISPLAY = DISPLAY:FLON:"d":FLOFF
REPEAT
PRINT DISPLAY:
RETURN
!
! TAGG OR UN-TAGG ROUTINE
!
2000  CTR = START - 1
LOOP
CTR = CTR + 1
WHILE CTR LE FINISH DO
LOCATE CTR IN SEL.B<1> BY 'AR' SETTING VL THEN
DEL SEL.B<1,VL>
END ELSE INS CTR BEFORE SEL.B<1,VL>
REPEAT
GOSUB 5000
RETURN
