    SUBROUTINE PRICE.SUB (PROD.B, CUST.B, CUSTNO, PRDNO, SPTO, QNTY, UOM, XMISC)
    !
    !
    !











    !
    !      XMISC <1> = CALCULATED PRICE
    !            <2> = LONG FURMULA METHOD DESCRIPTION
    !            <3> = DISCOUNT PERCENT
    !            <4> = LIST PRICE
    !            <5> = QUANTITY BREAKS
    !            <6> = SHORT FURMULA
    !            <7> = CHAIN DISCOUNT USED
    !            <8> = % - FOR <COST + ITEMS>
    !            <9> = SHOW Chain (D)iscount, (M)ultiplier,
    !                       (B)oth or (N)on
    !           <21> = SAME AS 1-20 COST FORMULA
    !           <41> = PRICING DATE
    !
    INCLUDE UTLSBP ASSIGN.VAR
    !
    F = ''
    OPEN 'PRICE.MATRIX' TO F.PRICE.MATRIX ELSE F = F:',PRICE.MATRIX'
    OPEN 'PRICE.GROUP' TO F.PRICE.GROUP ELSE F=F:', PRICE.GROUP'
    OPEN 'PROD.LINE' TO F.PROD.LINE ELSE F=F:', PROD.LINE'
    OPEN 'PRICE.CONTRACT' TO F.PRICE.CONTRACT ELSE F = F:',PRICE.CONTRACT'
    OPEN 'CONTROL' TO F.CONTROL ELSE F = F:',CONTROL'
    IF INDEX(CUSTNO,'PRICE.CHANGE',1) THEN
        OPEN 'PRICE.CHANGE' TO F.PRICE.CHANGE ELSE F = F:', PRICE.CHANGE'
    END
    !
    IF TRIM(F) # '' THEN PRINT FILE.ERRORS:F:BCK: ; INPUT NL,1:_ ; STOP
    !
    READ CUSTOM.PGM.PARAM FROM F.CONTROL, 'CUSTOM.PRICING.PROGRAM' THEN
        CUSTOM.PGM = CUSTOM.PGM.PARAM<1>
        CALL @CUSTOM.PGM (PROD.B, CUST.B, CUSTNO, PRDNO, SPTO, QNTY, UOM, XMISC)
        IF XMISC<2> = '' THEN XMISC<2> = CUSTOM.PGM.PARAM<2>
        IF XMISC<2> = '' THEN XMISC<2> = 'Custom'
        IF XMISC<3> = '' THEN XMISC<3> = 0
        IF XMISC<4> = '' THEN XMISC<4> = XMISC<1>
        IF XMISC<6> = '' THEN XMISC<6> = 'Custom'
        RETURN
    END
    !
    CNO = CUSTNO
    IF CNO = '' THEN CNO = JUNK
    DLLR = QNTY<1,2>
    WGHT = QNTY<1,3>
    PRICE.DATE = QNTY<1,4>
    IF PRICE.DATE = '' THEN PRICE.DATE = DATE()


    FUTURE = QNTY<1,5>
    BRANCH = QNTY<1,6>
    IF FUTURE THEN
        OPEN 'FUTURE.PRICE.MATRIX' TO F.PRICE.MATRIX ELSE STOP
    END
    QNTY = QNTY<1,1>
    READ COMP.B FROM F.CONTROL, 'COMPANY' ELSE COMP.B = ''
    PRDEC = COMP.B<25>
    ROUNDTO = COMP.B<15>
    CSTFLD = COMP.B<24>
    !
    PRICE.FACTOR = OCONV(UOM,'TUOM;X;1;1')
    IF PRICE.FACTOR = '' THEN PRICE.FACTOR = 1
    !
    READV CPO FROM F.CONTROL, 'CUSTOMER.PRODUCT.OVERRIDE.PM.ALL', 1 ELSE CPO = ''
    READV CONTR.PROD.OVER FROM F.CONTROL, 'PRODUCT.CONTRACT.OVERRIDE.PM',1 ELSE CONTR.PROD.OVER = ''
    !
    ! PRODUCT PRICE/COST CHANGE HISTORY FIELDS
    !
    CHANGE.B = 'C1ýC4ýC6ýC7ýL1ýL2ýL3ýL4'
    CHANGE.B<2> = '129ý130ý131ý132ý133ý134ý135ý136'
    CHANGE.B<3> = '3ý62ý122ý124ý13ý64ý66ý126'
    READV MAJ.PGROUP FROM F.PRICE.GROUP, PROD.B<112>,2 ELSE MAJ.PGROUP=''
    !
    PR.RND.DEC = ''
    READV PR.RND.DEC FROM F.PRICE.GROUP,PROD.B<112>,3 ELSE PR.RND.DEC = ''
    IF PR.RND.DEC = '' THEN
        READV PR.RND.DEC FROM F.PROD.LINE,PROD.B<26>,26 ELSE PR.RND.DEC = ''
    END
    IF PR.RND.DEC # '' AND NUM(PR.RND.DEC) THEN
        PR.RND.DEC = PR.RND.DEC * 100
        PR.RND.DEC = FIELD(OCONV(PR.RND.DEC,'MD4'),'.',2)
    END ELSE
        PR.RND.DEC =''
    END
    MODE = 'COST'
1   :    XMISC = '' ; PSPTO = SPTO
    MASTER.PRICE.CNO=CUST.B<104>
    TEMP.CUST.B=CUST.B
    TEMP.CNO=CNO
    CHECK.MASTER.CNO = FALSE
    !
    IF TRIM(PSPTO) = '' THEN PSPTO = JUNK
    !
    ! PRICE LEVEL 1    (Product Only)
    !
    IF CPO # 'Y' THEN
        IF BRANCH # '' THEN
            LVL.DESC = 'Br\Product'
            PMID = '||':PRDNO:'|||||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
        LVL.DESC = 'Product'
        PMID = '||':PRDNO:'|||||' ; GOSUB 1000
        IF LVL.OK THEN GO 4000
    END
    !
    ! PRICE LEVEL 2    (Shipto/Product)
    !
105 :  IF PSPTO # '' & PSPTO # JUNK THEN
        IF BRANCH # '' THEN
            IF TEMP.CNO = CNO THEN
                LVL.DESC = 'Br\ShipTo\Prod'
            END ELSE LVL.DESC='Br\MstrShp\Prod'
            PMID = CNO:'||':PRDNO:'||':PSPTO:'|||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN CNO=TEMP.CNO ; GO 4000
        END
        IF TEMP.CNO = CNO THEN
            LVL.DESC = 'ShipTo\Prod'
        END ELSE LVL.DESC='MstrShp\Prod'
        PMID = CNO:'||':PRDNO:'||':PSPTO:'|||' ; GOSUB 1000
        IF LVL.OK THEN CNO=TEMP.CNO ; GO 4000
    END
    !
    ! PRICE LEVEL 3    (Shipto/Price Group/Product Line)
    !
    IF PROD.B<26> # '' & PROD.B<112> # '' & PSPTO # '' & PSPTO # JUNK THEN
        IF BRANCH # '' THEN
            IF TEMP.CNO = CNO THEN
                LVL.DESC = 'Br\Shipto\PG'
            END ELSE LVL.DESC = 'Br\MstrShpt\PG'
            PMID = CNO:'|||':PROD.B<26>:'|':PSPTO:'|':PROD.B<112>:'||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
            IF MAJ.PGROUP # '' THEN
                PMID = CNO:'||||':PSPTO:'|':MAJ.PGROUP:'||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
            END
        END
        IF TEMP.CNO = CNO THEN
            LVL.DESC = 'Shipto\PG'
        END ELSE LVL.DESC = 'MstrShpt\PG'
        PMID = CNO:'|||':PROD.B<26>:'|':PSPTO:'|':PROD.B<112>:'||' ; GOSUB 1000
        IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
        IF MAJ.PGROUP # '' THEN
            PMID = CNO:'||||':PSPTO:'|':MAJ.PGROUP:'||' ; GOSUB 1000
            IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
        END
    END
    !
    ! PRICE LEVEL 4    (Shipto/Price Group)
    !
    IF PSPTO # '' & PSPTO # JUNK THEN
        IF BRANCH # '' THEN
            IF TEMP.CNO = CNO THEN
                LVL.DESC = 'Br\Shipto\PG'
            END ELSE LVL.DESC = 'Br\MstrShp\PG'
            IF PROD.B<112> # '' THEN
                PMID = CNO:'||||':PSPTO:'|':PROD.B<112>:'||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
            END
            IF MAJ.PGROUP # '' THEN
                PMID = CNO:'||||':PSPTO:'|':MAJ.PGROUP:'||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
            END
        END
        IF TEMP.CNO = CNO THEN
            LVL.DESC = 'Shipto\PG'
        END ELSE LVL.DESC = 'MstrShp\PG'
        IF PROD.B<112> # '' THEN
            PMID = CNO:'||||':PSPTO:'|':PROD.B<112>:'||' ; GOSUB 1000
            IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
        END
        IF MAJ.PGROUP # '' THEN
            PMID = CNO:'||||':PSPTO:'|':MAJ.PGROUP:'||' ; GOSUB 1000
            IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
        END
    END
    !
    ! PRICE LEVEL 5    (Shipto/Product Line)
    !
    IF PSPTO # '' & PSPTO # JUNK THEN
        IF BRANCH # '' THEN
            IF TEMP.CNO = CNO THEN
                LVL.DESC = 'Br\Shipto\PL'
            END ELSE LVL.DESC='Br\MstrShpt\PL'
            PMID = CNO:'|||':PROD.B<26>:'|':PSPTO:'|||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN CNO=TEMP.CNO ; GO 4000
        END
        IF TEMP.CNO = CNO THEN
            LVL.DESC = 'Shipto\PL'
        END ELSE LVL.DESC='MstrShpt\PL'
        PMID = CNO:'|||':PROD.B<26>:'|':PSPTO:'|||' ; GOSUB 1000
        IF LVL.OK THEN CNO=TEMP.CNO ; GO 4000
    END
    !
    ! PRICE LEVEL 6    (Customer/Product)
    !
    IF BRANCH # '' THEN
        IF TEMP.CNO = CNO THEN
            LVL.DESC = 'Br\Cust\Prod'
        END ELSE LVL.DESC='Br\Mstr\Prod'
        PMID = CNO:'||':PRDNO:'|||||':BRANCH ; GOSUB 1000
        IF LVL.OK THEN CNO=TEMP.CNO ; GO 4000
    END
    IF TEMP.CNO = CNO THEN
        LVL.DESC = 'Cust\Prod'
    END ELSE LVL.DESC='Mstr\Prod'
    PMID = CNO:'||':PRDNO:'|||||' ; GOSUB 1000
    IF LVL.OK THEN CNO=TEMP.CNO ; GO 4000
    !
    ! PRICE LEVEL 12   (Contract/Product)
    !
    IF CONTR.PROD.OVER = "Y" THEN
        ALL.CONTRACTS = '' ; ACTR = 0
        CONTRACTS = DCOUNT(CUST.B<113>,CHAR(253))
        CONTR.CTR = 0
        LOOP
            CONTR.CTR = CONTR.CTR + 1
        WHILE CONTR.CTR LE CONTRACTS DO
            READV CONTR.NAME FROM F.PRICE.CONTRACT, CUST.B<113,CONTR.CTR>, 1 THEN
                LVL.OK = FALSE
                IF BRANCH # '' THEN
                    LVL.DESC = 'Contract\Br\Prod'
                    PMID = '||':PRDNO:'||||':CUST.B<113,CONTR.CTR>:'|':BRANCH ; GOSUB 1000
                END
                IF NOT(LVL.OK) THEN
                    LVL.DESC = 'Contract\Prod'
                    PMID = '||':PRDNO:'||||':CUST.B<113,CONTR.CTR>:'|' ; GOSUB 1000


                END
                IF LVL.OK THEN
                    ACTR = ACTR + 1
                    FOR I = 1 TO 10
                        ALL.CONTRACTS<I,ACTR> = XMISC<I>
                    NEXT I
                    ALL.CONTRACTS<11,ACTR> = CUST.B<113,CONTR.CTR>
                    XMISC = ''
    *                 GO 1500
                END
            END
        REPEAT


        IF ALL.CONTRACTS # "" THEN GO 1500
    END
    !
    ! PRICE LEVEL 7    (Customer/Price Group/Product Line)
    !
    IF PROD.B<26> # '' & PROD.B<112> # '' THEN
        IF BRANCH # '' THEN
            IF TEMP.CNO = CNO THEN
                LVL.DESC = 'Br\Customer\PGPL'
            END ELSE LVL.DESC = 'Br\MstrCust\PGPL'
            PMID = CNO:'|||':PROD.B<26>:'||':PROD.B<112>:'||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
            IF MAJ.PGROUP # '' THEN
                PMID = CNO:'|||':PROD.B<26>:'||':MAJ.PGROUP:'||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
            END
        END
        IF TEMP.CNO = CNO THEN
            LVL.DESC = 'Customer\PGPL'
        END ELSE LVL.DESC = 'MstrCust\PGPL'
        PMID = CNO:'|||':PROD.B<26>:'||':PROD.B<112>:'||' ; GOSUB 1000
        IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
        IF MAJ.PGROUP # '' THEN
            PMID = CNO:'|||':PROD.B<26>:'||':MAJ.PGROUP:'||' ; GOSUB 1000
            IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
        END
    END
    !
    ! PRICE LEVEL 8    (Customer/Price Group)
    !
    IF BRANCH # '' THEN
        IF TEMP.CNO = CNO THEN
            LVL.DESC = 'Br\Customer\PG'
        END ELSE LVL.DESC = 'Br\MstrCust\PG'
        IF PROD.B<112> # '' THEN
            PMID = CNO:'|||||':PROD.B<112>:'||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
        END
        IF MAJ.PGROUP # '' THEN
            PMID = CNO:'|||||':MAJ.PGROUP:'||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
        END
    END
    IF TEMP.CNO = CNO THEN
        LVL.DESC = 'Customer\PG'
    END ELSE LVL.DESC = 'MstrCust\PG'
    IF PROD.B<112> # '' THEN
        PMID = CNO:'|||||':PROD.B<112>:'||' ; GOSUB 1000
        IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
    END
    IF MAJ.PGROUP # '' THEN
        PMID = CNO:'|||||':MAJ.PGROUP:'||' ; GOSUB 1000
        IF LVL.OK THEN CNO = TEMP.CNO ; GO 4000
    END
    !
    ! PRICE LEVEL 22   (Customer Category/Price Group)
    !
    READV VERIFY FROM F.CONTROL, 'CATEGORY.PRICE.GROUP.OVERRIDE', 1 THEN
        IF BRANCH # '' THEN
            IF PROD.B<112> # '' THEN
                LVL.DESC = 'Br\CustCat\PG'
                PMID = '|':CUST.B<17>:'||||':PROD.B<112>:'||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN GO 4000
                IF MAJ.PGROUP # '' THEN
                    LVL.DESC = 'Br\CustCat\MG'
                    PMID = '|':CUST.B<17>:'||||':MAJ.PGROUP:'||':BRANCH ; GOSUB 1000
                    IF LVL.OK THEN GO 4000
                END
            END
        END
        IF PROD.B<112> # '' THEN
            LVL.DESC = 'CustCat\PG'
            PMID = '|':CUST.B<17>:'||||':PROD.B<112>:'||' ; GOSUB 1000
            IF LVL.OK THEN GO 4000
            IF MAJ.PGROUP # '' THEN
                LVL.DESC = 'CustCat\MG'
                PMID = '|':CUST.B<17>:'||||':MAJ.PGROUP:'||' ; GOSUB 1000
                IF LVL.OK THEN GO 4000
            END
        END
    END
    !
    ! PRICE LEVEL 9    (Customer/Product Line)
    !
    IF BRANCH # '' THEN
        IF TEMP.CNO = CNO THEN
            LVL.DESC = 'Br\Cust\PL'
        END ELSE LVL.DESC='Br\Mstr\PL'
        PMID = CNO:'|||':PROD.B<26>:'||||':BRANCH ; GOSUB 1000
        IF LVL.OK THEN CNO=TEMP.CNO ; GO 4000
    END
    IF TEMP.CNO = CNO THEN
        LVL.DESC = 'Cust\PL'
    END ELSE LVL.DESC='Mstr\PL'
    PMID = CNO:'|||':PROD.B<26>:'||||' ; GOSUB 1000
    IF LVL.OK THEN CNO=TEMP.CNO ; GO 4000
    !
    ! PRICE LEVEL 10   (Shipto Only)
    !
    IF BRANCH # '' THEN
        LVL.DESC = 'Br\Shipto'
        PMID = CNO:'||||':PSPTO:'|||':BRANCH ; GOSUB 1000
        IF LVL.OK THEN GO 4000
    END
    LVL.DESC = 'Shipto'
    PMID = CNO:'||||':PSPTO:'|||' ; GOSUB 1000
    IF LVL.OK THEN GO 4000
    !
    ! PRICE LEVEL 11   (Customer Only)
    !
    IF BRANCH # '' THEN
        LVL.DESC = 'Br\Customer'
        PMID = CNO:'|||||||':BRANCH ; GOSUB 1000
        IF LVL.OK THEN GO 4000
    END
    LVL.DESC = 'Customer'
    PMID = CNO:'|||||||' ; GOSUB 1000
    IF LVL.OK THEN GO 4000
    !
    ! Check for Parent Pricing Account
    !
    IF NOT(CHECK.MASTER.CNO) THEN
        IF CUST.B<104> # '' THEN CNO = CUST.B<104>
        CHECK.MASTER.CNO = TRUE
        GO 105
    END
    CNO = TEMP.CNO
    !
    ! PRICE LEVEL 12   (Contract/Product)
    !
    ALL.CONTRACTS = '' ; ACTR = 0
    CONTRACTS = DCOUNT(CUST.B<113>,CHAR(253))
    CONTR.CTR = 0
    LOOP
        CONTR.CTR = CONTR.CTR + 1
    WHILE CONTR.CTR LE CONTRACTS DO
        READV CONTR.NAME FROM F.PRICE.CONTRACT, CUST.B<113,CONTR.CTR>, 1 THEN
            LVL.OK = FALSE
            IF BRANCH # '' THEN
                LVL.DESC = 'Contract\Br\Prod'
                PMID = '||':PRDNO:'||||':CUST.B<113,CONTR.CTR>:'|':BRANCH ; GOSUB 1000
            END
            IF NOT(LVL.OK) THEN
                LVL.DESC = 'Contract\Prod'
                PMID = '||':PRDNO:'||||':CUST.B<113,CONTR.CTR>:'|' ; GOSUB 1000


            END
            IF LVL.OK THEN
                ACTR = ACTR + 1
                FOR I = 1 TO 10
                    ALL.CONTRACTS<I,ACTR> = XMISC<I>
                NEXT I
                ALL.CONTRACTS<11,ACTR> = CUST.B<113,CONTR.CTR>
                XMISC = ''
    *              GO 1500
            END
        END
    REPEAT



    IF ALL.CONTRACTS # "" THEN GO 1500
    !
    ! PRICE LEVEL 13   (Contract/Price Group/Product Line)
    !
    IF PROD.B<112> # '' & PROD.B<26> # '' THEN
        PGRP=PROD.B<112>
        FIRST.FLAG=0
1006    :    CONTR.CTR = 0
        LOOP
            CONTR.CTR = CONTR.CTR + 1
        WHILE CONTR.CTR LE CONTRACTS DO
            READV CONTR.NAME FROM F.PRICE.CONTRACT, CUST.B<113,CONTR.CTR>, 1 THEN
                LVL.OK = FALSE
                CONTRACT.OK = TRUE
                IF CUST.B<113,CONTR.CTR>[1,2] = 'R*' THEN
                    CONTRACT.PLN = OCONV(CUST.B<113,CONTR.CTR>,'G1*1')
                    CONTRACT.MC = OCONV(CUST.B<113,CONTR.CTR>,'G2*1')
                    IF PROD.B<26> = CONTRACT.PLN & PROD.B<137> = CONTRACT.MC ELSE CONTRACT.OK = FALSE
                END
                IF CONTRACT.OK THEN
                    IF BRANCH # '' THEN
                        LVL.DESC = 'Contract\Br\PGPL'
                        PMID = '|||':PROD.B<26>:'||':PGRP:'|':CUST.B<113,CONTR.CTR>:'|':BRANCH ; GOSUB 1000
                    END
                    IF NOT(LVL.OK) THEN
                        LVL.DESC = 'Contract\PGPL'
                        PMID = '|||':PROD.B<26>:'||':PGRP:'|':CUST.B<113,CONTR.CTR>:'|' ; GOSUB 1000
                    END
                    IF LVL.OK THEN
                        XMISC<2> = CHANGE(XMISC<2>,'PGPL\','PG=':PGRP:',PL=':PROD.B<26>:'\')
                        ACTR = ACTR + 1
                        FOR I = 1 TO 10
                            ALL.CONTRACTS<I,ACTR> = XMISC<I>
                        NEXT I
                        ALL.CONTRACTS<11,ACTR> = CUST.B<113,CONTR.CTR>
                        XMISC = ''
                    END
                END
            END
        REPEAT
        IF MAJ.PGROUP # '' THEN
            IF FIRST.FLAG = 0 THEN
                FIRST.FLAG=1 ; PGPR=MAJ.PGROUP ; GO 1006
            END
        END
    END
    !
    ! PRICE LEVEL 14   (Contract/Price Group)
    !
    PGRP=PROD.B<112>
    FIRST.FLAG=0
1007 : CONTR.CTR = 0
    LOOP
        CONTR.CTR = CONTR.CTR + 1
    WHILE CONTR.CTR LE CONTRACTS DO
        READV CONTR.NAME FROM F.PRICE.CONTRACT, CUST.B<113,CONTR.CTR>, 1 THEN
            LVL.OK = FALSE
            IF BRANCH # '' THEN
                LVL.DESC = 'Contract\Br\PG'
                PMID = '|||||':PGRP:'|':CUST.B<113,CONTR.CTR>:'|':BRANCH ; GOSUB 1000
            END
            IF NOT(LVL.OK) THEN
                LVL.DESC = 'Contract\PG'
                PMID = '|||||':PGRP:'|':CUST.B<113,CONTR.CTR>:'|' ; GOSUB 1000
            END
            IF LVL.OK THEN
                XMISC<2> = CHANGE(XMISC<2>,'PG\','PG=':PGRP:'\')
                ACTR = ACTR + 1
                FOR I = 1 TO 10
                    ALL.CONTRACTS<I,ACTR> = XMISC<I>
                NEXT I
                ALL.CONTRACTS<11,ACTR> = CUST.B<113,CONTR.CTR>
                XMISC = ''
            END
        END
    REPEAT
    IF MAJ.PGROUP # '' THEN
        IF FIRST.FLAG = 0 THEN
            FIRST.FLAG=1 ; PGPR=MAJ.PGROUP ; GO 1007
        END
    END
    !
    ! PRICE LEVEL 15   (Contract/Product Line)
    !
    CONTR.CTR = 0
    LOOP
        CONTR.CTR = CONTR.CTR + 1
    WHILE CONTR.CTR LE CONTRACTS DO
        READV CONTR.NAME FROM F.PRICE.CONTRACT, CUST.B<113,CONTR.CTR>, 1 THEN
            LVL.OK = FALSE
            CONTRACT.OK = TRUE
            IF CUST.B<113,CONTR.CTR>[1,2] = 'R*' THEN
                CONTRACT.PLN = OCONV(CUST.B<113,CONTR.CTR>,'G1*1')
                CONTRACT.MC = OCONV(CUST.B<113,CONTR.CTR>,'G2*1')
                IF PROD.B<26> = CONTRACT.PLN & PROD.B<137> = CONTRACT.MC ELSE CONTRACT.OK = FALSE
            END
            IF CONTRACT.OK THEN
                IF BRANCH # '' THEN
                    LVL.DESC = 'Contract\Br\PL'
                    PMID = '|||':PROD.B<26>:'|||':CUST.B<113,CONTR.CTR>:'|':BRANCH ; GOSUB 1000
                END
                IF NOT(LVL.OK) THEN
                    LVL.DESC = 'Contract\PL'
                    PMID = '|||':PROD.B<26>:'|||':CUST.B<113,CONTR.CTR>:'|' ; GOSUB 1000
                END
                IF LVL.OK THEN
                    XMISC<2> = CHANGE(XMISC<2>,'PL\','PL=':PROD.B<26>:'\')
                    ACTR = ACTR + 1
                    FOR I = 1 TO 10
                        ALL.CONTRACTS<I,ACTR> = XMISC<I>
                    NEXT I
                    ALL.CONTRACTS<11,ACTR> = CUST.B<113,CONTR.CTR>
                    XMISC = ''
                END
            END
        END
    REPEAT
    !
    ! If Contracts exist then find the lowest PRICE/COST Contract
    !
1500 : ! CONTRACTS ROUTINE
    CONTRACTS = DCOUNT(ALL.CONTRACTS<1>,CHAR(253))
    LOW.PRICE = '' ; FOUND = 0 ; ACTR = 0 ; NETFOUND = 0
    LOOP
        ACTR = ACTR + 1
    WHILE ACTR LE CONTRACTS & NOT(NETFOUND) DO
        CALC.PRICE = ALL.CONTRACTS<1,ACTR>
        IF LOW.PRICE = '' ! (LOW.PRICE GT CALC.PRICE) THEN
            LOW.PRICE = CALC.PRICE
            FOUND = ACTR
        END
    REPEAT
    
    
    
    
    
    
    
    
    
    
    
    IF FOUND THEN
        XMISC = ''
        FOR I = 1 TO 10
            XMISC<I> = ALL.CONTRACTS<I,FOUND>
        NEXT I
        CONTRACT.NBR = ALL.CONTRACTS<11,FOUND>
        MATRIX.FORMULA = XMISC<2>
        CONTRACT.NBR = 'C#':CONTRACT.NBR
        MATRIX.FORMULA = OCONV(MATRIX.FORMULA,'G1\99')
        MATRIX.FORMULA = CONTRACT.NBR:'\':MATRIX.FORMULA

        XMISC<2> = MATRIX.FORMULA
        GO 4000
    END
    !
    ! PRICE LEVEL 16   (Shipto Category/Product)
    !
    IF PSPTO # '' & PSPTO # JUNK THEN
        SPTO.CATG = OCONV(CNO:'!':PSPTO,'TSHIPTO;X;18;18')
        IF SPTO.CATG # '' THEN
            IF BRANCH # '' THEN
                LVL.DESC = 'Br\ShiptoCatg\Product'
                PMID = '|':SPTO.CATG:'|':PRDNO:'|||||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN GO 4000
            END
            LVL.DESC = 'ShiptoCatg\Product'
            PMID = '|':SPTO.CATG:'|':PRDNO:'|||||' ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
    END
    !
    ! PRICE LEVEL 17   (Shipto Category/Price Group/Product Line)
    !
    IF PROD.B<26> # '' & PROD.B<112> # '' & PSPTO # '' THEN
        IF PSPTO # '' & PSPTO # JUNK THEN
            SPTO.CATG = OCONV(CNO:'!':PSPTO,'TSHIPTO;X;18;18')
            IF SPTO.CATG # '' THEN
                IF BRANCH # '' THEN
                    LVL.DESC = 'Br\ShiptoCatg\PGPL'
                    PMID = '|':SPTO.CATG:'||':PROD.B<26>:'||':PROD.B<112>:'||':BRANCH ; GOSUB 1000
                    IF LVL.OK THEN GO 4000
                    IF MAJ.PGROUP # '' THEN
                        LVL.DESC = CHANGE(LVL.DESC,'PGPL','MG')
                        PMID = CNO:'|||':PROD.B<26>:'|':PSPTO:'|':MAJ.PGROUP:'||':BRANCH ; GOSUB 1000
                        IF LVL.OK THEN GO 4000
                    END
                END
                LVL.DESC = 'ShiptoCatg\PGPL'
                PMID = '|':SPTO.CATG:'||':PROD.B<26>:'||':PROD.B<112>:'||' ; GOSUB 1000
                IF LVL.OK THEN GO 4000
                IF MAJ.PGROUP # '' THEN
                    LVL.DESC = CHANGE(LVL.DESC,'PGPL','MG')
                    PMID = CNO:'|||':PROD.B<26>:'|':PSPTO:'|':MAJ.PGROUP:'||' ; GOSUB 1000
                    IF LVL.OK THEN GO 4000
                END
            END
        END
    END
    !
    ! PRICE LEVEL 18   (Shipto Category/Price Group)
    !
    IF PSPTO # '' & PSPTO # JUNK THEN
        SPTO.CATG = OCONV(CNO:'!':PSPTO,'TSHIPTO;X;18;18')
        IF SPTO.CATG # '' THEN
            IF BRANCH # '' THEN
                LVL.DESC = 'Br\ShiptoCatg\PG'
                IF PROD.B<112> # '' THEN
                    PMID = '|':SPTO.CATG:'||||':PROD.B<112>:'||':BRANCH ; GOSUB 1000
                    IF LVL.OK THEN GO 4000
                END
                IF MAJ.PGROUP # '' THEN
                    LVL.DESC = CHANGE(LVL.DESC,'\PG','\MG')
                    PMID = CNO:'||||':PSPTO:'|':MAJ.PGROUP:'||':BRANCH ; GOSUB 1000
                    IF LVL.OK THEN GO 4000
                END
            END
            LVL.DESC = 'ShiptoCatg\PG'
            IF PROD.B<112> # '' THEN
                PMID = '|':SPTO.CATG:'||||':PROD.B<112>:'||' ; GOSUB 1000
                IF LVL.OK THEN GO 4000
            END
            IF MAJ.PGROUP # '' THEN
                LVL.DESC = CHANGE(LVL.DESC,'\PG','\MG')
                PMID = CNO:'||||':PSPTO:'|':MAJ.PGROUP:'||' ; GOSUB 1000
                IF LVL.OK THEN GO 4000
            END
        END
    END
    !
    ! PRICE LEVEL 19   (Shipto Category/Product Line)
    !
    IF PSPTO # '' & PSPTO # JUNK THEN
        SPTO.CATG = OCONV(CNO:'!':PSPTO,'TSHIPTO;X;18;18')
        IF SPTO.CATG # '' THEN
            IF BRANCH # '' THEN
                LVL.DESC = 'Br\ShiptoCatg\PL'
                PMID = '|':SPTO.CATG:'||':PROD.B<26>:'||||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN GO 4000
            END
            LVL.DESC = 'ShiptoCatg\PL'
            PMID = '|':SPTO.CATG:'||':PROD.B<26>:'||||' ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
    END
    !
    ! PRICE LEVEL 20   (Customer Category/Product)
    !
    IF BRANCH # '' THEN
        LVL.DESC = 'Br\CustCat\Prod'
        PMID = '|':CUST.B<17>:'|':PRDNO:'|||||':BRANCH ; GOSUB 1000
        IF LVL.OK THEN GO 4000
    END
    LVL.DESC = 'CustCat\Prod'
    PMID = '|':CUST.B<17>:'|':PRDNO:'|||||' ; GOSUB 1000
    IF LVL.OK THEN GO 4000
    !
    ! PRICE LEVEL 21   (Customer Category/Price Group/Product Line)
    !
    IF PROD.B<112> # '' & PROD.B<26> # '' THEN
        IF BRANCH # '' THEN
            LVL.DESC = 'Br\CustCat\PGPL'
            PMID = '|':CUST.B<17>:'||':PROD.B<26>:'||':PROD.B<112>:'||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN GO 4000
            IF MAJ.PGROUP # '' THEN
                PMID = '|':CUST.B<17>:'||':PROD.B<26>:'||':MAJ.PGROUP:'||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN GO 4000
            END
        END
        LVL.DESC = 'CustCat\PGPL'
        PMID = '|':CUST.B<17>:'||':PROD.B<26>:'||':PROD.B<112>:'||' ; GOSUB 1000
        IF LVL.OK THEN GO 4000
        IF MAJ.PGROUP # '' THEN
            PMID = '|':CUST.B<17>:'||':PROD.B<26>:'||':MAJ.PGROUP:'||' ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
    END
    !
    ! PRICE LEVEL 22   (Customer Category/Price Group)
    !
    IF PROD.B<112> # '' THEN
        IF BRANCH # '' THEN
            LVL.DESC = 'Br\CustCat\PG'
            PMID = '|':CUST.B<17>:'||||':PROD.B<112>:'||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN GO 4000
            IF MAJ.PGROUP # '' THEN
                PMID = '|':CUST.B<17>:'||||':MAJ.PGROUP:'||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN GO 4000
            END
        END
        LVL.DESC = 'CustCat\PG'
        PMID = '|':CUST.B<17>:'||||':PROD.B<112>:'||' ; GOSUB 1000
        IF LVL.OK THEN GO 4000
        IF MAJ.PGROUP # '' THEN
            PMID = '|':CUST.B<17>:'||||':MAJ.PGROUP:'||' ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
    END
    !
    ! PRICE LEVEL 23   (Customer Category/Product Line)
    !
    IF CUST.B<17> # '' & PROD.B<26> # '' THEN
        IF BRANCH # '' THEN
            LVL.DESC = 'Br\CustCat\PL'
            PMID = '|':CUST.B<17>:'||':PROD.B<26>:'||||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
        LVL.DESC = 'CustCat\PL'
        PMID = '|':CUST.B<17>:'||':PROD.B<26>:'||||' ; GOSUB 1000
        IF LVL.OK THEN GO 4000
    END
    !
    ! PRICE LEVEL 1    (Product Only)
    !
    IF CPO = 'Y' THEN
        IF BRANCH # '' THEN
            LVL.DESC = 'Br\Product'
            PMID = '||':PRDNO:'|||||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
        LVL.DESC = 'Product'
        PMID = '||':PRDNO:'|||||' ; GOSUB 1000
        IF LVL.OK THEN GO 4000
    END
    !
    ! PRICE LEVEL 24   (Shipto Category Only)
    !
    IF PSPTO # '' & PSPTO # JUNK THEN
        SPTO.CATG = OCONV(CNO:'!':PSPTO,'TSHIPTO;X;18;18')
        IF SPTO.CATG # '' THEN
            IF BRANCH # '' THEN
                LVL.DESC = 'Br\ShiptoCat'
                PMID = '|':SPTO.CATG:'||||||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN GO 4000
            END
            LVL.DESC = 'ShiptoCat'
            PMID = '|':SPTO.CATG:'||||||' ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
    END
    !
    ! PRICE LEVEL 25   (Customer Category Only)
    !
    IF CUST.B<17> # '' THEN
        IF BRANCH # '' THEN
            LVL.DESC = 'Br\CustCat'
            PMID = '|':CUST.B<17>:'||||||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
        LVL.DESC = 'CustCat'
        PMID = '|':CUST.B<17>:'||||||' ; GOSUB 1000
        IF LVL.OK THEN GO 4000
    END
    !
    ! PRICE LEVEL 26   (Price Group/Product Line Only)
    !
    IF PROD.B<26> # '' & PROD.B<112> # '' THEN
        IF BRANCH # '' THEN
            LVL.DESC = 'Br\PGPL'
            PMID = '|||':PROD.B<26>:'||':PROD.B<112>:'||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN GO 4000
            IF MAJ.PGROUP # '' THEN
                PMID = '|||':PROD.B<26>:'||':MAJ.PGROUP:'||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN GO 4000
            END
        END
        LVL.DESC = 'PGPL'
        PMID = '|||':PROD.B<26>:'||':PROD.B<112>:'||' ; GOSUB 1000
        IF LVL.OK THEN GO 4000
        IF MAJ.PGROUP # '' THEN
            PMID = '|||':PROD.B<26>:'||':MAJ.PGROUP:'||' ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
    END
    !
    ! PRICE LEVEL 27   (Price Group Only)
    !
    IF PROD.B<112> # '' THEN
        IF BRANCH # '' THEN
            LVL.DESC = 'Br\PG'
            PMID = '|||||':PROD.B<112>:'||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN GO 4000
            IF MAJ.PGROUP # '' THEN
                PMID = '|||||':MAJ.PGROUP:'||':BRANCH ; GOSUB 1000
                IF LVL.OK THEN GO 4000
            END
        END
        LVL.DESC = 'PG'
        PMID = '|||||':PROD.B<112>:'||' ; GOSUB 1000
        IF LVL.OK THEN GO 4000
        IF MAJ.PGROUP # '' THEN
            PMID = '|||||':MAJ.PGROUP:'||' ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
    END
    !
    ! PRICE LEVEL 28   (Product Line Only)
    !
    IF PROD.B<26> # '' THEN
        IF BRANCH # '' THEN
            LVL.DESC = 'Br\PL'
            PMID = '|||':PROD.B<26>:'||||':BRANCH ; GOSUB 1000
            IF LVL.OK THEN GO 4000
        END
        LVL.DESC = 'PL'
        PMID = '|||':PROD.B<26>:'||||' ; GOSUB 1000
        IF LVL.OK THEN GO 4000
    END
    !
    ! PRICE/COST MATRIX NOT FOUND
    !
    IF MODE = 'PRICE' THEN
        LP.CLM = OCONV('LIST.PRICE.COLUMN','TCONTROL;X;1;1')
        IF LP.CLM = '' THEN LP.CLM = 'L1'
        BEGIN CASE
            CASE LP.CLM = 'L1' ; LP.ATTR = 13
            CASE LP.CLM = 'L2' ; LP.ATTR = 64
            CASE LP.CLM = 'L3' ; LP.ATTR = 66
            CASE LP.CLM = 'L4' ; LP.ATTR = 126
        END CASE
        XMISC = PROD.B<LP.ATTR>
        IF PR.RND.DEC # '' THEN
            TMP.PRICE = PROD.B<LP.ATTR>
            GOSUB 9000                   ; * CHECK FOR PRICE ROUNDING DECIMAL
            XMISC = TMP.PRICE
        END
        XMISC<2> = 'List Price Used !'
        XMISC<3> = 0
        XMISC<4> = XMISC<1> * PRICE.FACTOR
        XMISC<6> = LP.CLM
    END
    GO 4000
    !
    ! DETERMINE PRICE/COST
    !
1000 : LVL.OK = FALSE
    READ PM.B FROM F.PRICE.MATRIX, PMID THEN
        ACTIVE.PM = TRUE
        IF PM.B<5> # '' THEN IF PRICE.DATE GE PM.B<5> ELSE ACTIVE.PM = FALSE
        IF PM.B<6> # '' THEN IF PRICE.DATE LE PM.B<6> ELSE ACTIVE.PM = FALSE
        PM.CONTRACT = OCONV(PMID,'G6|1')
        IF PM.CONTRACT # '' THEN
            READ CONTR.B FROM F.PRICE.CONTRACT, PM.CONTRACT ELSE CONTR.B = ''
            IF CONTR.B<3> # '' THEN IF PRICE.DATE GE CONTR.B<3> ELSE ACTIVE.PM = FALSE
            IF CONTR.B<4> # '' THEN IF PRICE.DATE LE CONTR.B<4> ELSE ACTIVE.PM = FALSE
        END
        
        
        
        
        
        
        
        
        IF ACTIVE.PM THEN
            LVL.OK = TRUE
            QBRKS = DCOUNT(PM.B<10>,CHAR(253)) ; COUNT = 0 ; BRK.LEVEL = 0
            LOOP
                COUNT = COUNT + 1
            WHILE COUNT LE QBRKS AND NOT(BRK.LEVEL) DO
                IF INDEX(PM.B<10,COUNT>,'B',1) THEN
                    BQ = PROD.B<81>
                    IF BQ GT 1 ELSE BQ = 0
                    QUANTITY = BQ * CHANGE(PM.B<10,COUNT>,'B','')
                END ELSE QUANTITY = PM.B<10,COUNT>
                IF PM.B<22> = '$' THEN
                    IF ABS(DLLR) < QUANTITY THEN
                        IF QUANTITY + 0 = 0 THEN BRK.LEVEL = 1 ELSE
                            BRK.LEVEL = COUNT - 1
                        END
                    END
                END ELSE IF PM.B<22> = 'W' THEN
                    IF ABS(WGHT) < QUANTITY THEN
                        IF QUANTITY + 0 = 0 THEN BRK.LEVEL = 1 ELSE
                            BRK.LEVEL = COUNT - 1
                        END
                    END
                END ELSE
                    IF ABS(QNTY) < QUANTITY THEN
                        IF QUANTITY + 0 = 0 THEN BRK.LEVEL = 1 ELSE
                            BRK.LEVEL = COUNT - 1
                        END
                    END
                END
            REPEAT
            IF BRK.LEVEL = 0 THEN
                IF PM.B<22> = '$' THEN
                    IF ABS(DLLR) + 0 LE 0 THEN BRK.LEVEL = 1
                END ELSE IF PM.B<22> = 'W' THEN
                    IF ABS(WGHT) + 0 LE 0 THEN BRK.LEVEL = 1
                END ELSE
                    IF ABS(QNTY) + 0 LE 0 THEN BRK.LEVEL = 1
                END
                IF QBRKS GT 1 & INDEX(PM.B<10,QBRKS>,'B',1) & PROD.B<81> + 0 LE 1 THEN BRK.LEVEL = 1 ELSE BRK.LEVEL = QBRKS
            END
            IF QBRKS GT 1 THEN
                IF PM.B<19> = 'Y' THEN QB.MSG = 'MB' ELSE QB.MSG = 'B'
                IF PM.B<22> = '$' THEN QB.MSG = QB.MSG:'\$'
                IF PM.B<22> = 'W' THEN QB.MSG = QB.MSG:'\W'
                LVL.DESC = LVL.DESC:'\':QB.MSG:'\':PM.B<10,BRK.LEVEL>
            END
            PCD= PM.B<14>
            IF MODE = 'PRICE' THEN ATTR = 1 ELSE ATTR = 15
            CL = PM.B<ATTR,BRK.LEVEL>
            IF CL = '' THEN LVL.OK = FALSE ; RETURN
            PD = PM.B<ATTR+1,BRK.LEVEL>
            PM = PM.B<ATTR+2,BRK.LEVEL>
            FA = PM.B<ATTR+3,BRK.LEVEL>
            CD = PM.B<11,BRK.LEVEL>
            IF CL = 'N' THEN
                XMISC = PM.B<ATTR+3,BRK.LEVEL>
                XMISC<2> = LVL.DESC:' Net Price'
                XMISC<3> = 0
                XMISC<4> = PM.B<ATTR+3,BRK.LEVEL> * PRICE.FACTOR
                IF PR.RND.DEC # '' THEN
                    TMP.PRICE = XMISC<4>
                    GOSUB 9000             ; * CHECK FOR PRICE ROUNDING DECIMAL
                    XMISC<4> = TMP.PRICE
                END
                IF QBRKS GT 1 THEN XMISC<5> = PM.B<10>
                XMISC<6> = 'Net':OCONV(PM.B<ATTR+3,BRK.LEVEL>*PRICE.FACTOR,'MD2') 'R#7'
                RETURN
            END
            DESC = CL
            GOSUB 3000                   ; * DETERMINE PRICE/COST BASED ON PRICE DATE
            IF AMOUNT + 0 = 0 THEN
                IF MODE = 'PRICE' THEN
                    LP.CLM = OCONV('LIST.PRICE.COLUMN','TCONTROL;X;1;1')
                    IF LP.CLM = '' THEN LP.CLM = 'L1'
                    BEGIN CASE
                        CASE LP.CLM = 'L1' ; LP.ATTR = 13
                        CASE LP.CLM = 'L2' ; LP.ATTR = 64
                        CASE LP.CLM = 'L3' ; LP.ATTR = 66
                        CASE LP.CLM = 'L4' ; LP.ATTR = 126
                    END CASE
                    XMISC = PROD.B<LP.ATTR>
                    IF PR.RND.DEC # "" THEN
                        TMP.PRICE =XMISC
                        GOSUB 9000
                        XMISC = TMP.PRICE
                    END
                    XMISC<3> = 0
    *                 XMISC<4> = PROD.B<13> * PRICE.FACTOR
                    XMISC<4> = XMISC<1> * PRICE.FACTOR
                    DESC = DESC:' Missing !, ':LP.CLM:' - Used'
                    XMISC<2> = DESC
                    XMISC<6,BRK.LEVEL> = PM.B<ATTR,BRK.LEVEL>:' ':PM.B<ATTR+2,BRK.LEVEL>:OCONV(PM.B<ATTR+3,BRK.LEVEL>,'MD2') 'R#6':PM.B<ATTR+1,BRK.LEVEL>
                END
                RETURN
            END
            DESC = DESC:' ':PM:' '
            IF PD = '$' THEN
                DESC = DESC:'$':OCONV(FA,'MD2')
            END ELSE DESC = DESC:OCONV(FA,'MD2'):'%'
            IF PD = '$' THEN
                IF PM = '+' THEN PRICE = AMOUNT + (FA * 100) ELSE
                    PRICE = AMOUNT - (FA * 100)
                END
                IF PRICE + 0 LT 0 THEN PRICE = 0
            END ELSE
                IF AMOUNT + 0 = 0 ! FA + 0 = 0 THEN ADJAMT = 0 ELSE
                    PCNT = FA
                    ADJAMT = OCONV(AMOUNT,'MD4') * PCNT
                END
                IF PM = '+' THEN PRICE = AMOUNT + ADJAMT ELSE
                    PRICE = AMOUNT - ADJAMT
                END
            END
            IF MODE = 'PRICE' THEN
                IF ROUNDTO = 'N' ! ROUNDTO = 'D' THEN
                    RMISC = ROUNDTO ; RMISC<2> = 4
                    CALL ROUNDING.SUB (PRICE, RMISC)
                    PRICE = RMISC<3>
                END
            END
            IF AMOUNT + 0 = 0 ! PRICE + 0 = 0 THEN DISC = 0 ELSE
                DISC = (((AMOUNT - PRICE) / AMOUNT) * 10000)
            END
            IF MODE = 'PRICE' THEN
                CVDI = 'MD4':PRDEC
                CVDO = 'MD':PRDEC
                RNDAMT = 0
                IF ROUNDTO = 'P' & PRDEC = 2 THEN RNDAMT = ".0049"
                AMOUNT = ICONV(OCONV(ICONV(OCONV(AMOUNT,'MD4')+RNDAMT,CVDI),CVDO),'MD4')
                PRICE = ICONV(OCONV(ICONV(OCONV(PRICE,'MD4')+RNDAMT,CVDI),CVDO),'MD4')
            END ELSE
                AMOUNT = ICONV(OCONV(AMOUNT,'MD4'),'MD4')
                PRICE = ICONV(OCONV(PRICE,'MD4'),'MD4')
            END
    !!!    07/12/07  PRICE ROUNDING DECIMAL
    *           IF MODE = 'PRICE' THEN
    *              IF PR.RND.DEC # '' AND NUM(PR.RND.DEC) THEN
    *                 TMP.PRICE = PRICE
    *                 GOSUB 9000             ; * CHECK FOR PRICE ROUNDING DECIMAL
    *                 PRICE = TMP.PRICE
    *                 AMOUNT = TMP.PRICE
    *                 DISC = ''
    *              END
    *           END
            XMISC = PRICE
            XMISC<2> = LVL.DESC:' (':DESC:')'
            IF DISC + 0 LT 0 THEN DISC = 0 ; AMOUNT = PRICE
            XMISC<3> = DISC
            XMISC<4> = AMOUNT * PRICE.FACTOR
            IF QBRKS GT 1 THEN XMISC<5> = PM.B<10>
            XMISC<6,BRK.LEVEL> = PM.B<ATTR,BRK.LEVEL>:' ':PM.B<ATTR+2,BRK.LEVEL>:OCONV(PM.B<ATTR+3,BRK.LEVEL>,'MD2') 'R#6':PM.B<ATTR+1,BRK.LEVEL>
            XMISC<7> = CD
            XMISC<8> = FA
            XMISC<9> = PM.B<12>
        END
    END
    RETURN
    !
    ! DETERMINE PRICE/COST BASED ON PRICE DATE FROM HISTORY
    !
3000 : AMOUNT = ''
    IF PCD # '' THEN
        LOCATE CL IN CHANGE.B<1> SETTING CVM THEN
            HISTATTR = CHANGE.B<2,CVM>
            CURRATTR = CHANGE.B<3,CVM>
            PRICE.CHANGES = DCOUNT(PROD.B<128>,CHAR(253))
            PCTR = 0 ; FOUND = 0
            LOOP
                PCTR = PCTR + 1
            WHILE PCTR LE PRICE.CHANGES & NOT(FOUND) DO
                CHANGE.DATE = PROD.B<128,PCTR>
                PRIOR.CHANGE.DATE = PROD.B<128,PCTR+1>
                IF PRIOR.CHANGE.DATE = '' THEN PRIOR.CHANGE.DATE = CHANGE.DATE
                IF PCD LE CHANGE.DATE & PCD GE PRIOR.CHANGE.DATE THEN FOUND = PCTR
            REPEAT
            IF FOUND THEN
                IF PROD.B<HISTATTR,FOUND> # '' THEN
                    AMOUNT = PROD.B<HISTATTR,FOUND>
                END ELSE AMOUNT = PROD.B<CURRATTR,FOUND>
            END
            IF AMOUNT = '' THEN
                IF PRICE.CHANGES THEN
                    IF PCD LE PROD.B<128,PRICE.CHANGES> THEN
                        AMOUNT = PROD.B<HISTATTR,PRICE.CHANGES>
                    END
                END
            END
        END
    END
    IF AMOUNT = '' THEN
        BEGIN CASE
            CASE CL = 'C1' ; AMOUNT = PROD.B<3>
            CASE CL = 'C2' ; AMOUNT = PROD.B<5>
            CASE CL = 'C3' ; AMOUNT = PROD.B<19>
            CASE CL = 'C4'
                AMOUNT = PROD.B<62>
                IF INDEX(CNO,'PRICE.CHANGE',1) THEN
                    READV AMOUNT FROM F.PRICE.CHANGE, PRDNO, 3 ELSE AMOUNT = ''
                END
            CASE CL = 'C5' ; AMOUNT = PROD.B<120>
            CASE CL = 'C6' ; AMOUNT = PROD.B<122>
            CASE CL = 'C7' ; AMOUNT = PROD.B<124>
            CASE CL = 'CC'
                BEGIN CASE
                    CASE PRICE.B<21> # '' ; AMOUNT = PRICE.B<21>
                    CASE CSTFLD = 'C1' ; AMOUNT = PROD.B<3> * PRICE.FACTOR
                    CASE CSTFLD = 'C2' ; AMOUNT = PROD.B<5> * PRICE.FACTOR
                    CASE CSTFLD = 'C3' ; AMOUNT = PROD.B<19> * PRICE.FACTOR
                    CASE CSTFLD = 'C4' ; AMOUNT = PROD.B<62> * PRICE.FACTOR
                    CASE CSTFLD = 'C5' ; AMOUNT = PROD.B<120> * PRICE.FACTOR
                    CASE CSTFLD = 'C6' ; AMOUNT = PROD.B<122> * PRICE.FACTOR
                    CASE CSTFLD = 'C7' ; AMOUNT = PROD.B<124> * PRICE.FACTOR
                END CASE
            CASE CL = 'L1'
                AMOUNT = PROD.B<13>
                IF INDEX(CNO,'PRICE.CHANGE',1) THEN
                    READV AMOUNT FROM F.PRICE.CHANGE, PRDNO, 2 ELSE AMOUNT = ''
                END
            CASE CL = 'L2' ; AMOUNT = PROD.B<64>
            CASE CL = 'L3' ; AMOUNT = PROD.B<66>
            CASE CL = 'L4' ; AMOUNT = PROD.B<126>
        END CASE
    END
    IF PR.RND.DEC # '' THEN
        TMP.PRICE = AMOUNT
        GOSUB 9000                      ; * CHECK FOR PRICE ROUNDING DECIMAL
        AMOUNT = TMP.PRICE
    END
    RETURN
    !
    ! DETERMINE MODE
    !
4000 : IF MODE = 'PRICE' THEN
        FOR I = 1 TO 20
            PRICE.B<I> = XMISC<I>
        NEXT I
        XMISC = PRICE.B
    END ELSE
        PRICE.B = ''
        FOR I = 1 TO 20
            PRICE.B<I+20> = XMISC<I>
        NEXT I
        IF XMISC<21> # '' THEN
            XMISC<21> = XMISC<21> * PRICE.FACTOR
        END
        MODE = 'PRICE'
        GO 1
    END
   
    RETURN
    
    
    
    
    
    
    
    
    
9000: ! PRICE ROUNDING DECIMAL
    !!!!  PRICE ROUNDING DECIMAL   07/11/07
    TMP.PRICE = OCONV(TMP.PRICE,'MD4')
    TMP.INT = FIELD(TMP.PRICE,'.',1)
    TMP.DEC = FIELD(TMP.PRICE,'.',2)
    IF TMP.DEC > PR.RND.DEC THEN TMP.INT = TMP.INT + 1
    TMP.PRICE = TMP.INT:".":PR.RND.DEC
    TMP.PRICE = ICONV(TMP.PRICE,'MD4')
    RETURN
    ***
    END
