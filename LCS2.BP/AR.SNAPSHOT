     SUBROUTINE AR.SNAPSHOT (ERRORS, F.WOI, F.WAR, XMISC)
     !
     !
     !   XMISC<1> = As of A/R Date
     !   XMISC<2> = Customer Number
     !   XMISC<3> = Age Monthly? (Y)es/(N)o
     !   XMISC<4> = Cutoff Day
     !   XMISC<5> = Current Sales Period
     !   XMISC<6> = 'Y' If Temporary work files are created.
     !               This is returned to calling program so that they could
     !               be deleted.
     !
     !
     !-----------------------------------------------------------------------
     INCLUDE UTLSBP ASSIGN.VAR
     !
     ERRORS = ''; F.WAR = ''; F.WOI = ''
     !
     SNAPSHOT = TRUE
     IF XMISC<1> GE DATE() THEN SNAPSHOT = FALSE ELSE
          IF XMISC<3> = 'Y' THEN
               MONTH.OK = FALSE; MON = XMISC<5>[1,2]; DAY = XMISC<4>; YEAR = XMISC<5>[3,2]
               MONTH.END.DATE = ''
               CTR = DAY + 1
               LOOP
                    CTR = CTR - 1
               WHILE NOT(MONTH.OK) & CTR GT 0 DO
                    MONTH.END.DATE = MON:'/':CTR:'/':YEAR
                    MONTH.END.DATE = ICONV(MONTH.END.DATE,'D')
                    IF MONTH.END.DATE # '' THEN MONTH.OK = TRUE
               REPEAT
               IF XMISC<1> LT MONTH.END.DATE ELSE SNAPSHOT = FALSE
               IF DATE() GT XMISC<1> THEN SNAPSHOT = TRUE
          END
     END
     IF NOT(SNAPSHOT) THEN
          XMISC<6> = 'N'
          OPEN 'OPEN-ITEMS' TO F.WOI ELSE
               ERRORS<-1> = "Can't Open OPEN-ITEMS file!"
          END
          OPEN 'CUST.AR' TO F.WAR ELSE
               ERRORS<-1> = "Can't Open CUST.AR file!"
          END
          IF ERRORS # '' THEN
               ERRORS = CHANGE(ERRORS,IAMC,CRLF)
               RETURN
          END
     END ELSE
          *XMISC<6> = 'Y'
          OPEN 'PAID.OI' TO F.PAID.OI ELSE
               ERRORS<-1> = "Can't Open PAID.OI file!"
          END
          OPEN 'OPEN-ITEMS' TO F.OI ELSE
               ERRORS<-1> = "Can't Open OPEN-ITEMS file!"
          END
          OPEN 'CUST.AR' TO F.CUST.AR ELSE
               ERRORS<-1> = "Can't Open CUST.AR file!"
          END
          OPEN 'INVOICE' TO F.INVOICE ELSE
               ERRORS<-1> = "Can't Open INVOICE file!"
          END
          IF ERRORS THEN
               ERRORS = CHANGE(ERRORS,IAMC,CRLF)
               RETURN
          END
          FN = 'CUST.AR.SNAPSHOT/':PORT
          FN1 = FN
          EXECUTE 'CREATE-FILE ':FN:' 1,1 101,1' CAPTURING OUTPUT
          OPEN FN TO F.WAR ELSE
               ERRORS<-1> = "Can't Open file ":FN
               RETURN
          END
          FN = 'OI.SNAPSHOT/':PORT
          EXECUTE 'CREATE-FILE ':FN:' 1,1 1001,1' CAPTURING OUTPUT
          OPEN FN TO F.WOI ELSE
               ERRORS<-1> = "Can't Open file ":FN
               RETURN
          END
          FN2=FN
     !
          AS.OF.DATE = XMISC<1>
          CNO = XMISC<2>
     !
          IF CNO # '' THEN
               READ CAR.B FROM F.CUST.AR, CNO ELSE CAR.B = ''
               OILIST  = CHANGE(CAR.B<2>,IVM,IAMC)
               POILIST = CHANGE(CAR.B<3>,IVM,IAMC)
          END ELSE
               EXECUTE 'SSELECT OPEN-ITEMS BY CUST# BY INV.DATE' CAPTURING OUTPUT
               READLIST OILIST ELSE OILIST = ''
               EXECUTE 'SSELECT PAID.OI BY CUST# BY INV.DATE' CAPTURING OUTPUT
               READLIST POILIST ELSE POILIST = ''
          END
     !
          CMPLIST = OILIST:IAMC:POILIST
          IDCNT = DCOUNT(CMPLIST,IAMC); CTR = 0
          LOOP
               CTR = CTR + 1
          WHILE CTR LE IDCNT DO
               ID = CMPLIST<CTR>
               READ OI.B FROM F.OI, ID THEN GOSUB 1000 ELSE
                    READ OI.B FROM F.PAID.OI, ID THEN
                         READV VOIDED FROM F.INVOICE, ID, 87 ELSE VOIDED = ''
                         IF VOIDED # 'Y' THEN
                              IF OI.B<5> = '' THEN
                                   OI.B<5> = OI.B<2>
                                   OI.B<4> = OI.B<3>
                              END
                              IF ID[1,1] = 'P' THEN
                                   PAID = SUM(OI.B<4>) + SUM(OI.B<6>) + SUM(OI.B<7>)
                                   IF PAID + 0 # OI.B<3> THEN OI.B<3> = PAID
                              END
                              GOSUB 1000
                         END
                    END
               END
          REPEAT
     END

     ! RETURN TO CALLING PROGRAM
     DOS.CMD = "DELETE.FILE ":FN1
     PRINT DOS.CMD
     EXECUTE DOS.CMD CAPTURING CAP
     PRINT CAP
     DOS.CMD = "DELETE.FILE ":FN2
     PRINT DOS.CMD
     EXECUTE DOS.CMD CAPTURING CAP
     PRINT CAP
     DOS.CMD = "CNAME ":FN2:" TO OI.SNAPZ"
     PRINT DOS.CMD
     EXECUTE DOS.CMD CAPTURING CAP
     PRINT CAP
     DOS.CMD = "CNAME ":FN1:" TO CUST.AR.SNAPZ"
     PRINT DOS.CMD
     EXECUTE DOS.CMD CAPTURING CAP
     PRINT CAP
     !
     RETURN
     !
     ! DETERMINE IF RECORD IS PART OF THE SNAPSHOT
     !
1000 IF OI.B<2> LE AS.OF.DATE THEN
          PAYMENTS = DCOUNT(OI.B<5>,IVM); PCTR = 0; LAST.DATE.PAID = ''
          LOOP
               PCTR = PCTR + 1
          WHILE PCTR LE PAYMENTS DO
               IF OI.B<5,PCTR> GT AS.OF.DATE THEN
                    DEL OI.B<4,PCTR>
                    DEL OI.B<5,PCTR>
                    DEL OI.B<6,PCTR>
                    DEL OI.B<7,PCTR>
                    DEL OI.B<24,PCTR>
                    PAIDOFF = FALSE
                    PCTR = PCTR - 1; PAYMENTS = PAYMENTS - 1
               END ELSE
                    IF LAST.DATE.PAID = '' THEN LAST.DATE.PAID = OI.B<5,PCTR>
                    IF LAST.DATE.PAID LT OI.B<5,PCTR> THEN LAST.DATE.PAID = OI.B<5,PCTR>
               END
          REPEAT
          BALANCE = OI.B<3> - (SUM(OI.B<4>) + SUM(OI.B<6>) + SUM(OI.B<7>))
     !
          READ CAR.B FROM F.WAR, OI.B<1> ELSE CAR.B = ''
          IF BALANCE + 0 # 0 THEN
               CAR.B<1> = CAR.B<1> + BALANCE
               CAR.B<2,-1> = ID
               WRITE OI.B ON F.WOI, ID
          END ELSE
               INS ID BEFORE CAR.B<3,1>
               INS OI.B<2> BEFORE CAR.B<4,1>
               INS OI.B<3> BEFORE CAR.B<5,1>
               INS LAST.DATE.PAID BEFORE CAR.B<6,1>
          END
          WRITE CAR.B ON F.WAR, OI.B<1>
     END
     RETURN
