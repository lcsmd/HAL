      OPEN 'TRANS' TO TRANS ELSE PRINT 'TRANS';STOP
      OPEN 'LCS.CONTROL' TO LCS.CONTROL ELSE PRINT 'LCS.CONTROL';STOP
      OPEN 'RULES' TO RULES ELSE PRINT 'RULES';STOP
      OPEN 'ACCOUNTS' TO ACCOUNTS ELSE PRINT 'ACCOUNTS';STOP
      INCLUDE FILE.EQU TRANS.EQ
      INCLUDE FILE.EQU RULES.EQ
      PROMPT ''
      READ NEXT.RULE.ID FROM LCS.CONTROL,"NEXT.RULE.ID" ELSE NEXT.RULE.ID = "5001"
      LOOP WHILE READV EXISTS FROM RULES,NEXT.RULE.ID,0 DO
         NEXT.RULE.ID +=1
      REPEAT
      LABS = "KEYWORD,NAME,ACCOUNT.ID,ACCOUNT NAME,CLASS,DEPT,OK"
      LABL = "20,20,15,40,10,10,2"
      LABD = ",,,,Private,,Y"
      LABS = CHANGE(LABS,',',@FM)
      LABL = CHANGE(LABL,',',@FM)
      LABD = CHANGE(LABD,',',@FM)
      LABC = DCOUNT(LABL,@FM)
      LAB= ""
      LABLINE = ""
      FOR LABX = 1 TO LABC
         LAB<LABX>=LABS<LABX>:SPACE(LABL<LABX>-LEN(LABS<LABX>))
         LABLINE :=LAB<LABX>
      NEXT LABX
      LM = 1
      CRLF = CHAR(13):CHAR(10)
      FINISHED = @FALSE
      FILTER.MODE = 4
      ESC = CHAR(27)
      FILTER = ""
      SORTBY = 'TR.DESC'
      A.TRANS.POINT = 1
      FILTER<1,1> = ' ';FILTER<1,2> = 'NO FILTER'
      FILTER<2,1> = ' WITH RULE = "" ';FILTER<2,2> = 'NO RULE'
      FILTER<3,1> = ' WITH NAME = "" ';FILTER<3,2> = 'UNNAMED'
      FILTER<4,1> = ' WITH ACCOUNT.ID = "" ';FILTER<4,2> = 'UNCLASSIFIED'


      ITEM.CT = 0
      ITEM.CT += 1
      SCREEN.WIDTH = 190
      SCREEN.LENGTH = 48
      DISPLAY.ROWS = 40
      COMMAND.LOC= DISPLAY.ROWS+5
      GOSUB WYSE.WIDE:
      HEADER.LINE = ""
      GOSUB REFRESH.LIST:

MAIN: LOOP

      WHILE NOT(FINISHED) DO
         GOSUB SHOW.TRANS:
         GOSUB GET.COMMAND:
      REPEAT
      GOSUB RESET.SESSION:
      PRINT ITEM.CT :" ITEMS PROCESSED"

      STOP

GET.COMMAND:
      PRINT STR("-",80)
      PRINT " (.) Back (/) Forward, (F)ilter (T)opm (A)mt (Q)uit:":;INPUT COMMAND,1:
      COMMAND = UPCASE(COMMAND)
      BEGIN CASE
         CASE COMMAND = "/"
            A.TRANS.POINT = A.TRANS.POINT + DISPLAY.ROWS
            IF A.TRANS.POINT > A.KEY.CT THEN A.TRANS.POINT = 1
            GOSUB SHOW.TRANS:
         CASE COMMAND = "."
            A.TRANS.POINT = A.TRANS.POINT - DISPLAY.ROWS
            IF A.TRANS.POINT < 1 THEN A.TRANS.POINT = A.KEY.CT-DISPLAY.ROWS
            GOSUB SHOW.TRANS:
         CASE COMMAND = ""
            GOSUB GET.RULE:
         CASE COMMAND = "Q"
            FINISHED = @TRUE
         CASE COMMAND = "F"
            FILTER.MODE += 1
            IF FILTER.MODE > 4 THEN FILTER.MODE = 1
            GOSUB REFRESH.LIST:
         CASE COMMAND = "T"
            GOSUB REFRESH.LIST:
         CASE COMMAND = "A"
            SORTBY="AMOUNT"
            GOSUB REFRESH.LIST:
         CASE COMMAND = "D"
            SORTBY = "TR.DESC"
            GOSUB REFRESH.LIST:
      END CASE
      RETURN

GET.RULE:
      RULE.EXP = ""
      RULE.NUM = ""
      YPOS = COMMAND.LOC+2
      PRINT @(1,45):LABLINE:
      DATAL=""
      DLX=1
      RULE.EXP = ""
      XPOS=1
      *INPUT @(x, y) {,} {:} var {, length {, fill}} {_} {:} {format} {modes}
      IF LABD<DLX> THEN DATAL<DLX> = LABD<DLX>
      KEYWD=DATAL<DLX>
      LEN.IN = LABL<DLX>
      INPUT @(XPOS,YPOS):KEYWD,LEN.IN,"*":
      DATAL<DLX>=KEYWD
      IF DATAL<1>[1,2] = "X " THEN
         RULE.EXP = UPCASE(TRIM(DATAL<1>[3,25]))
         DATAL<1> = ""
      END
      IF DATAL<DLX> <> "" THEN
         FOR DLX = 2 TO LABC
            IF LABD<DLX> THEN DATAL<DLX> = LABD<DLX>
            IF DLX = 2 THEN DATAL<DLX> = DATAL<DLX-1>
            KEYWD=DATAL<DLX>
            LEN.IN = LABL<DLX>
            IF LABS<DLX> = "ACCOUNT NAME" THEN
               PRINT @(XPOS,YPOS):DATAL<DLX-1> "L":LABL<DLX-1>:@(XPOS+LABL<DLX>,YPOS):ACCOUNT.NAME:SPACE(LABL<DLX>-LEN(ACCOUNT.NAME)) "L":LABL<DLX>:

            END ELSE
               XPOS+=LABL<DLX-1>
               INPUT @(XPOS,YPOS):KEYWD,LEN.IN,"*":
               DATAL<DLX>=KEYWD
               IF LABS<DLX> = "ACCOUNT.ID" THEN
                  ACC.KEY = KEYWD
                  SELECT.STR = 'SSELECT ACCOUNTS WITH NAME LIKE "...':ACC.KEY:'..."'
                  EXECUTE SELECT.STR CAPTURING CAP
                  READLIST ACCTS.ID THEN
                     ACCT.CT = DCOUNT(ACCTS.ID,@FM)
                     IF ACCT.CT=1 THEN
                        DATAL<DLX>=ACCTS.ID<1>
                     END ELSE
                        PTR = 1
                        LEN.IN = LABL<DLX>
                        DATAL<DLX>=""
                     END
                     LOOP WHILE NOT(DATAL<DLX>) DO
                        IF PTR > ACCT.CT THEN PTR =1
                        ACCT.ID = ACCTS.ID<PTR>
                        READV ACCOUNT.NAME FROM ACCOUNTS,ACCT.ID,1 ELSE ACCOUNT.NAME = ""
                        PRINT @(XPOS+LABL<DLX>,YPOS):ACCOUNT.NAME:SPACE(LABL<DLX>-LEN(ACCOUNT.NAME)):
                        INPUT TRIGGER,1:
                        IF TRIGGER = " " THEN
                           PTR+=1
                        END ELSE
                           IF TRIGGER = "" THEN
                              DATAL<DLX>=KEYWD
                           END ELSE
                              KEYWD=TRIGGER
                              INPUT @(XPOS,YPOS):KEYWD,LEN.IN,"*": APPEND
                              DATAL<DLX>=KEYWD
                           END
                        END
                     REPEAT
                     READV ACCOUNT.NAME FROM ACCOUNTS,DATAL<DLX>,1 ELSE ACCOUNT.NAME = ""
                     PRINT @(XPOS+LABL<DLX>,YPOS):ACCOUNT.NAME:SPACE(LABL<DLX>-LEN(ACCOUNT.NAME)):
                  END
               END
            END
         NEXT DLX
      END
      RULE.NUM = NEXT.RULE.ID
      RU.B = ""
      RU.KEYWORD = DATAL<1>
      RU.ACCT.ID = DATAL<3>
      RU.DEPT = DATAL<5>
      RU.PAYEE.NAME = DATAL<2>
      WRITE RU.B ON RULES,RULE.NUM
      NEXT.RULE.ID +=1
      WRITE NEXT.RULE.ID ON LCS.CONTROL,"NEXT.RULE.ID"
      CALL RUN.RULE(RULES,TRANS,RULE.EXP,RULE.NUM)
EXIT: GOSUB REFRESH.LIST:
      RETURN

SHOW.TRANS:
      A.TRANS.END = A.TRANS.POINT + DISPLAY.ROWS
      STATUS.LINE = "*** MODE: ":FILTER<FILTER.MODE,2>:"  UNCATEGORIZED: ":A.KEY.CT:"      LINE #:":A.TRANS.POINT:"-":A.TRANS.END
      GOSUB BUILD.KEY.BLOCK:
      SELECT.STR = "LIST TRANS ":KEY.BLOCK:" @1 HDR.SUP COUNT.SUP"
      WRITE SELECT.STR ON LCS.CONTROL,"SELECT"
      IF LM THEN PRINT SELECT.STR
      EXECUTE SELECT.STR CAPTURING SCREEN
      CONVERT ESC TO "" IN SCREEN
      HEADER.LINE = SCREEN<1>[5,190]
      DEL SCREEN<1>
      DEL SCREEN<2>
      SCREEN = CHANGE(SCREEN,@FM,CRLF)
      PRINT @(-1):STATUS.LINE
      PRINT HEADER.LINE
      PRINT SCREEN
      RETURN

REFRESH.LIST:

      SELECT.STR = 'SSELECT TRANS ':FILTER<FILTER.MODE,1>:'BY TR.TYPE BY ':SORTBY
      IF LM THEN PRINT SELECT.STR
      EXECUTE SELECT.STR CAPTURING CAP
      A.KEY.CT = FIELD(CAP," ",1)
      IF A.KEY.CT > 0 THEN
         READLIST A.KEYS ELSE A.KEYS = ""
         A.TRANS.POINT = 1
      END ELSE
         PRINT " NO RECORDS FOUND WITH ":FILTER<FILTER.MODE,1>
         SLEEP 5
      END
      RETURN

      !
      ! WYSE WIDE CHARATER SET
      !
WYSE.WIDE: *

      ************
SET.SESSION:
      ************
      SET.CMND = "AT.SS":@FM
      SET.CMND:="DATA SET":@FM
      SET.CMND:="DATA ,,1,1,"
      IF SCREEN.WIDTH GE 82 THEN
         SET.CMND:=SCREEN.WIDTH:",":SCREEN.LENGTH:",,,1"
      END ELSE
         SET.CMND:=",,":SCREEN.WIDTH:",":SCREEN.LENGTH:",0"
      END
      EXECUTE SET.CMND
      RETURN
      ***************
RESET.SESSION:
      **************
      EXECUTE "AT.SS":@FM:"DATA RESET":@FM:"DATA "
      RETURN
      *
BUILD.KEY.BLOCK:
      KEY.BLOCK = ""
      FOR BLOCK.X = A.TRANS.POINT TO A.TRANS.END
         KEY.BLOCK := A.KEYS<BLOCK.X>:" "
      NEXT BLOCK.X
      RETURN

      *TEST.BLOCK:
      *A.TRANS.POINT = 1
      *A.TRANS.END = 40
      *EXECUTE "SELECT TRANS WITH TR.TYPE = AMERICAN"
      *READLIST A.KEYS THEN
      *KEY.CT=DCOUNT(A.KEYS,@FM)
      *KEY.BLOCK = ""
      *FOR X = 1 TO KEY.CT
      *KEY.BLOCK = A.KEYS<X>:" "
      *UNTIL X = KEY.CT
      *NEXT X
      *END
      *
      *
      *
      *
      *A.TRANS.POINT = 1
      *A.TRANS.START= 1
      *A.TRANS.END = 40
      *

