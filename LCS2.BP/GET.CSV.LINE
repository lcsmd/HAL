SUBROUTINE GET.CSV.LINE(S,A,B)

*   Lucian Pata

*  S : Position, starts at 1
*  A : The entire CSV file
*  B : Next line
*Use this routine for CSV files that may have CR:LF as part of the data. 
*The routine extracts a CSV line for further processing. 
*After each extraction the start position S is incremented to point to the next line. 
*At the end of file, S is set to 0. 
*Example: 
*
  *READ CSV ...          ;* CSV file
*
  *START = 1
  *LOOP
  *WHILE START DO
    *CALL GET.CSV.LINE(START,CSV,LINE)
    *CALL CONVERT.FROM.CSV(LINE,ARRAY)
    *...
    *process ARRAY
    *...
  *REPEAT

* For CSV files that have CR:LF as part of the data.
* Returns the CSV line that starts at position S.
* S is updated to point to the next line.
* On EOF, S is set to 0.

EQU CR TO CHAR(13)      ;* Carriage Return
EQU LF TO CHAR(10)      ;* Line Feed

  B = ''
  L = LEN(A)

  FOR I = 1 TO L
    POS = INDEX(A[S,L],CR:LF,I)
    IF POS ELSE
      B = A[S,L]
      EXIT
    END
    K = COUNT(A[S,POS],'"')
    IF MOD(K,2) ELSE
      B = A[S,POS-1]
      EXIT
    END
  NEXT I

  S += LEN(B) + 2       ;* 2 for CR:LF
  IF S > L THEN S = 0

  RETURN
END

