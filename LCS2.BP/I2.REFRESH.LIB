     SUBROUTINE I2.REFRESH.LIB

     NEXT.UPDATE = ""
     UPDATE.OPTIONS.IN = "REFRESH"
     UPDATE.OPTIONS.IN<2> = "LP"
     UPDATE.OPTIONS.IN<3> = FIELD(@SENTENCE," ",2)
     PRINT @(-1):
     DEFFUN EXTRACT.FIELD(BUFF,LABEL)
     DEFFUN CONVERT.ROLLUP.GROUP(PRICE.GROUP,F.PRICE.GROUP)
     INCLUDE UTLSBP ASSIGN.VAR
     LM=0                              ; * SET LIVE MONITORING 0=OFF 1=ON
     DM=0

     **I2.PRICE.UPDATE.LOG
     EQU UPDATE.TYPE.P TO XMISC<1>, UPDATEID TO XMISC<2>, USER TO XMISC<3>, UPDATE.NUM TO XMISC<4>
     EQU UPDATE.CODE TO XMISC<5>,  UPDATE.NAME TO XMISC<6>
     EQU NOTE TO XMISC<7>, MAST.EFFDATE TO XMISC<8>, MAST.EXPDATE TO XMISC<9>, UPDATE.MODE TO XMISC<10>
     EQU UPDATE.SCOPE TO XMISC<11>
     EQU FILE.NAME TO XMISC<12,1>, FILE.PATH TO XMISC<12,2>, ITEM.NAME TO XMISC<13>
     EQU DELIM TO XMISC<14>
     EQU COL.LABELS TO XMISC<15>
     EQU COL.VALUES TO XMISC<16>
     *EQU UPC.COL TO XMISC<16,1>, CAT.COL TO XMISC<16,2>, EFFDATE.COL TO XMISC<16,3>,COST.COL TO XMISC<16,4>, UOM.COL TO XMISC<16,5>
     EQU HEADER.LABELS TO XMISC<17>
     EQU UPC.FORMAT TO XMISC<18,1>, DATA.START TO XMISC<18,2>
     EQU CUSTOM.SUB TO XMISC<19>
     EQU MAST.UOM TO XMISC<20>
     EQU RUN.COUNT TO XMISC<21>
     EQU V.NUM TO XMISC<22>, V.NAME TO XMISC<23>, V.NEW TO XMISC<24>
     EQU V.UPDATE TO XMISC<25>, V.DELETE TO XMISC<26>, V.ZAP TO XMISC<27>
     EQU V.TOTAL TO XMISC<28>
     EQU T.NEW TO XMISC<29>, T.UPDATE TO XMISC<30>, T.DELETE TO XMISC<31>
     EQU T.ZAPPED TO XMISC<32>, T.TOTAL TO XMISC<33>

     ** MAP LINE LABELS AND VALUES
     *UOM:            UOM.COL
     *UPC:            UPC.COL
     *CAT.NUM:        CAT.COL
     *EFF.DATE:       EFFDATE.COL
     *COST:           COST.COL
     *VEND.NUM:       V.NUM
     *VEND.NAME:      V.NAME
     *VEND.CODE:      UPDATE.NUM
     *MAST.UOM:       MAST.UOM
     *CUSTOM.SUB:     CUSTOM.SUB
     *UPC.FORMAT:     UPC.FORMAT
     *DELIM:          DELIM
     *SCOPE:          UPDATE.SCOPE
     *MODE:           UPDATE.MODE
     *FILE.NAME:      FILE.NAME
     *FILE.PATH:      FILE.PATH
     *ITEM.NAME:      ITEM.NAME
     *DATA.START:     DATA.START
     *MAST.EFFDATE    MAST.EFFDATE
     *MAST.EXPDATE    MAST.EXPDATE

     *** DETAIL OF NEXT.PRICE.UPDATE
     EQU NEXT.TRADE.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,1>
     EQU NEXT.VENDOR.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,2>
     EQU NEXT.MANUAL.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,3>
     EQU NEXT.REFRESH.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,4>

     **** CUSTOM.PRICES - START OF EQU section
     EQU CUS.UP.DATE.H TO 1, CUS.UP.DATE TO CUS.B<1>
     EQU CUS.SOURCE.NUM.H TO 10, CUS.SOURCE.NUM TO CUS.B<10>
     EQU CUS.SOURCE.NAME.H TO 11, CUS.SOURCE.NAME TO CUS.B<11>
     EQU CUS.C4.COST.DT.H TO 12, CUS.C4.COST.DT TO CUS.B<12>
     EQU CUS.NET.COST.ROLLUP.H TO 15, CUS.NET.COST.ROLLUP TO CUS.B<15>
     EQU CUS.TYPE.H TO 2, CUS.TYPE TO CUS.B<2>
     EQU CUS.EFF.DATE.H TO 3, CUS.EFF.DATE TO CUS.B<3>
     EQU CUS.EXP.DATE.H TO 4, CUS.EXP.DATE TO CUS.B<4>
     EQU CUS.TS.COST.H TO 5, CUS.TS.COST TO CUS.B<5>
     EQU CUS.VEND.COST.H TO 6, CUS.VEND.COST TO CUS.B<6>
     EQU CUS.MAN.COST.H TO 7, CUS.MAN.COST TO CUS.B<7>
     EQU CUS.C4.COST.H TO 8, CUS.C4.COST TO CUS.B<8>
     EQU CUS.COST.UOM.H TO 9, CUS.COST.UOM TO CUS.B<9>
     ****CUSTOM.PRICES - END of EQU section



     OPEN 'CONTROL' TO F.CONTROL ELSE PRINT 'CONTROL' ; STOP
     OPEN 'TRADESERVICE,ARCHIVE' TO F.I2.ARCHIVE ELSE PRINT 'TRADESERVICE,ARCHIVE';STOP
     OPEN 'LCS.CONTROL' TO LCS.CONTROL ELSE PRINT 'LCS.CONTROL' ; STOP
     READV APPLY.CUSTOM.COSTS.TRADE FROM LCS.CONTROL,"APPLY.CUSTOM.COSTS.TRADE",1 ELSE APPLY.CUSTOM.COSTS.TRADE = "N"
     OPEN 'I2.PRICE.UPDATE.LOG' TO F.I2.PRICE.UPDATE.LOG ELSE PRINT 'I2.PRICE.UPDATE.LOG' ; STOP
     LOG.ID = NEXT.UPDATE
     READ NEXT.PRICE.UPDATE.LOG FROM LCS.CONTROL,"NEXT.PRICE.UPDATE.LOG" ELSE NEXT.PRICE.UDPATE.LOG = ""
     READ XMISC FROM F.I2.PRICE.UPDATE.LOG,LOG.ID ELSE XMISC = ''
     UPDATE.TYPE = UPDATE.TYPE.P
     UPDATE.MODE = UPDATE.OPTIONS.IN<1>
     UPDATE.SCOPE = UPDATE.OPTIONS.IN<2>



     IF UPDATE.MODE="REFRESH" THEN
          REFRESH = 1
          CUSTOM.COST.TYPE = ""
          UPDATE.TYPE = "R"
          IF UPDATE.SCOPE = "" THEN UPDATE.SCOPE = "LP"
          IF LM THEN PRINT "STARTING REFRESH MODE:"
     END ELSE
          REFRESH = 0
          IF LM THEN PRINT "READING LOG.ID:":LOG.ID:"   ":XMISC
     END

     ARC.FILE.NAME = FILE.NAME

     IF LM THEN
          PRINT "***** STARTING I2.CREATE.PRODUCT.SOURCE"
          PRINT "NEXT.UPDATE:":LOG.ID
          PRINT "SCOPE:":UPDATE.SCOPE
          PRINT "MODE:":UPDATE.MODE
          PRINT "UPDATE.TYPE:":UPDATE.TYPE
     END
     UPDATE.SOURCE = COUNT(UPDATE.SCOPE,"S")
     UPDATE.LIB = COUNT(UPDATE.SCOPE,"L")
     UPDATE.PRODUCT = COUNT(UPDATE.SCOPE,"P")
     IF UPDATE.MODE = "REFRESH" THEN UPDATE.REFRESH = 1

     *    XMISC = ""

     IF DELIM = "" THEN
          DELIM = "COMMA"
          DELIMITER = ","
          DEBUG.MODE = 0
     END

     X.ITEM = DATA.START
     TEST.LINES = 0
     LOG.CT=0
     ANS = ""
     F=""
     PROC.LOG.MODE = 1



     GOSUB INIT.VARIABLES
     GOSUB OPEN.FILES

     IF UPDATE.SOURCE THEN
          GOSUB GET.HEADER.INFO
          IF DM THEN DEBUG
          GOSUB WRITE.UPDATE.LOG.ENTRY:
     END

MAIN:
     GOSUB SHOW.HEADER:
     EOF = FALSE
     TOTAL.COUNT = 0
     IF REFRESH THEN
          IF UPDATE.OPTIONS.IN<3> THEN
               SELECT.STR = "GET-LIST ":UPDATE.OPTIONS.IN<3>
               EXECUTE SELECT.STR CAPTURING CAP
               IF LM THEN PRINT CAP
               I2.PRODS.CT = FIELD(CAP<2>," ",1)
               IF I2.PRODS.CT = "0" THEN
                    PRINT "NO ITEMS FOUND IN LIST ":UPDATE.OPTIONS.IN<3>
                    STOP
               END
          END
          SELECT.STR = "SELECT I2.PRODUCT WITH UPC"
          IF LM THEN PRINT SELECT.STR
          EXECUTE SELECT.STR CAPTURING CAP
          IF LM THEN PRINT CAP
          I2.PRODS.CT = FIELD(CAP<2>," ",1)
          IF I2.PRODS.CT <> "0" THEN
               PRINT @(10,10):"READING ":I2.PRODS.CT:" FROM I2.PRODUCT"
               T.TOTAL= I2.PRODS.CT
               READLIST I2.PRODS THEN
                    I2.PRODS.CT = DCOUNT(I2.PRODS,@FM)
                    I2.PROD.X = 0
               END
          END ELSE
               PRINT "NO PRODUCTS SELECTED"
               STOP
          END

     END
     CUS.B = ""
     LOOP
          IF TEST.LINES THEN
               IF TOTAL.COUNT > TEST.LINES THEN EOF = TRUE
          END
          PNO=''
          UPC = ""
          CATNUM = ""
          PF.B = ""
          LIB.B = ""
          I2.PIK = ""
          PF.UPC=""
          PROD.B = ""
          ORIG.B = ""

          ERROR.STATUS = 0
     UNTIL EOF DO
          IF REFRESH THEN
               IF UPDATE.LIB THEN
                    LOOP
                         I2.PROD.X += 1
                         UPDATE.SOURCE.CT1 += 1
                         TOTAL.COUNT = TOTAL.COUNT + 1
                    WHILE NOT(EOF) DO
                         I2.PIK = I2.PRODS<I2.PROD.X>
                         IF I2.PROD.X >= I2.PRODS.CT THEN EOF = 1
                         SELECT.STR = "SELECT I2.PRODUCT.SOURCE = T] AND WITH PS_PIK = ":I2.PIK:" BY-DSND @ID"
                         IF LM THEN PRINT SELECT.STR
                         EXECUTE SELECT.STR CAPTURING CAP
                         IF FIELD(CAP<2>," ",1) <> "0" THEN
                              READLIST PS.KEYS THEN
                                   PRICE.SOURCE.LINE.ID = PS.KEYS<1,1>
                                   READ PF.B FROM F.I2.PRODUCT.SOURCE,PRICE.SOURCE.LINE.ID THEN
                                        PF.B2 = PF.B
                                        I2.PIK = PF.B<1>
                                        READ LIB.B FROM F.I2.PRODUCT,I2.PIK THEN ALIB.B = LIB.B ELSE ALIB.B=""
                                        IF LIB.B THEN GOSUB UPDATE.LIB:
                                        WRITE LIB.B ON F.I2.PRODUCT,I2.PIK
                                        IF UPDATE.PRODUCT THEN
                                             GOSUB LOOKUP.PRODUCT:
                                             IF PROD.B THEN
                                                  ORIG.B = PROD.B
                                                  PROD.B<61> = I2.PIK
                                                  PROD.B<141> = PRICE.SOURCE.LINE.ID
                                                  WRITE PROD.B ON F.PRODUCT,PNO
                                                  IF UPDATE.PRODUCT THEN
                                                       GOSUB UPDATE.PRODUCT
                                                  END
                                             END

                                        END
                                   END
                              END
                         END
                         IF NOT(MOD(TOTAL.COUNT,100)) THEN GOSUB DISPLAY.PROGRESS
                    REPEAT
               END
          END ELSE
               TOTAL.COUNT = TOTAL.COUNT + 1
               LINE.NUM = ("000000000":TOTAL.COUNT) "R#10"
               PRICE.SOURCE.LINE.ID = LOG.ID:":":LINE.NUM
               IF UPDATE.SOURCE THEN GOSUB UPDATE.SOURCE:
               IF NOT(EOF) THEN
                    IF UPDATE.LIB THEN GOSUB UPDATE.LIB:
                    IF UPDATE.PRODUCT THEN GOSUB UPDATE.PRODUCT:
               END
          END
          IF NOT(MOD(TOTAL.COUNT,100)) THEN GOSUB DISPLAY.PROGRESS
     REPEAT
     GOSUB WRITE.UPDATE.LOG.ENTRY:
     IF UPDATE.TYPE = 'T' THEN WRITE VEND.B ON F.CONTROL, 'I2.VEND'
FINISHED:
     PRINT ; PRINT
     RETURN


SHOW.HEADER:
     TITLE = "LOG.ID: ":LOG.ID:"  VENDOR:":UPDATE.CODE:"  ":UPDATE.NAME:"  VENDOR#:":UPDATE.NUM "R#6 ":"MODE:":UPDATE.MODE:" SCP:":UPDATE.SCOPE
     TITLE2 = "UPDATE.TYPE:":UPDATE.TYPE:" USER: ":USER:"  UPDATEID: ":UPDATEID:"  EFFDATE:":OCONV(MAST.EFFDATE,"D4/")
     TITLE3 = ""
     *IF NOTE THEN TITLE3 := "   NOTE:":NOTE
     PROC.LOG = "RAN INTERPRET.XMISC: XMISC = ":XMISC:" UPDATE.TYPE:":FILE.NAME:" MAST.EFFDATE:":OCONV(MAST.EFFDATE,"D4/")
     GOSUB WRITE.PROC.LOG
     PROC.LOG = TITLE ; GOSUB WRITE.PROC.LOG
     PROC.LOG = TITLE2 ; GOSUB WRITE.PROC.LOG
     PROC.LOG = TITLE3 ; GOSUB WRITE.PROC.LOG

     PRINT @(-1): ; PRINT TITLE ; PRINT TITLE2 ; PRINT TITLE3
     PRINT @(1,15):"SOURCE" "R#9":"  : ":"LIBRARY" "R#9":"      ":"PRODUCT" "R#9":

     PROC.LOG = "FILE.NAME":"|":"CUSTOM.COST.TYPE":"|":"PF.CUST.COST.ATT":"|":"PF.CUSTOM.COST":"|":"CUSTOM.COST.ATT":"|":"CUSTOM.COST":"|":"PF.CUSTOM.COST.UOM|":"CUSTOM.COST.UOM" ; GOSUB WRITE.PROC.LOG
     PROC.LOG = FILE.NAME:"|":CUSTOM.COST.TYPE:"|":PF.CUSTOM.COST.ATT:"|":PF.CUSTOM.COST:"|":CUSTOM.COST.ATT:"|":LIB.B<CUSTOM.COST.ATT>:"|":PF.CUSTOM.COST.UOM.ATT:"|":CUSTOM.COST.UOM ; GOSUB WRITE.PROC.LOG
     PROC.LOG = LIB.B ; GOSUB WRITE.PROC.LOG
     RETURN

UPDATE.SOURCE:
     UPDATE.SOURCE.CT1 += 1
     IF LM THEN PRINT "**** UPDATING SOURCE: (UPDATE TYPE:":UPDATE.TYPE:"  ":UPDATE.SOURCE.CT1
     IF UPDATE.TYPE = "T" THEN
          READSEQ PF.B FROM F.I2PSVC ELSE EOF = 1
     END ELSE
          PF.B = PF.BALL<ITEM.X>
          ITEM.X += 1
          IF ITEM.X > ITEM.X.CT THEN EOF = 1
     END
     PF.B.O = PF.B
     IF LM THEN PRINT "PF.B:":PF.B
     PROC.LOG = "READ SOURCE FILE:":LINE.NUM ; GOSUB WRITE.PROC.LOG:
     IF PF.B <> "" THEN
          CONVERT DELIMITER TO @FM IN PF.B
          PROC.LOG = "READING PRICE RECORD:":PF.B ; GOSUB WRITE.PROC.LOG
          PF.B = TRIM(PF.B,"$","A")
          PF.B = TRIM(PF.B,",","A")
          PF.B = TRIM(PF.B,'"',"A")
          PF.B = TRIM(PF.B,"-","B")
          PF.B2 = ""

          IF UPDATE.TYPE <> 'T' THEN
               FOR SC.X = 1 TO ATT.COUNT
                    IF XMISC<16,SC.X,1> THEN
                         UP.ATT = XMISC<16,SC.X,2>
                         PF.ATT = XMISC<16,SC.X,1>
                         IF UP.ATT = 99 THEN
                              UPC.P = PF.B<PF.ATT>
                              GOSUB GET.UPC:
                              PF.B<PF.ATT> = UPC.P
                              UPC = UPC.P
                              GOSUB LOOKUP.PRODUCT:
                              GOSUB UPDATE.CUSTOM.PRICES:
                         END
                         PF.B2<UP.ATT> = PF.B<PF.ATT>
                    END
               NEXT SC.X
          END ELSE
               READ OPF.B FROM F.I2.PRODUCT.SOURCE,PRICE.SOURCE.LINE.ID THEN PF.B = OPF.B ELSE OPF.B = ""
               IF OPF.B <> PF.B THEN
                    PF.B2 = PF.B
               END
          END
          IF PF.B.O <> PF.B2 THEN
               WRITE PF.B2 ON F.I2.PRODUCT.SOURCE,PRICE.SOURCE.LINE.ID
               UPDATE.SOURCE.CT2 += 1
          END
     END ELSE EOF = 1
     RETURN

UPDATE.LIB:
     UPDATE.LIB.CT1 += 1
     IF PF.B2 = "" THEN
          * DIDN'T JUST DO UPDATE.SOURCE SO READ FROM SOURCE
          READ PF.B FROM F.I2.PRODUCT.SOURCE,PRICE.SOURCE.LINE.ID ELSE
               PRINT PRICE.SOURCE.LINE.ID:" MISSING FROM I2.PRODUCT.SOURCE"
               *DEBUG
          END
     END
     IF UPDATE.TYPE ='R' THEN
          GOSUB UPDATE.PRODUCT.LIB
     END
     IF UPDATE.TYPE = "T" THEN
          I2.PIK = PF.B<1>
          PROC.LOG = "READING EXISTING I2.PRODUCT RECORD ":I2.PIK ; GOSUB WRITE.PROC.LOG
          READ ALIB.B FROM F.I2.PRODUCT,I2.PIK ELSE ALIB.B = ""
          LIB.B = ALIB.B
          GOSUB UPDATE.PRODUCT.LIB:
     END
     IF UPDATE.TYPE = "V" THEN
          IF NOT(I2.PIK) THEN GOSUB PARSE.VENDOR.DATA:
          UPC = PF.B<99>
          IF PNO THEN PF.B<9> = PNO
          IF I2.PIK THEN PF.B<1> = I2.PIK
          READ ALIB.B FROM F.I2.PRODUCT,I2.PIK ELSE ALIB.B = ""
          LIB.B = ALIB.B
          GOSUB UPDATE.PRODUCT.LIB:
     END
     IF UPDATE.TYPE = "M" THEN
          IF NOT(I2.PIK) THEN GOSUB PARSE.MANUAL.DATA:
          READ ALIB.B FROM F.I2.PRODUCT,I2.PIK ELSE ALIB.B = ""
          LIB.B = ALIB.B
          GOSUB UPDATE.PRODUCT.LIB:
     END
     RETURN

UPDATE.CUSTOM.PRICES:
     IF I2.PIK THEN
          C4.COST.NOW = ""
          READ CUS.B FROM F.CUSTOM.PRICES,I2.PIK ELSE CUS.B = ""
          CUS.B.O = CUS.B
          CUS.B<CUS.TYPE.H> = UPDATE.TYPE

          CUS.B<CUS.SOURCE.NUM.H> = PRICE.SOURCE.LINE.ID
          CUS.B<CUS.SOURCE.NAME.H> = UPDATEID
          IF UPDATE.TYPE = "M" THEN
               CUS.MAN.COST = PF.B2<54>
               CUS.COST.UOM = PF.B2<73>
               IF CUS.MAN.COST THEN C4.COST.NOW = CUS.MAN.COST
          END ELSE
               IF UPDATE.TYPE = "V" THEN
                    CUS.VEND.COST = PF.B2<53>
                    CUS.COST.UOM = PF.B2<72>
                    IF CUS.VEND.COST THEN C4.COST.NOW = CUS.VEND.COST
               END ELSE
                    IF UPDATE.TYPE = "T" THEN
                         CUS.TS.COST = PF.B2<52>
                         CUS.COST.UOM = PF.B2<71>
                         IF CUS.TS.COST THEN C4.COST.NOW = CUS.TS.COST
                    END
               END
          END
          CMD = 10000
          CP.UOM = CUS.COST.UOM
          IF CP.UOM = "EA" THEN CP.UOM = 'E'
          BEGIN CASE
               CASE CP.UOM = 'E' ; CMD = 10000
               CASE CP.UOM = 'C' ; CMD = 100
               CASE CP.UOM = 'M' ; CMD = 10
          END CASE

          IF NUM(C4.COST.NOW) THEN C4.COST.NOW = C4.COST.NOW * CMD

          IF C4.COST.NOW <> CUS.C4.COST THEN
               CUS.C4.COST = C4.COST.NOW
               IF CUS.EFF.DATE THEN CUS.C4.COST.DT = CUS.EFF.DATE ELSE
                    CUS.C4.COST.DT = TODAY
               END
          END
          IF CUS.B.O <> CUS.B THEN
               CUS.UP.DATE = TODAY
               WRITE CUS.B ON F.CUSTOM.PRICES,I2.PIK
          END
     END
     RETURN


UPDATE.PRODUCT.LIB:
     PROC.LOG = "UPDATING CHANGED ATTRIBUTES OF I2.PRODUCT RECORD" ; GOSUB WRITE.PROC.LOG
     PROC.LOG = ""
     CHANGE.LOG = ""
     CHANGED.ATT = 0
     NEW.REC = ""
     OLD.REC=""
     NEW.ATT=""
     IF REFRESH THEN
          GOSUB COPY.SOURCE.TO.LIB:
     END
     IF UPDATE.TYPE <> "T" THEN
          LIB.B<99> = ALIB.B<99>
          LIB.B<1> = ALIB.B<1>
     END
     GOSUB WRITE.I2.PRODUCT:
     RETURN


COPY.SOURCE.TO.LIB:
     LIB.COUNT = DCOUNT(PF.B,@FM)
     FOR PF.ATT = 1 TO LIB.COUNT
          NEW.FIELD = PF.B<PF.ATT>
          IF NEW.FIELD = '~' THEN
               LIB.B<PF.ATT> = ""
          END
          IF PF.ATT = 99 THEN
               UPC.P =NEW.FIELD
               GOSUB GET.UPC:
               IF UPC.P THEN NEW.FIELD = UPC.P
          END
          IF UPDATE.TYPE <> "T" THEN
               IF NEW.FIELD > "" THEN
                    LIB.B<PF.ATT> = NEW.FIELD
               END
          END ELSE
               LIB.B<PF.ATT> = NEW.FIELD
          END
     NEXT PF.ATT

     RETURN

WRITE.I2.PRODUCT:
     IF LIB.B <> ALIB.B THEN
          LIB.B<3> = "U"
          LIB.B<42> = PRICE.SOURCE.LINE.ID
          WRITE LIB.B ON F.I2.PRODUCT,I2.PIK
          UPDATE.LIB.CT2 += 1
     END
     *IF (UPDATE.TYPE = "V" OR UPDATE.TYPE = "M") THEN
     *
     *GOSUB UPDATE.CUSTOM.PRICES:
     *END
     RETURN

SHOW.NEW:
     PRINT "    **WRITING ON I2.PRODUCT ":PRICE.SOURCE.LINE.ID:" ":I2.PIK
     PRINT OLD.REC
     PRINT NEW.REC
     RETURN



UPDATE.PRODUCT:
     UPDATE.PRODUCT.CT1 += 1
     GOSUB LOOKUP.PRODUCT:
     IF LIB.B = "" THEN
          READV I2.PIK FROM F.I2.PRODUCT.SOURCE,PRICE.SOURCE.LINE.ID,1 THEN
               READ LIB.B FROM F.I2.PRODUCT,I2.PIK ELSE
                    PRINT "NO RECORD FOUND FOR ":I2.PIK:" IN I2.PRODUCT" ; DEBUG
               END
          END ELSE
               PRINT "NO RECORD FOUND FOR ":PRICE.SOURCE.LINE.ID:" IN I2.PRODUCT.SOURCE" ; DEBUG
          END
     END
     IF PROD.B THEN
          ORIG.B = PROD.B
          EFFDATE = ""
          IF LIB.B<44> THEN EFFDATE = ICONV(LIB.B<44>,"D4-") ELSE EFFDATE = MAST.EFFDATE
          PROC.LOG = " *** FOUND EXISTING PRODUCT IN PRODUCT FILE ":PNO ; GOSUB WRITE.PROC.LOG
          PROC.LOG = PROD.B ; GOSUB WRITE.PROC.LOG:
          PRICE.SOURCE.LINE.ID = LIB.B<42>
          MFGUCC = LIB.B<4>
          ITEMNO= LIB.B<5>
          MFGPNO= LIB.B<22>
          UPC = MFGUCC:ITEMNO
          MFG = LIB.B<18>
          MFG.NAME = LIB.B<19>
          COMM.PIK = LIB.B<33>
          PROD.B<141> = PRICE.SOURCE.LINE.ID
          GOSUB UPDATE.DESC            ; *** SET UPC KEYWORD IN PROD.FIND
          GOSUB UPDATE.UPC             ; *** SET UP MFGPNO KEYWORD IN PROD.FIND
          GOSUB UPDATE.MFGPNO          ; *** UPDATE LIST.PRICE IF NO PRICING TEMPLATE EXISTS
          GOSUB UPDATE.PSVNO           ; *** UPDATE PSVNO
          GOSUB CHECK.EOM              ; *** CHECK FOR UOM CHANGE & IF SO UPDATE PRICE UOM
          READ CPBUFF FROM F.CUSTOM.PRICES,I2.PIK THEN
               GOSUB CUSTOM.PRICES:      ; *** CHECK FOR CUSTOM PRICES
          END

          IF CUS.C4.COST THEN
          END
          NET.COST.ROLLUP = ""
          IF REFRESH THEN NET.COST.ROLLUP = PROD.B<161>
          IF CUSTOM.COST.TYPE THEN
               PROD.B<180> = CUSTOM.COST.TYPE      ; *** SET I2.FIELD.TEMPLATE IF ITEM GETS SPECIAL COSTS
               IF PROD.B<62> = PROD.B<3> THEN
                    PROC.LOG = "C4 IS THE SAME AS C1, NOT REALLY A SPECIAL PRICE. DISCOUNT ZERO" ; GOSUB WRITE.PROC.LOG
                    ZERO.DISC.CT += 1
                    *                  PROD.B<62> = ""
               END
               IF PROD.B<62> THEN
                    PROD.B<124> = CUS.C4.COST
                    PROD.B<62> = CUS.C4.COST
                    PROD.B<63> = CUS.C4.COST.DT
                    PROD.B<125> = CUS.C4.COST.DT
                    EFFDATE = CUS.C4.COST.DT
                    PROC.LOG = "C4 REPRESENTS A SPECIAL COST OF ":PROD.B<62>:" AND EFFDATE ":EFFDATE ; GOSUB WRITE.PROC.LOG
                    PROD.B<63> = EFFDATE
                    *PRICE.GROUP = PROD.B<112>[1,3]:"NET"
                    *NET.COST.ROLLUP = CONVERT.ROLLUP.GROUP(PRICE.GROUP,F.PRICE.ROLLUP)
                    *PROC.LOG = "NET.COST.ROLLUP WAS ":NET.COST.ROLLUP:" GIVEN PRICE.GROUP OF ":PRICE.GROUP ; GOSUB WRITE.PROC.LOG
               END
          END
          *IF NET.COST.ROLLUP THEN
          *PROD.B<161> = NET.COST.ROLLUP
          *PROC.LOG = "NET.COST.ROLLUP ASSIGNED TO ATTRIBUTE 161" ; GOSUB WRITE.PROC.LOG
          *END
          NEW.B = ""
          CALL PRICE.ROLLUP.SUB (F.PRODUCT, F.PROD.LINE, F.PRICE.GROUP, PNO, PROD.B, NEW.B, 1)     ; ****  Call PRICE.ROLLUP.SUB
          PROD.B = NEW.B
          PROC.LOG = "CALLED PRICE.ROLLUP.SUB" ; GOSUB WRITE.PROC.LOG
          PROC.LOG = PROD.B<180>:"|":PROD.B<3>:"|":PROD.B<62>:"|":PROD.B<124> ; GOSUB WRITE.PROC.LOG

          IF PROD.B <> ORIG.B THEN
               PROD.B<152> = DATE() ; PROD.B<153> = TIME()
               PROC.LOG = "PRODUCT RECORD CHANGED... COMMITTING UPDATE :":PROD.B
               WRITE PROD.B ON F.PRODUCT, PNO
               UPDATE.PRODUCT.CT2 += 1
               IF PROD.B<61> THEN
                    WRITEV PNO ON F.I2.PRODUCT,PROD.B<61>,9
               END
          END ELSE
               PROC.LOG = "NO CHANGE IN PRODUCT RECORD DETECTED.  UPDATE NOT NECESSARY"
          END
          GOSUB WRITE.PROC.LOG:
     END ELSE RELEASE F.PRODUCT, PNO
     RETURN


CUSTOM.PRICES:

     *CALL APPLY.CUSTOM.PRICES(UPDATE.TYPE,I2.PIK,LIB.B,PNO,PROD.B,PRICE.SOURCE.LINE.ID,PF.B)
     EXECUTE "APPLY.CP ":I2.PIK:" UPDATE" CAPTURING CAP

     RETURN


PARSE.VENDOR.DATA:
     GOSUB LOOKUP.PRODUCT:
     IF EFFDATE THEN
          EFFDATE = ICONV(EFFDATE,"D4-")
     END ELSE
          EFFDATE = ICONV(MAST.EFFDATE,"D4-")
     END
     IF MAST.EXPDATE THEN EXPDATE = MAST.EXPDATE
     IF NOT(EFFDATE) THEN EFFDATE = MAST.EFFDATE
     RETURN

GET.UPC:
     IF UPC.FORMAT THEN GOSUB RUN.FORMULA:

     UPC.L = LEN(UPC.P)
     IF LEN(UPC.P) = 5 THEN UPC.P = UPDATE.CODE:UPC.P
     IF UPC.L = 14 THEN
          UPC.P=UPC.P[3,11]
     END
     IF UPC.L = 12 THEN UPC.P = UPC.P[1,11]
     IF UPC.L = 10 THEN UPC.P = "0":UPC.P
     UPC.L = LEN(UPC.P)
     IF UPC.L <> 11 THEN
          UPC.P = ("00000000000":UPC.P) "R#11"
          IF LM THEN PRINT "UPC3:":UPC.P
          UPC.P = RIGHT(UPC.P,11)
          IF LM THEN PRINT "UPC4:":PF.UPC
     END
     UPC.P = UPC.P[1,11]
     IF LEN(UPC.P) = 11 THEN
          MFF_UCC_NUM = UPC.P[1,6]
          ITEM_NUM = UPC.P[7,5]
     END
     RETURN


RUN.FORMULA:
     IF UPC.FORMAT = "6:5" THEN
          UPC.P = UPDATE.CODE:ITEM_NUM
     END
     RETURN



PARSE.MANUAL.DATA:
     RETURN


INIT.VARIABLES:
     NOW = DATE()
     EFFDATE = ""
     HEADER = ""
     TODAY = NOW
     OTODAY = OCONV(TODAY,"D4-")
     MONTH = ("00":FIELD(OTODAY,"-",1)) "R#2"
     DAY = ("00":FIELD(OTODAY,"-",2)) "R#2"
     YEAR = FIELD(OTODAY,"-",3)
     DATE.STAMP = YEAR:MONTH:DAY
     BEFORE.NOW = NOW
     START.TIME = TIME()
     PROC.LOG = ""
     PRICE.SOURCE.LINE.ID =0
     UPDATE.LIB.CT1 = 0
     UPDATE.LIB.CT2 = 0
     UPDATE.PRODUCT.CT1 = 0
     UPDATE.PRODUCT.CT2 = 0
     UPDATE.SOURCE.CT1 = 0
     UPDATE.SOURCE.CT2 = 0
     F = ""
     MASTER.CUSTOM.COST.UOM = ""
     PF.HEAD.ITEMS = ""
     PF.HEAD = ""
     PF.HEAD.HEADER = ""
     NOTE = ""
     UPC.OPTIONS = ""
     ZERO.DISC.CT = 0
     MAST.EXPDATE = ""
     UPC11 = ""
     TOTAL.COMPLETED.DATE = ""
     PF.CUSTOM.COST = 0
     PF.CUSTOM.COST.ATT=0
     PF.CUSTOM.COST.UOM = ""
     CUSTOM.COST = 0
     CUSTOM.COST.ATT = 0
     CUSTOM.COST.UOM.ATT = 0
     CUSTOM.COST.UOM = ""
     CUSTOM.COST.DATE.ATT = 0
     CUSTOM.COST.DATE = ""
     I2.LOG.FILE.NAME = LOG.ID:"-":DATE.STAMP
     OPENSEQ "I2.LOGS",I2.LOG.FILE.NAME TO IPUTL ELSE CREATE IPUTL ELSE PRINT "UNABLE TO CREATE I2.PRODUCT.PRICE.UPDATE.LOG.":LOG.ID:DATE.STAMP ; STOP
     IF LM THEN PRINT "STARTING INIT.VARIABLES"
     IF NOT(REFRESH) THEN
          FILE.EXT = FIELD(ITEM.NAME,".",2)
          IF UPCASE(FILE.EXT) = "CSV" THEN DELIM = "COMMA"
          IF UPCASE(FILE.EXT) = 'TSV' THEN DELIM = "TAB"
          IF UPCASE(FILE.EXT) = 'TXT' THEN DELIM = 'TAB'
          IF DELIM = "TAB" THEN DELIMITER = CHAR(9)
          IF DELIM = "COMMA" THEN DELIMITER = ","
          IF LEN(DELIM) = 1 THEN DELIMITER=DELIM
          PROC.LOG = "DELIM:":DELIM ; GOSUB WRITE.PROC.LOG
     END

     LIB.B = ""
     PROC.LOG = "CHECKPOINT 1 UPDATE:":UPDATEID:"  PATH:":FILE.NAME:"  RUN AT ":OCONV(DATE(),'D4/'):" ":OCONV(TIME(),"MHS") ; GOSUB WRITE.PROC.LOG

     PF.C1.ATT = 0

     IF LM THEN PRINT "LOG.ID:":LOG.ID
     PNO=""
     PRCLMS = 'C1':IVM:'C2':IVM:'C3':IVM:'C4':IVM:'C5':IVM:'C6':IVM:'C7':IVM:'L1':IVM:'L2':IVM:'L3':IVM:'L4'
     PRCLMS<2> = 3:IVM:5:IVM:19:IVM:62:IVM:120:IVM:122:IVM:124:IVM:13:IVM:64:IVM:66:IVM:126

     *      COL   PROD  LIB
     *      C1    3     51
     *      C2    5
     *      C3    19
     *      C4    62    52 OR 53 OR 54
     *      C5    120
     *      C6    122
     *      C7    124
     *      L1    13    47
     *      L2    64    4
     *      L3    66    49
     *      L4    126   46

     PF.CUSTOM.COST.UOM.ATT = 0
     IF UPDATE.TYPE = "T" THEN
          CUSTOM.COST.TYPE = ""
     END ELSE
          IF UPDATE.TYPE ="V" THEN
               CUSTOM.COST.TYPE = "V"
               CUSTOM.COST.ATT=53
               CUSTOM.COST.UOM.ATT=72
               IF MAST.EFFDATE = "" THEN MAST.EFFDATE = FIELD(FIELD(UPDATEID,".",1),"_",2)
               MAST.EFFDATE = ICONV(MAST.EFFDATE,"D4-")
          END ELSE
               IF UPDATE.TYPE = "M" THEN
                    CUSTOM.COST.TYPE = "M"
                    CUSTOM.COST.ATT=54
                    CUSTOM.COST.UOM.ATT=73
                    *        CUSTOM.COST.DATE.ATT = 112
               END
          END
     END
     IF LM THEN PRINT @(20,20):"  UPDATE.SOURCE.CT: ":UPDATE.SOURCE.CT1
     CATNUM.ATT = 23
     UPC.ATT = 99
     EFFDATE.ATT = 44
     MAST.EXPDATE.ATT = 45
     PRICE.SOURCE.LINE.ID = 0
     PNO.ATT = 9
     PS.PIK.ATT = 1

     IF REFRESH THEN PROC.LOG = "REFRESH MODE" ELSE
          IF LM THEN PRINT "OPTIONS:":UPDATE.MODE
          PROC.LOG = "CUSTOM.COST.TYPE:":CUSTOM.COST.TYPE:TAB:"CUSTOM.COST.ATT:":CUSTOM.COST.ATT:TAB:"CUSTOM.COST.UOM.ATT:":CUSTOM.COST.UOM.ATT:TAB:"CUSTOM.COST.DATE.ATT:":CUSTOM.COST.DATE.ATT ; GOSUB WRITE.PROC.LOG
          PROC.LOG = "FILE.NAME:":FILE.NAME:"  FILE.NAME:":FILE.NAME:"  LOG.ID:":LOG.ID:" UPDATE:":UPDATEID ; GOSUB WRITE.PROC.LOG
     END
     PREV.VEND = '' ; PREV.COMM = ''

     UPDATE.DATE = TODAY
     TOTAL.COUNT = 0
     ERROR.COUNT = 0
     UPDATE.LIB.CT2 = 0
     UPDATE.PRODUCT.CT2 = 0
     ZERO.DISC.CT = 0

     I2KEY = ''
     EOF = FALSE
     VENDOR = ""
     RETURN

GET.HEADER.INFO:
     IF LM THEN PRINT "GETTING HEADER INFO"
     *        KEY  =  1001 10002 10003 ETC
     *        XMISC<1>    Official Name of Update (DIVCD:YYYYMMDD) (i.e 3-M20090627) or ISSUE NUMBER FOR EDATAFLEX UPDATES
     *              2     User running the update
     *              3     3 CHARACTER VENDOR CODE
     *              4     VENDOR NUMBER
     *              5     VENDOR NAME
     *              6     Effective Date of Update
     *              7     Effective To Date
     *              8     Options (UPC11, etc)
     *              9     Type of update (T=TRADE, V=VENDOR Disk, M=MANUAL Conserve Entered prices)
     *              10    UPDATE = Name of file that is the source of price update data VENDOR_UPDATES
     *              11    ITEM NAME 3-M20120701.CSV
     *              12    NUMBER OF TIMES UPDATE WAS RUN
     *              13    HEADER RECORD 1
     *              14    HEADER RECORD 2
     *              15    MASTER.CUSTOM.UOM
     *              16    DATES THE UPDATE RAN
     *              17    NUMBER OF PRICE RECORDS PROCESSED
     *              18    NUMBER OF SOURCE RECORDS PROCESSED
     *              19    NUMBER OF LIBRARY RECORDS PROCESSED
     *              20    NUMBER OF PRODUCTS UPDATED
     *              21    NUMBER OF ERRORS
     *              22    RUN TYPE "S,L,P,SLP,ETC"
     *              23    MULTIVALUE VENDORS IN UPDATE
     *              24    NUMBER OF PRICE RECORDS FOR VENDOR
     *              25    NUMBER OF UPDATES FOR VENDOR SOURCE
     *              26    NUMBER OF VENDOR LIBRARY RECORDS PROCESSED
     *              27    NUMBER OF VENDOR PRODUCTS UPDATED
     *              28    NUMBER OF VENDOR ERRORS
     *              29    NUMBER OF PRICE RECORDS WITH ZERO DISCOUNT

     PROD.LOG = "STARTING GET.HEADER.INFO" ; GOSUB WRITE.PROC.LOG
     I2PSVC.PATH = FILE.PATH:ITEM.NAME
     PRINT ARC.FILE.NAME:",":ITEM.NAME
     IF UPDATE.TYPE <> 'T' THEN
          OPEN ARC.FILE.NAME TO F.ARC.FILE.NAME ELSE PRINT ARC.FILE.NAME;STOP
          READ PF.BALL FROM F.ARC.FILE.NAME,ITEM.NAME THEN
               ITEM.X.CT = DCOUNT(PF.BALL,@FM)
               ITEM.X =DATA.START
          END ELSE EOF = 1
     END ELSE
          OPENSEQ ARC.FILE.NAME,ITEM.NAME TO F.I2PSVC ELSE PRINT ARC.FILE.NAME:" ":ITEM.NAME;DEBUG
     END
     PF.EFFDATE.ATT = 44
     PF.MAST.EXPDATE.ATT = 45
     PF.CUSTOM.COST.UOM.ATT = 39
     PF.CUSTOM.COST.ATT = 52
     PF.CATNUM.ATT = 23
     PF.UPC.ATT = 99
     PF.PRODNO.ATT = 9
     PF.PSNUM.ATT = 1
     PF.C1.ATT = 51

     IF UPDATE.TYPE = "T" THEN
          UPDATE.CODE = "T-S"
          READSEQ PF.HEAD FROM F.I2PSVC ELSE PF.HEAD = ""
          IF PF.HEAD THEN
               HEADER = PF.HEAD
               I2.HEADER = CHANGE(PF.HEAD,TAB,@FM)
               WRITE I2.HEADER ON LCS.CONTROL,"I2.HEADER"
          END
          ATT.COUNT = DCOUNT(PF.HEAD,TAB)
          IF PF.HEAD THEN PF.HEAD.HEADER = PF.HEAD
          I2COMM = UPDATEID:"-comm.txt"
          I2MEDIA = UPDATEID:"-media.tbl"
          OPENSEQ FILE.PATH:I2COMM TO F.I2COMM ELSE PRINT 'UNABLE TO OPEN ':FILE.PATH:I2COMM ; DEBUG
          IF NOT(F) THEN PROC.LOG = "TRADESERVICE FILES OPENED WITH NO ERRORS" ; GOSUB WRITE.PROC.LOG
          UPDATE.NAME = "TRADESERVICE";
          GOSUB CHECK.ERRORS
          GOSUB COMM.DATA


     END ELSE
          V.HEADER = HEADER
          IF DELIM = "COMMA" THEN DELIMETER = ","
          IF DELIM = "TAB" THEN DELIMETER = CHAR(9)
          IF DELIM = "COLON" THEN DELIMETER = ":"
          IF DATA.START > 1 THEN
               *FOR SKIP.X = 1 TO DATA.START
               *READSEQ PF.HEAD FROM F.I2PSVC ELSE PF.HEAD = ""
               *NEXT SKIP.X
               PF.HEAD = PF.BALL<1>
          END
          ATT.COUNT = DCOUNT(COL.LABELS,@VM)
     END

     RETURN


WRITE.HEADER.TO.LOG:
     PROC.LOG<-1> = "ARC.FILE.NAME = ":ARC.FILE.NAME
     PROC.LOG<-1> = "USER = ":USER
     PROC.LOG<-1> = "NOTE = ":NOTE
     PROC.LOG<-1> = "UPDATE.CODE" = UPDATE.CODE
     PROC.LOG<-1> = "UPC11 = ":UPC11
     PROC.LOG<-1> = "UPDATE.NUM = ":UPDATE.NUM
     PROC.LOG<-1> = "UPDATE.NAME = ":UPDATE.NAME
     PROC.LOG<-1> = "MAST.UOM = ":MASTER.CUSTOM.COST.UOM
     PROC.LOG<-1> = "MAST.EFFDATE = ":MAST.EFFDATE
     PROC.LOG<-1> = "EFFDATE.ATT = ":PF.EFFDATE.ATT
     PROC.LOG<-1> = "MAST.EXPDATE = ":PF.MAST.EXPDATE.ATT
     PROC.LOG<-1> = "PF.CUSTOM.COST.UOM.ATT = ":PF.CUSTOM.COST.UOM.ATT
     PROC.LOG<-1> = "PF.CUSTOM.COST.ATT = ":PF.CUSTOM.COST.ATT
     PROC.LOG<-1> = "PF.CATNUM.ATT = ":PF.CATNUM.ATT
     PROC.LOG<-1> = "PF.UPC.ATT = ":PF.UPC.ATT
     PROC.LOG<-1> = "PF.PRODNO.ATT = ":PF.PRODNO.ATT
     PROC.LOG<-1> = "PF.PSNUM.ATT = ":PF.PSNUM.ATT
     PROC.LOG<-1> = "PF.C1.ATT = ":PF.C1.ATT
     GOSUB WRITE.PROC.LOG:
     RETURN

OPEN.FILES:
     IF LM THEN PRINT "OPENING FILES"
     OPEN 'I2.PRODUCT' TO F.I2.PRODUCT ELSE F = F:', I2.PRODUCT'

     OPEN 'PSVNO' TO F.PSVNO ELSE F = F:', PSVNO'
     OPEN 'CATVNO' TO F.CATVNO ELSE F = F:', CATVNO'
     OPEN 'PRODUCT' TO F.PRODUCT ELSE F = F:', PRODUCT'
     OPEN 'PROD.FIND' TO F.PROD.FIND ELSE F = F:', PROD.FIND'
     OPEN 'PROD.LINE' TO F.PROD.LINE ELSE F = F:', PROD.LINE'
     OPEN 'PRICE.ROLLUP' TO F.PRICE.ROLLUP ELSE F = F:', PRICE.ROLLUP'
     OPEN 'PRICE.GROUP' TO F.PRICE.GROUP ELSE F = F:', PRICE.GROUP'
     OPEN 'UPC.PSVNO.XREF' TO F.UPC.PSVNO.XREF ELSE F = F:', UPC.PSVNO.XREF'
     OPEN 'UOM' TO F.UOM ELSE F = F:', UOM'
     OPEN 'I2.PRODUCT.SOURCE' TO F.I2.PRODUCT.SOURCE ELSE PRINT 'I2.PRODUCT.SOURCE' ; STOP
     OPEN 'I2.PRODUCT.ARCHIVE' TO F.I2.PRODUCT.ARCHIVE ELSE F = F:', I2.PRODUCT.ARCHIVE'
     OPEN 'UPC.PRODUCT.XREF' TO F.UPC.PRODUCT.XREF ELSE F = F:", UPC.PRODUCT.XREF"
     OPEN 'CUSTOM.PRICES' TO F.CUSTOM.PRICES ELSE F = F:", CUSTOM.PRICES"
     OPEN 'PRICE.TRANS' TO PRICE.TRANS ELSE PRINT 'PRICE.TRANS' ; STOP
     PROC.LOG = "OPEN.FILES DONE WITH F=":F
     OPEN 'DICT','I2.PRICE.UPDATE.LOG' TO D.I2PUL ELSE PRINT "DICT,I2.PRICE.UPDATE.LOG";STOP
     OPEN 'DICT','I2.PRODUCT' TO D.I2.PRODUCT ELSE PRINT 'DICT,I2.PRODUCT';STOP
     IF NOT(REFRESH) THEN
          OPEN ARC.FILE.NAME TO F.UP ELSE PRINT ARC.FILE.NAME ; DEBUG
     END

     GOSUB CHECK.ERRORS:

     READ I2.HEADER FROM LCS.CONTROL, "I2.HEADER" ELSE I2.HEADER = ''

     READ OPT.B FROM F.CONTROL, 'I2.OPTIONS' ELSE OPT.B = ''
     READ NEVER.UPDATE.DESC.I2 FROM F.CONTROL,'NEVER.UPDATE.DESC.I2' ELSE NEVER.UPDATE.DESC.I2 = ''
     READ I2FT.B FROM F.CONTROL, 'I2.FIELD.TEMPLATE' ELSE I2FT.B = ''
     READ VEND.B FROM F.CONTROL, 'I2.VEND' ELSE VEND.B = ''
     READ UPD.UOM FROM F.CONTROL,"I2.UPD.UOM" ELSE UPD.UOM = "N"
     READ COMP.B FROM F.CONTROL, 'COMPANY' ELSE COMP.B = ''
     READ C4.OVERWRITABLE FROM F.CONTROL, 'C4.OVERWRITABLE' ELSE C4.OVERWRITABLE = 'N'
     GUI.SYSTEM = COMP.B<247>
     GOSUB CHECK.ERRORS

     RETURN


UPDATE.DESC:
     IF NEVER.UPDATE.DESC.I2 # "Y" THEN
          IF (PROD.B<1> = LIB.B<18>:' ':LIB.B<22>) AND (LIB.B<27> # "") THEN
               IF OPT.B<1> = 'Y' THEN DESC = LIB.B<22>:' ':LIB.B<27> ELSE
                    DESC = LIB.B<27>
                    IF MFG # '' THEN DESC = MFG:' ':DESC
                    IF DESC = '' THEN DESC = LIB.B<22>
               END
               CALL CONVERT.LENGTH (DESC, 30, PXMISC)
               PROD.B<1> = PXMISC<1,1>
               DEL PXMISC<1,1>
               IF PXMISC<1,1> # '' THEN
                    PROD.B<71> = PXMISC<1,1>
                    DEL PXMISC<1,1>
               END
               IF PXMISC<1,1> # '' THEN
                    PROD.B<151> = PXMISC<1>
               END
          END
     END
     RETURN

UPDATE.UPC:
     IF PROD.B<18> # UPC & UPC # '' THEN
          READU KW.B FROM F.PROD.FIND, PROD.B<18> THEN
               LOCATE PNO IN KW.B<1> SETTING VM THEN DEL KW.B<1,VM>
               IF KW.B<1> = '' THEN
                    DELETE F.PROD.FIND, PROD.B<18>
               END ELSE
                    WRITE KW.B ON F.PROD.FIND, PROD.B<18>
               END
          END ELSE RELEASE F.PROD.FIND, PROD.B<18>
          READU KW.B FROM F.PROD.FIND, UPC ELSE KW.B = ''
          LOCATE PNO IN KW.B<1> SETTING VM ELSE KW.B<1,VM> = PNO
          WRITE KW.B ON F.PROD.FIND, UPC
          PROD.B<18> = UPC
     END
     RETURN


CHECK.EOM:
     FOR PRICE.INDEX = 46 TO 54
          IF NOT(NUM(LIB.B<PRICE.INDEX>)) THEN LIB.B<PRICE.INDEX> = 0
     NEXT PRICE.INDEX

     PROC.LOG = "CHECK.EOM:" ; GOSUB WRITE.PROC.LOG
     MD=10000
     CMD = 10000
     UOM = LIB.B<39>
     PUOM = 'EA'
     CUOM = LIB.B<CUSTOM.COST.UOM.ATT>
     IF CUOM = "EA" THEN CUOM = 'E'
     IF UOM = "EA" THEN UOM = 'E'
     BEGIN CASE
          CASE CUOM = 'E' ; CMD = 10000
          CASE CUOM = 'C' ; CMD = 100
          CASE CUOM = 'M' ; CMD = 10
     END CASE

     BEGIN CASE
          CASE UOM = 'E' ; MD = 10000 ; PUOM = 'EA'
          CASE UOM = 'C' ; MD = 100 ; PUOM = 'C'
          CASE UOM = 'M' ; MD = 10 ; PUOM = 'M'
     END CASE
     PROC.LOG = "CUSTOM.COST.UOM.ATT:":CUSTOM.COST.UOM.ATT:" "
     PROC.LOG := "UOM: ":UOM:" PUOM:":PUOM:" CUOM:":CUOM:" CMD:":CMD:" MD:":MD ; GOSUB WRITE.PROC.LOG
     *      COL   PROD  LIB
     *      C1    3     51
     *      C2    5
     *      C3    19
     *      C4    62    53 OR 54
     *      C5    120
     *      C6    122
     *      C7    124
     *      L1    13    47
     *      L2    64    48
     *      L3    66    49
     *      L4    126   46
     PROC.LOG = "CCA:":CUSTOM.COST.ATT:" LIB.B:":LIB.B<CUSTOM.COST.ATT> ; GOSUB WRITE.PROC.LOG
     PROC.LOG = "C4.OVERWRITABLE:":C4.OVERWRITABLE:" BEFORE C4:":PROD.B<62>
     PROD.B<3> = LIB.B<51> * MD
     PROD.B<13> = LIB.B<47> * MD
     PROD.B<64> = LIB.B<48> * MD
     PROD.B<66> = LIB.B<49> * MD
     PROD.B<126> = LIB.B<46> * MD
     IF PROD.B<126>+0 = 0 THEN PROD.B<126> = PROD.B<66>
     IF PROD.B<64>+0 = 0 THEN PROD.B<64> = PROD.B<66>
     IF PROD.B<13>+0 = 0 THEN PROD.B<13> = PROD.B<64>
     IF PROD.B<3>+0 = 0 THEN PROD.B<3> = PROD.B<62>+0
     PROD.B<7> = PUOM
     PROD.B<97> = 'EA'
     PROD.B<98> = 'EA'
     PROD.B<140> = 'EA'
     RETURN


UPDATE.MFGPNO:
     IF PROD.B<25> # MFGPNO & MFGPNO # '' THEN
          READU KW.B FROM F.PROD.FIND, PROD.B<25> THEN
               LOCATE PNO IN KW.B<1> SETTING VM THEN DEL KW.B<1,VM>
               IF KW.B<1> = '' THEN
                    DELETE F.PROD.FIND, PROD.B<25>
               END ELSE
                    WRITE KW.B ON F.PROD.FIND, PROD.B<25>
               END
          END ELSE RELEASE F.PROD.FIND, PROD.B<25>
          LOCATE PROD.B<25> IN PROD.B<73> SETTING VM THEN
               PROD.B<73,VM> = MFGPNO
          END
          PROD.B<25> = MFGPNO
          READU KW.B FROM F.PROD.FIND, MFGPNO ELSE KW.B = ''
          LOCATE PNO IN KW.B<1> SETTING VM ELSE KW.B<1,VM> = PNO
          WRITE KW.B ON F.PROD.FIND, MFGPNO
     END
     RETURN

LOOKUP.PRODUCT:
     PNO.T = ""
     PROD.B.T = ""
     IF I2.PIK THEN
          PNO.T = "!":I2.PIK
          READ PROD.B.T FROM F.PRODUCT,PNO.T ELSE
               READV PNO.T FROM F.PSVNO,I2.PIK,9 ELSE
                    SELECT.STR = "SELECT PRODUCT WITH PRICE.SRVC# = ":I2.PIK
                    EXECUTE SELECT.STR CAPTURING CAP
                    IF FIELD(CAP<2>," ",1) <> "0" THEN
                         READLIST PNOS THEN
                              PNO.T = PNOS<1,1>
                         END
                    END
               END
          END
     END ELSE
          IF UPC THEN
               READV I2.PIK.T FROM F.UPC.PSVNO.XREF,UPC,1 ELSE
                    READV PNO.T FROM F.I2.PRODUCT,I2.PIK,9 ELSE
                         READ PNO.T FROM F.PSVNO,UPC ELSE
                              SELECT.STR = "SELECT PRODUCT WITH UPC = ":UPC
                              EXECUTE SELECT.STR CAPTURING CAP
                              IF FIELD(CAP<2>," ",1) <> "0" THEN
                                   READLIST PNOS THEN
                                        PNO.T = PNOS<1,1>
                                   END
                              END
                         END
                    END
               END
          END
     END
     IF PNO.T THEN
          READ PROD.B.T FROM F.PRODUCT,PNO.T THEN
               PNO = PNO.T
               PROD.B = PROD.B.T
          END
     END

     RETURN



UPDATE.PSVNO:
     READV TEST FROM F.PSVNO,I2.PIK,1 ELSE WRITEV PNO ON F.PSVNO,I2.PIK,1
     READV TEST FROM F.UPC.PSVNO.XREF,UPC,1 ELSE
          WRITEV I2.PIK ON F.UPC.PSVNO.XREF,UPC,1
     END
     IF CATNUM THEN
          READ CATBUFF.O FROM F.CATVNO,MFG:CATNUM ELSE CATBUFF.O = ""
          CATBUFF = CATBUFF.O
          IF I2.PIK THEN
               CATBUFF<1> = I2.PIK
               CATBUFF<2> = "!":I2.PIK
          END
          IF UPC THEN CATBUFF<3> = UPC
          CATKEY = MFG:CATNUM
          IF CATBUFF.O <> CATBUFF THEN WRITE CATBUFF ON F.CATVNO,CATKEY
     END
     RETURN


     RETURN

WRITE.UPDATE.LOG.ENTRY:
     RUN.COUNT += 1

     WRITE XMISC ON F.I2.PRICE.UPDATE.LOG,LOG.ID

     RETURN

COMM.DATA: ***   Read and update COMMODITY DATA
     COMM.B = '' ; COUNT = 0 ; FIRST = TRUE
     EOF = FALSE
     LOOP
          READSEQ LINE FROM F.I2COMM THEN
               IF FIRST THEN FIRST = FALSE ELSE
                    CONVERT DELIMITER TO CHAR(253) IN LINE
                    COMM.PIK = LINE<1,1>
                    COMM.CODE= LINE<1,4>
                    COMM.DESC= LINE<1,5>
                    COUNT = COUNT + 1
                    COMM.B<1,COUNT> = COMM.PIK
                    COMM.B<2,COUNT> = COMM.CODE
                    COMM.B<3,COUNT> = COMM.DESC
               END
          END ELSE EOF = TRUE
     UNTIL EOF DO REPEAT
     WRITE COMM.B ON F.CONTROL, 'I2.COMM'
     PROC.LOG = "WRITING ":COUNT:" RECORDS TO I2.COMM IN CONTROL FILE" ; GOSUB WRITE.PROC.LOG
     RETURN

UPDATE.VEND:

     *LOCATE MFGUCC IN XMISC<23> BY 'AL' SETTING VL THEN
     *XMISC<24,VL> += 1
     *XMISC<25,VL> += UPDATE.SOURCE.CT2
     *XMISC<26,VL> += UPDATE.LIB.CT2
     *XMISC<27,VL> += UPDATE.PRODUCT.CT2
     *XMISC<28,VL> += ERROR.STATUS
     *END ELSE
     *INS MFGUCC BEFORE XMISC<23,VL>
     *INS 1 BEFORE XMISC<24,VL>
     *INS UPDATE.SOURCE.CT2 BEFORE XMISC<25,VL>
     *INS UPDATE.LIB.CT2 BEFORE XMISC<26,VL>
     *INS UPDATE.PRODUCT.CT2 BEFORE XMISC<27,VL>
     *INS ERROR.STATUS BEFORE XMISC<28,VL>
     *INS '' BEFORE XMISC<29,VL>
     *END
     *
     *LOCATE MFGUCC IN VEND.B<1> BY 'AL' SETTING VM ELSE
     *FOR I = 1 TO 10
     *INS '' BEFORE VEND.B<I,VM>
     *NEXT I
     *VEND.B<1,VM> = MFGUCC
     *VEND.B<2,VM> = MFG
     *VEND.B<3,VM> = MFG.NAME
     *END
     RETURN

UPDATE.COMM:
     LOCATE COMM.PIK IN VEND.B<4,VM> BY 'AR' SETTING SVM ELSE
          INS COMM.PIK BEFORE VEND.B<4,VM,SVM>
          INS '' BEFORE VEND.B<5,VM,SVM>
     END
     IF PREV.COMM # COMM.PIK THEN
          PREV.COMM = COMM.PIK
          LOCATE COMM.PIK IN COMM.B<1> SETTING VM THEN COMM.DESC = COMM.B<3,VM> ELSE COMM.DESC = COMM.PIK
     END
     RETURN

PRICE.SOURCE:
     *XMISC<17,DATE.LOC> = TOTAL.COUNT
     *XMISC<18,DATE.LOC> = UPDATE.SOURCE.CT2
     *XMISC<21,DATE.LOC> = ERROR.COUNT
     *XMISC<22,DATE.LOC> = UPDATE.SCOPE
     *XMISC<30> = TOTAL.COMPLETED.DATE

     *        WRITE XMISC ON F.I2.PRICE.UPDATE.LOG,LOG.ID

     RETURN



CHECK.ERRORS:
     IF F # '' THEN
          PROC.LOG = F
          GOSUB WRITE.PROC.LOG
          PRINT FILE.ERRORS:F:BCK: ; INPUT NL
          IF NL = 'D' THEN DEBUG
          IF NL = 'E' THEN GOTO FINISHED:
     END
     RETURN

WRITE.PROC.LOG:
     *IF LM THEN PRINT PROC.LOG
     IF PROC.LOG.MODE THEN
          LOG.CT += 1
          CONVERT @FM TO "|" IN PROC.LOG
          BEFORE.NOW = NOW
          NOW = DATE()
          DELTA = NOW - BEFORE.NOW
          LOG.ENTRY = NOW:":":DELTA:" ":LOG.CT:"|":UPDATEID:"-":FILE.NAME:"|":PROC.LOG
          PROC.LOG = ""
          *IF LM THEN PRINT LOG.ENTRY
          WRITESEQ LOG.ENTRY ON IPUTL ELSE PRINT 'UNABLE TO WRITE TO PROCESS LOG':IPUTL ; STOP
          BEFORE.NOW = NOW
          IF INDEX(PROC.LOG,"FATAL",1) THEN
               ERROR.STATUS = 1
               ERROR.COUNT = ERROR.COUNT + 1
               WRITESEQ "*** FATAL ERROR ENCOUNTERED.  UPDATE FILE ":UPDATEID:" SKIPPED." ON IPUTL ELSE PRINT 'UNABLE TO WRITE TO PROCESS LOG':IPUTL
               *DEBUG
          END
     END
     RETURN



DISPLAY.PROGRESS:
     NOW.TIME = TIME()
     ITEMS.DONE=UPDATE.SOURCE.CT1
     IF UPDATE.TYPE='R' THEN ITEMS.DONE=UPDATE.PRODUCT.CT1
     TIME.ELAPSED = NOW.TIME-START.TIME
     PRINT @(1,16):OCONV(UPDATE.SOURCE.CT1,'MD0,') "R#9":"    ":OCONV(UPDATE.LIB.CT1,'MD0,') "R#9":"      ":OCONV(UPDATE.PRODUCT.CT1,'MD0,') "R#9":
     PRINT @(1,17):OCONV(UPDATE.SOURCE.CT2,'MD0,') "R#9":"  : ":OCONV(UPDATE.LIB.CT2,'MD0,') "R#9":"      ":OCONV(UPDATE.PRODUCT.CT2,'MD0,') "R#9":
     IF T.TOTAL THEN PRINT @(1,19):"TOTAL ITEMS:":OCONV(T.TOTAL,"MD0,") "R#9   ":
     IF T.TOTAL > 0 THEN
          DONE = OCONV(((ITEMS.DONE/T.TOTAL)*1000),'MD1')
          TIM.EXPECTED = (TIME.ELAPSED*T.TOTAL)/ITEMS.DONE
          TIM.REM = (TIM.EXPECTED-TIME.ELAPSED)
          PRINT DONE:" %    ":"ELAPSED: ":OCONV(TIME.ELAPSED,'MD0,'):" SEC   REMAINING: ":TIM.REM
          SEC.REM = TIM.REM
          HOURS.REM = INT(SEC.REM/3600)
          SEC.REM = SEC.REM - HOURS.REM*3600
          MIN.REM = INT(SEC.REM/60)
          SEC.REM = SEC.REM - MIN.REM*60
          SEC.REM = INT(SEC.REM)
          PRINT "REMAINING TIME: HOURS:":HOURS.REM:"  MIN:":MIN.REM:" SEC:":SEC.REM
     END
     RETURN




