!BP AR.AGING.STATS                            >AR AGING STATISTICS
!                                              (Display Aging Chart)
!
!CREATED BY> LEON SHOYKHETBROD           DATE >11/21/94
!CHANGED BY>
!CLIENT    > WAL-RICH CORP.                   >
!
!
!
!-------------------------------------------------------------------------
!
! COMMON CELL BLOCK
!
COMMON CTRL.F, CUST.F, LETT.F, FILES(5), FNAME.B, ATTRIBUTE
COMMON INVLIST, SEL.B
!
$INCLUDE UTLSBP ASSIGN.VAR
!
MAT FILES = ''
EQU AR30.F TO FILES(1)
EQU AR60.F TO FILES(2)
EQU AR90.F TO FILES(3)
EQU AROV.F TO FILES(4)
EQU ARPASTDUE.F TO FILES(5)
!
FNAME.B = ''
FNAME.B<1> = 'AR-AGING,0-30'
FNAME.B<2> = 'AR-AGING,31-60'
FNAME.B<3> = 'AR-AGING,61-90'
FNAME.B<4> = 'AR-AGING,OVER-90'
FNAME.B<5> = 'AR-AGING,ALL-PAST-DUE'
!
! OPEN FILES
!
F = ''
OPEN 'CONTROL'          TO CTRL.F   ELSE F=F:', CONTROL'
OPEN 'CUSTOMER'         TO CUST.F   ELSE F=F:', CUSTOMER'
OPEN 'AR-AGING-LETTERS' TO LETT.F   ELSE F=F:', AR-AGING-LETTERS'
OPEN 'OPEN-ITEMS'       TO OI.F     ELSE F=F:', OPEN-ITEMS'
CTR = 0
LOOP
CTR = CTR + 1
WHILE CTR LE 5 DO
OPEN FNAME.B<CTR>           TO FILES(CTR) ELSE F=F:', ':FNAME.B<CTR>
REPEAT
!
IF TRIM(F) # '' THEN PRINT FILE.ERRORS:F:BCK:; INPUT NL,1:_; STOP
!
READ COMP.B FROM CTRL.F, 'COMPANY' ELSE COMP.B = ''
AR.AGE.MONTHLY = COMP.B<36>
IF COMP.B<22> = 'D' THEN ATTR = 8 ELSE ATTR = 2
IF AR.AGE.MONTHLY = 'Y' THEN CALL AR.MONTHLY.AGE.DATES (AGE.B, '')
!
PRGID = 'AR.AGING.STATS'; TITLE = 'AR AGING STATISTICS'
XMISC = TITLE
CALL READFILE (CTRL.F, PRGID, CTRL.B, XMISC)
IF XMISC<2> THEN RQM; RQM; STOP
!
! SCREEN
!
CALL SCREENTITLE (TITLE, NEW.TITLE, COL)
SCR = CLS
SCR = SCR:@(COL,0):WIDE:NEW.TITLE
PRINT SCR
!
! CONTINUE ?
!
RESP = ''
LOOP
PRINT C22:@(18):'LOAD AR AGING STATISTICS ? (Y)es/(N)o - [ ]':
XMISC=''; CALL CIP(59,22,1,'',RESP,ESCFLG,XMISC)
UNTIL RESP = 'Y' ! RESP = 'N' ! ESCFLG DO
REPEAT
IF RESP = 'N' OR ESCFLG THEN STOP
!
! MAIN LOOP
!
PRINT C22:DMON:'Loading OPEN-ITEMS...':DMOFF:
CTR = 0
LOOP
CTR = CTR + 1
WHILE CTR LE 5 DO
CLEARFILE FILES(CTR)
REPEAT
TOT.B = ''; CTR = 0
EOF = FALSE
SELECT OI.F
LOOP
READNEXT ID THEN
READ OI.B FROM OI.F, ID THEN
*** BETTY FROM PUMPS & CONTROL NEEDS DISPUTED
*L*            IF OI.B<41> = '' THEN
CTR = CTR + 1
IF MOD(CTR,100) ELSE PRINT @(23,22):OCONV(CTR,'MD,'):
CUSTNO = OI.B<1>
INVAMT = OI.B<3>; AMTPAID = 0; PCT = 0
ITEMS = DCOUNT(OI.B<5>,IVM)
LOOP
PCT = PCT + 1
WHILE PCT LE ITEMS DO
AMTPAID = AMTPAID + OI.B<4,PCT> + OI.B<6,PCT> + OI.B<7,PCT>
REPEAT
AMTDUE = INVAMT - AMTPAID
IF AMTDUE + 0 # 0 THEN
DAYS = DATE() - OI.B<ATTR>
IF AR.AGE.MONTHLY = 'Y' THEN
BEGIN CASE
CASE OI.B<ATTR> LE AGE.B<1> & OI.B<ATTR> GT AGE.B<2>; I = 2
CASE OI.B<ATTR> LE AGE.B<2> & OI.B<ATTR> GT AGE.B<3>; I = 3
CASE OI.B<ATTR> LE AGE.B<3>; I = 4
CASE 1; I = 1
END CASE
GO 7500
END
BEGIN CASE
CASE DAYS LE 30              ; I = 1
CASE DAYS GT 30 & DAYS LE 60 ; I = 2
CASE DAYS GT 60 & DAYS LE 90 ; I = 3
CASE 1                       ; I = 4
END CASE
7500        END
OI.B<6> = AMTDUE
OI.B<7> = AMTPAID
WRITE OI.B ON FILES(I), ID
IF I # 1 THEN
WRITE OI.B ON FILES(5), ID
END
TOT.B<1,I> = TOT.B<1,I> + AMTDUE
*L*            END
END
END ELSE EOF = TRUE
UNTIL EOF DO
REPEAT
DISPLAY = TOT.B
CTR = 0
LOOP
CTR = CTR + 1
WHILE CTR LE 4 DO
DISPLAY<1,CTR> = OCONV(DISPLAY<1,CTR>,'MD2')
REPEAT
DISPLAY<4,1> = 'CURRENT'; DISPLAY<4,2> = '30 DAYS'
DISPLAY<4,3> = '60 DAYS'; DISPLAY<4,4> = '90&OVER'
DISPLAY<2,1> = 'CURNT'; DISPLAY<2,2> = '30DAYS'
DISPLAY<2,3> = '60DAYS'; DISPLAY<2,4> = '90+'
GOSUB 5000 ;* DISPLAY INFO
VALUE = 1; ROW.B = ''
ROW.B<1,1> = 4; ROW.B<1,2> = 9; ROW.B<1,3> = 14; ROW.B<1,4> = 19
1000 ***
PRINT C22:@(8):DMON:'Select ':DMOFF:'Option Number ':DMON:'for Detail':
XMISC=''
CALL CIP(2,22,3,'',SEL,ESCFLG,XMISC)
IF SEL = 'E' OR ESCFLG THEN STOP
IF SEL = '' THEN SEL = VALUE
IF SEL = 1 ! SEL = 2 ! SEL = 3 ! SEL = 4 ! SEL = 5 ELSE PRINT BELL:; GO 1000
VALUE = SEL; ATTRIBUTE = VALUE
CALL AR.AGING.STATS.2
PRINT SCR; GOSUB 5000
GO 1000
!
! DISPLAY INFO
!
5000  DATA ''
CALL CHART (30, 2, 44, 17, 'A/R AGING','BREAKDOWN','Period Statistics',DISPLAY)
CTR = 0; XCLM = 2
LOOP
CTR = CTR + 1
WHILE CTR LE 4 DO
PRINT @(1,CTR+XCLM):ULA:STR(HORZLN,3):URA
PRINT SPACE(1):VERTLN:DLON:' ':CTR:' ':DLOFF:VERTLN:
PRINT SPACE(2):DMON:DISPLAY<4,CTR> 'R#7 ':'= ':OCONV(TOT.B<1,CTR>,'MD2,'):DMOFF
PRINT SPACE(1):LLA:STR(HORZLN,3):LRA
XCLM = XCLM + 2
REPEAT
PRINT @(1,15):ULA:STR(HORZLN,3):URA
PRINT SPACE(1):VERTLN:DLON:' ':5:' ':DLOFF:VERTLN:
PRINT SPACE(2):DMON:'ALL PAST DUE':DMOFF
PRINT SPACE(1):LLA:STR(HORZLN,3):LRA
RETURN
