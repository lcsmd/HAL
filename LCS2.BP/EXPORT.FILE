      PROGRAM EXPORT.FILE
      FN = FIELD(@SENTENCE," ",2)
      MODE = FIELD(@SENTENCE," ",3)
      GLIST = FIELD(@SENTENCE," ",4)
      OPEN FN TO F.FILE ELSE PRINT FN:" not found";STOP
      OPEN 'DICT',FN TO D.FILE ELSE PRINT "DICT,":FN:" not found":;STOP
      OPENSEQ "FLAT.CSV",FN:".CSV" TO F.SEQ ELSE CREATE F.SEQ ELSE PRINT FN:".CSV not opened"; STOP
      OPENSEQ "FLAT.CSV","D_":FN:".CSV" TO D.SEQ ELSE CREATE D.SEQ ELSE PRINT "D_":FN:".CSV not opened"; STOP
      TAB = CHAR(9)
      CT=0
      SELECT.STR = 'SELECT DICT ':FN:' WITH TYPE = "D""I"'
      EXECUTE SELECT.STR CAPTURING CAP
      READLIST DIS THEN
         IF DIS THEN
            DC = DCOUNT(DIS,@FM)
            HEAD = ""
            CONVS = ""
            FOR X = 1 TO DC
               DI =  DIS<X>
               READ DBUFF FROM D.FILE,DI THEN
                  IF NUM(DBUFF<2>) AND DBUFF<2> > 0 THEN
                     HEAD<DBUFF<2>> = DI
                  END
                  IF DBUFF<3> THEN
                     DICT.ATT=DBUFF<2>
                     CON.FACTOR = DBUFF<3>
                     IF NUM(DICT.ATT) THEN
                        CONVS<DICT.ATT> = CON.FACTOR
                        PRINT "ATT:":DICT.ATT
                     END
                  END
               END
            NEXT X
            D.ALL = 'PH'
            D.ALL<2> = CHANGE(HEAD,@FM," ")
            WRITE D.ALL ON D.FILE,"@ALL"
            WRITESEQ FN:".KEY":TAB:CHANGE(HEAD,@FM,TAB) ON F.SEQ ELSE PRINT "WRITE";STOP
            IF (MODE = "DICT" OR MODE = "") THEN
               SELECT.STR = "SSELECT DICT ":FN:" WITH TYPE = D OR TYPE = I"
               EXECUTE SELECT.STR CAPTURING CAP
               LOOP WHILE READNEXT AA DO
                  READ DBUFF FROM D.FILE,AA THEN
                     DBUFF2 = ""
                     FOR AX = 1 TO 15
                        DBUFF2<AX> = DBUFF<AX>
                     NEXT AX
                     DBUFF = DBUFF2
                     DBUFF = CHANGE(DBUFF,@FM,TAB)
                     WRITESEQ AA:TAB:DBUFF ON D.SEQ ELSE PRINT "WRITE";STOP
                     CT+=1
                     PRINT CT "L#10":AA
                  END
               REPEAT
               CLOSESEQ D.SEQ
            END
            IF (MODE = "DATA" OR MODE = "") THEN
               IF GLIST THEN EXECUTE "GET-LIST ":GLIST
               SELECT F.FILE
               LOOP WHILE READNEXT AA DO
                  READ BUFF FROM F.FILE,AA THEN
                     FOR CX = 1 TO DCOUNT(BUFF,@FM)
                        IF CONVS<CX> THEN BUFF<CX> = OCONV(BUFF<CX,1,1>,CONVS<CX>)
                     NEXT CX
                     BUFF = CHANGE(BUFF,@FM,TAB)
                     BUFF = CHANGE(BUFF,@VM,"^|")
                     BUFF = CHANGE(BUFF,@SVM,"^~")

                     WRITESEQ AA:TAB:BUFF ON F.SEQ ELSE PRINT "WRITE";STOP
                     CT+=1
                     IF NOT(MOD(CT,100)) THEN PRINT CT "L#10":AA
                  END
               REPEAT
               CLOSESEQ F.SEQ
            END
         END
      END

