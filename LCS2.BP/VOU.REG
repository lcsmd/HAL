     !BP VOU.REG                                >VOUCHER REGISTER JOURNAL
     $INCLUDE UTLSBP ASSIGN.VAR
     !
     ! READ DATES FROM PROC BUFFER
     !
     PROCREAD BUFFER ELSE
          PRINT CLS
          PRINT @(10,10):BELL:'THIS PROGRAM MUST RUN FROM PROC !, ':
          PRINT 'HIT RETURN ':
          INPUT NL,1_
          STOP
     END
     SDT = OCONV(BUFFER,'G 1')
     EDT = OCONV(BUFFER,'G1 1')
     BRANCH = OCONV(BUFFER,'G2 1')
     IF BRANCH = '' THEN
          PERIOD = SDT
          BRANCH = EDT
          EDT = ''
     END
     !
     ! OPEN FILES
     !
     F = ''
     OPEN '','AP'        TO AP.F   ELSE F=F:', AP'
     OPEN '','CADF'      TO CADF.F ELSE F=F:', CADF'
     OPEN 'CONTROL' TO CTRL.F ELSE F = F:', CONTROL'
     OPEN 'LCS.CONTROL' TO LCS.CONTROL ELSE PRINT 'LCS.CONTROL';STOP
     OPEN 'VM.CATEGORY' TO F.VM.CATEGORY ELSE F = F:', VM.CATEGORY'
     !
     IF TRIM(F) # '' THEN PRINT FILE.ERRORS:F:BCK:; INPUT NL,1_; STOP
     !
     READ COMP.B FROM CTRL.F, 'COMPANY' ELSE COMP.B = ''
     !
     ! FOR ALL G/L ACCOUNTS OR SPECIFIC
     !
1    TYPE.B = ''
     TYPE.B<1,1> = 'All Purchases...'
     TYPE.B<1,2> = 'Specific G/L Account'
     TYPE.B<1,3> = 'Specific Vendor Category'
     XMISC = 'Report Type'
     CALL SELECTOR (TYPE.B, XMISC)
     IF XMISC<2> = '' THEN STOP
     TYPE = XMISC<2>
     IF TYPE = 2 THEN
          CALL GL.SELECTOR (CADF.F, GLNO)
          IF GLNO = '' THEN STOP
     END
     !
     IF TYPE = 3 THEN
100       PRINT @(25,13):DMON:'Vendor Category':DMOFF
          CALL CIP (41, 13, 5, '', CATG, ESCFLG, '')
          IF ESCFLG THEN GO 1
          IF CATG = '' THEN GO 100
          READV VCDESC FROM F.VM.CATEGORY, CATG, 1 ELSE GO 100
          IF VCDESC = '' THEN VCDESC = CATG
          PRINT @(41,13):VCDESC 'L#15':
     END
     SORT.B = ''
     SORT.B<1,1> = 'By Invoice Date'
     SORT.B<1,2> = 'By Vendor'
     XMISC = 'Sorting'; XMISC<3> = 2
     CALL SELECTOR (SORT.B, XMISC)
     IF XMISC<2> = '' THEN GO 1
     SORT = XMISC<2>
     !
     ! SUPPRESS NON VENDOR A/P (*MANUAL*) ?
     !
     LOOP
          PRINT C22:@(10):'Suppress NON VENDOR A/P (*MANUAL*) ? (Y)es':
          PRINT '/(N)o   [N]':STR(BS,2):
          INPUT NONAP,1:
          IF NONAP = '' THEN NONAP = 'N'
     UNTIL NONAP = 'Y' ! NONAP = 'N' DO REPEAT
     PRINT C22:
     !
     ! OPEN PRINTER JOB
     !
     PMISC = 'O'
     PMISC<2> = 'N'
     PMISC<3> = 'Y'
     CALL PRINTER.CONTROL (CTRL.F, DEVICE, ESCFLG, PMISC)
     IF ESCFLG THEN STOP
     !
     ! BUILD COMMAND
     !
     IF EDT # '' THEN
          CMND = 'SORT AP WITH INV.DATE GE "':OCONV(SDT,'D2/'):'" '
          CMND = CMND:'AND WITH INV.DATE LE "':OCONV(EDT,'D2/'):'" '
     END ELSE CMND = 'SORT AP WITH APMON = "[':PERIOD:']" '
     IF BRANCH # '*' THEN CMND = CMND:'AND WITH BRANCH "':BRANCH:'" '
     IF TYPE = 2 THEN CMND = CMND:'AND WITH GL.ACCT = "':GLNO:'" '
     IF TYPE = 3 THEN CMND = CMND:'AND WITH VM.CATEGORY = "':CATG:'" '
     IF NONAP = 'Y' THEN CMND = CMND:'AND WITH VENDOR # "*MANUAL*" '
     IF SORT = 1 THEN CMND = CMND:'BY INV.DATE ' ELSE
          IF COMP.B<21> = 'N' THEN CMND = CMND:'BY NAME ' ELSE
               CMND = CMND:'BY VENDOR '
          END
     END
     IF SORT = 2 THEN
          CMND = CMND:'BREAK-ON VNAME.OR.MANUAL ':\"'VU'" \
     END ELSE CMND = CMND:'VNAME.OR.MANUAL '
     *CMND = CMND:'INVOICE '
     **IF SORT = 1 THEN CMND = CMND:'BREAK-ON INV.DATE ':\"'VU'" \ ELSE
          *CMND = CMND:'INV.DATE '
     *END
     CMND = CMND:'TOTAL ORG.AMT DET-SUPP '
     *CMND = CMND:'GL.ACCT TOTAL GL.AMT '
     CMND = CMND:\GRAND-TOTAL "'U'VOUCHER REGISTER GRAND TOTALS :" \
     CMND = CMND:'ID-SUPP'
     CMND = CMND:\ HEADING "'T'   PAGE 'PCLL'\
     CMND = CMND:COMP.B<1>:"'CLL'"
     IF EDT = '' THEN
          CMND = CMND:\ M O N T H L Y   A / P   P U R C H A S E   J O U R N A L \
     END ELSE
          CMND = CMND:\ A / P    P U R C H A S E    J O U R N A L \
     END
     CMND = CMND:\'CLL'\
     IF BRANCH # '*' THEN
          BTITLE = 'FOR BRANCH CODE :':BRANCH
          CALL DOUBLESPACE (BTITLE, NTITLE)
          CMND = CMND:' ':NTITLE:" 'CLL'"
     END
     IF EDT = '' THEN
          CMND = CMND:\ FOR PERIOD OF \:SDT
     END ELSE
          CMND = CMND:\ FOR PERIOD OF \:OCONV(SDT,'D'):' TO ':OCONV(EDT,'D')
     END
     CMND = CMND:\'CLL'\
     IF TYPE = 2 THEN
          READV DESC FROM CADF.F, GLNO,1 ELSE DESC = ''
          CMND = CMND:\ FOR G/L ACCOUNT \:GLNO 'L####-##-##  ':DESC
          CMND = CMND:\'CLL'\
     END
     IF TYPE = 3 THEN
          CMND = CMND:\ FOR VENDOR CATEGORY \:CATG:' - ':VCDESC
          CMND = CMND:\'CLL'\
     END
     CMND = CMND:'" (P'
     PRINT C23:'Selecting A/P file, Please Wait...':
     EXECUTE CMND
     WRITEV CMND ON LCS.CONTROL,"VOU.REG",1
     !
     ! CLOSE PRINT JOB
     !
     PMISC<1> = 'C'
     CALL PRINTER.CONTROL (CTRL.F, DEVICE, '', PMISC)

