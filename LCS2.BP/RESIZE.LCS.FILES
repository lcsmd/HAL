     INCLUDE UTLSBP ASSIGN.VAR
     FILE.NAMES = TRIM(CHANGE(@SENTENCE,"RESIZE.LCS.FILES",""))
     IF (INDEX(FILE.NAMES,"?",1) OR  FILE.NAMES = "") THEN
          GOSUB HELP:
          STOP
     END ELSE
          REC = CHANGE(FILE.NAMES," ",@FM)
          ITEMS = DCOUNT(REC,@FM); CTR = 0
          LOOP
               CTR = CTR + 1
          WHILE CTR LE ITEMS DO
               FILE = REC<CTR>
               IF FILE[1,1] # '*' THEN
                    PRINT 'Hashing file  ':FILE:
                    EXECUTE 'HASH.HELP ':FILE CAPTURING OUTPUT
                    OUTLINES = DCOUNT(OUTPUT,IAMC)
                    ORIG.MODULO = ''; ORIG.SEPARATOR = ''; ORIG.FILE.TYPE = ''
                    MODULO = ''; SEPARATOR = ''; FILE.TYPE = ''
                    FOR I = 1 TO OUTLINES
                         LINE = TRIM(OCONV(OUTPUT<I>,'MCU'))
                         MODFLG = FALSE; SEPFLG = FALSE; TYPEFLG = FALSE
                         BEGIN CASE
                              CASE INDEX(LINE,'THE SMALLEST MODULO',1); MODFLG = TRUE
                              CASE INDEX(LINE,'THE SMALLEST SEPARATION',1); SEPFLG = TRUE
                              CASE INDEX(LINE,'THE BEST TYPE',1); TYPEFLG = TRUE
                         END CASE
                         IF MODFLG ! SEPFLG ! TYPEFLG THEN
                              LINE = CHANGE(LINE,' ',IVM)
                              VALUES = DCOUNT(LINE,IVM)
                              INFO = LINE<1,VALUES>
                              INFO = CHANGE(INFO,'.','')
                              IF NUM(INFO) THEN
                                   IF MODFLG THEN MODULO = INFO ELSE
                                        IF SEPFLG THEN SEPARATOR = INFO ELSE FILE.TYPE = INFO
                                   END
                              END
                         END
                         IF INDEX(LINE,'TYPE=',1) & INDEX(LINE,'MODULO=',1) & INDEX(LINE,'SEP=',1) THEN
                              LINE = CHANGE(LINE,'= ','=')
                              LINE = CHANGE(LINE,' ',IVM)
                              ORIG.FILE.TYPE = OCONV(LINE<1,3>,'G1=1')
                              ORIG.MODULO = OCONV(LINE<1,4>,'G1=1')
                              ORIG.SEPARATOR = OCONV(LINE<1,5>,'G1=1')
                         END
                    NEXT I
                    IF MODULO & SEPARATOR & FILE.TYPE THEN
                         PRINT
                         PRINT 'Resizing file ':FILE
                         PRINT 'From (Modulo: ':ORIG.MODULO:', Separator: ':ORIG.SEPARATOR:', File Type: ':ORIG.FILE.TYPE:')'
                         PRINT 'To   (Module: ':MODULO:', Separator: ':SEPARATOR:', File Type: ':FILE.TYPE:')'
                         EXECUTE 'RESIZE ':FILE:' ':FILE.TYPE:' ':MODULO:' ':SEPARATOR
                    END
                    PRINT; PRINT
               END
          REPEAT
     END
     STOP

HELP:
     PRINT "COMMAND SYNTAX:"
     PRINT "RESIZE.LCS.FILES <FILE1 FILE2 FILE3...>"
     RETURN
