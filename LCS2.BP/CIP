      SUBROUTINE CIP (COL, ROW, VLEN, VAR, NEW.VAR, ESCFLG, XMISC)
      !
      !PROGRAM CIP                                 >CUSTOM INPUT SUBROUTINE
      !
      !CREATED BY> LEON SHOYKHETBROD          DATE >11/09/93
      !CHANGED BY>
      !CLIENT    >
      !
      !COMMENTS  > Program is designed for special line editing. Easy access of
      !            arrow keys on most terminals.
      !
      !            Special features are :
      !            ====================
      !
      !               ctrl+B = MOVE BACK TO THE BEG.
      !               ctrl+D = DELETE A CHARACTER.
      !               ctrl+E = SKIP TO END OF LINE.
      !               ctrl+F = MOVE THE CURSOR FORWARD.
      !               ctrl+I = INSERT A BLANK CHARACTER.
      !               ctrl+K = KILL ALL TEXT TO END OF LINE.
      !               ctrl+O = DISPLAY OPTIONS AVAILABLE.
      !               ctrl+X = CANCEL ANY CHANGES & RESTORE ORIGINAL VALUE.
      !
      !VARIABLE  > DEFINITIONS:
      !
      !                     COL        = COLUMN
      !                     ROW        = ROW
      !                     VLEN       = VARIABLE LENGTH
      !                     VAR        = ORIGINAL VARIABLE
      !                     NEW.VAR    = NEW VARIABLE
      !                     ESCFLG = ESCAPE ? (TRUE OR FALSE)
      !                     XMISC      = MISC. ARRAY
      !                                  ELEMETS USED:
      !                                     <1,1> = BORDER MASKING ( [...] )
      !                                     <1,2> = SPECIAL MASK
      !                                     <2,1> = GO UP ONE LINE FLAG
      !                                     <2,2> = GO DOWN ONE LINE FLAG
      !                                     <2,3> = MOVE LEFT
      !                                     <2,4> = MOVE RIGHT
      !                                     <2,5> = DELETE CHAR
      !                                     <2,6> = DELETE LINE
      !                                     <2,7> = INSERT CHAR OR LINE
      !                                     <3,1> = UP ONE LINE USED
      !                                     <3,2> = DOWN ONE LINE USED
      !                                     <3,3> = MOVE LEFT USED
      !                                     <3,4> = MOVE RIGHT USED
      !                                     <3,5> = DELETE CHAR USED
      !                                     <3,6> = DELETE LINE USED
      !                                     <3,7> = INSERT CHAR OR LINE USED
      !                                   ATTR<3> = IS RETURNED TO CALLING PROG.
      !                                       <4> = ACTIVE FUNCTION KEYS
      !                                       <5> = FKEY DEFINITIONS
      !                                       <6> = FKEY VALUE USED
      !                                       <7> = ECHO OFF
      !                                       <8> = END OF LINE BELL NO <CR> RTN
      !                                       <9> = TOGGLE EXTERNAL SUBROUTINE
      !                                        9,2= COLUM
      !                                        9,3= ROW
      !                                       <10>= ROW TO DISPLAY FKEYS
      !                                             DEFAULT TO 21
      !                                       <11>=IGNORE '/' ',' '-' ON MASKING
      !                                       <12>=BYPASS CHARACTER
      !                                       <13>=FKEY VIDEO ATTRIBUTE
      !                                       <14>=RETURNS 1 IF SCANNED WITH
      !                                            A "~" OR "`" PREAMBLE
      !                                       <15>=0/'' TO PRINT SCANNED ITEMS
      !                                            1 ECHO OFF
      !                                       <16>=0/'' ERASE FKEY LINES AFTER
      !                                            ENTRY: UP, DOWN, LEFT, RIGHT
      !                                            1 DO NOT ERASE
      !                                       <17>=0/'' JUSTIFY FKEY LENGTH OF 9
      !                                            1 DO NOT JUSTIFY
      !                                       <18>= POINTER POSITION
      !                                       <19>= NO INSTANT MESSAGING
      !                                       <20>= ALLOW REALIGN AFTER
      !                                             DEL/INS CHAR.
      !                                       <21>= RETURN I/D TO REALIGN
      !                                       <22>= RETURN POINTER POSITION
      !                                       <23>= DO NOT BACKOUT TRAILING SPACES
      !                                       <24>= DO NOT HIGHLITE TEXT
      !                                       <25>= RETURN TO CALLING PROGRAM
      !                                             IN X NUMBER OF SECONDS
      !
      !---
      INCLUDE ASSIGN.VAR
      !
      ESCFLG = FALSE
      !
      ! CHECK FOR STACK ON
      !
      STACKON = FALSE
      IF SYSTEM(10) THEN
         ECHO OFF
         INPUT NEW.VAR:
         ECHO ON
         STACKON = TRUE
         GO 300
      END
      IF XMISC<1,1> = '' THEN XMISC<1,1> = 2
      !
      IF XMISC<4> # '' THEN
         IF XMISC<13> = '' THEN VAON = DMON ELSE
            BEGIN CASE
               CASE XMISC<13> = 1; VAON = LON
               CASE XMISC<13> = 2; VAON = DLON
               CASE 1; VAON = ''
            END CASE
         END
         BTM2 = ''
         IF XMISC<10> # '' THEN BTM = @(0,XMISC<10>):EOL ELSE BTM = C21
         FKEYS = DCOUNT(XMISC<4>,IVM)
         IF FKEYS GT 6 THEN BTM = C22; BTM2 = C23
         FKEYDISP = ''
         PRINT BTM:VAON:
         CTR = 0
         LOOP
            CTR = CTR + 1
         WHILE XMISC<4,CTR> # '' DO
            IF CTR = 7 THEN
               PRINT FKEYDISP:VAOFF:
               PRINT BTM2:VAON:
               FKEYDISP = ''
            END
            IF XMISC<17> THEN
               JUST = 'L#':LEN(XMISC<5,CTR>)+1
            END ELSE
               IF XMISC<4,CTR> GT 9 THEN JUST = 'L#8 ' ELSE JUST = 'L#9 '
            END
            IF VAON = DMON THEN
               FKEYDISP = FKEYDISP:VAOFF:"F":XMISC<4,CTR>:VAON:'-':XMISC<5,CTR> JUST
            END ELSE
               FKEYDISP = FKEYDISP:LON:"F":XMISC<4,CTR>:LOFF:DLON:'-':XMISC<5,CTR> JUST:DLOFF
            END
         REPEAT
         PRINT FKEYDISP:VAOFF:
      END
      !
      EQU RIGHT1     TO 6
      EQU RIGHT2     TO 12
      EQU RIGHT3     TO 18
      EQU LEFT1      TO 21
      EQU BACK       TO 2
      EQU BACK.SPACE TO 8
      EQU ICHAR      TO 14
      EQU ICHAR2     TO 9
      EQU DCHAR      TO 4
      EQU KILL.EOL   TO 127
      EQU UPLN       TO 11
      EQU UPLN2      TO 26
      EQU CANCEL     TO 24
      EQU SKIP.EOL   TO 5
      EQU HELP       TO 15
      EQU CR         TO 13
      EQU DWLN       TO 10
      EQU ESCAPE     TO 27
      EQU HOME       TO 30
      !
      INTG.B = 0:IVM:1:IVM:2:IVM:3:IVM:4:IVM:5:IVM:6:IVM:7:IVM:8:IVM:9
      !
      ! SKIP MASKING FIELDS
      !
      SKM.B = '/ý,ý-ý'
      IF XMISC<11> = 'Y' THEN SKM.B = ''
      STARTING.POINTER = XMISC<18>
      IF NOT(STARTING.POINTER) THEN STARTING.POINTER = 1
      NEW.VAR = ''
      BYPASS = FALSE
      !
      ! CHECK IF FIELD IS (DATE/TIME/PHONE/ZIP/SS#) BY SPECIAL MASK
      !
      DTFLG = FALSE; TMFLG = FALSE; TLFLG = FALSE; ZFLG = FALSE
      SSFLG = FALSE
      IF XMISC<1,2> # '' THEN
         IF LEN(XMISC<1,2>) = 8 THEN
            IF (XMISC<1,2>[3,1]:XMISC<1,2>[6,1]) = '//' THEN DTFLG = TRUE
         END
         IF OCONV(XMISC<1,2>,'MCU') = '##:##XM' THEN TMFLG = TRUE
         IF LEN(XMISC<1,2>) = 12 THEN
            IF (XMISC<1,2>[4,1]:XMISC<1,2>[8,1]) = '--' THEN TLFLG = TRUE
         END
         IF LEN(XMISC<1,2>) = 10 THEN
            IF (XMISC<1,2>[6,1]) = '-' AND (XMISC<1,2>[1,5]) = '#####' THEN
               ZFLG = TRUE
            END
         END
         IF LEN(XMISC<1,2>) = 11 THEN
            IF (XMISC<1,2>[4,1]:XMISC<1,2>[7,1]) = '--' THEN SSFLG = TRUE
         END
      END
      SKFLG = DTFLG+TMFLG+TLFLG+ZFLG+SSFLG
      IF SKFLG THEN SKFLG = FALSE ELSE SKFLG = TRUE
      !
      OPTIONS = ESCAPE:IAMC:CR:IAMC:RIGHT1:IAMC:LEFT1:IAMC:BACK.SPACE
      OPTIONS = OPTIONS:IAMC:ICHAR:IAMC:DCHAR:IAMC:HELP:IAMC:RIGHT2
      OPTIONS = OPTIONS:IAMC:SKIP.EOL:IAMC:KILL.EOL:IAMC:RIGHT3:IAMC:CANCEL
      OPTIONS = OPTIONS:IAMC:BACK:IAMC:HOME:IAMC:ICHAR2
      !
      IF XMISC<2,1> = 'Y' THEN
         OPTIONS = OPTIONS:IAMC:UPLN:IAMC:UPLN2
      END
      IF XMISC<1,2>[1,1] = '*' THEN XMISC<1,2> = ''
      IF XMISC<2,2> = 'Y' THEN OPTIONS = OPTIONS:IAMC:DWLN
      IF XMISC<1,1> # '' THEN BORDER = TRUE ELSE BORDER = FALSE
      IF XMISC<1,2> # '' THEN MC = XMISC<1,2> ELSE
         IF TERM.TYPE<1> = 'WYSE' ! TERM.TYPE<1> = 'IBM' THEN
            MC = FILL2
            IF TERM.TYPE<1> = 'WYSE' THEN
               IF TERM.TYPE<3> = 'N' ! TERM.TYPE<4> = 'N' THEN MC = '_'
            END
            *L*          IF TERM.TYPE<1> = 'IBM' THEN LON = ''; LOFF = ''
         END ELSE MC = '_'
      END
      IF XMISC<24> = 'Y' THEN LON = ''; LOFF = ''; MC = ''
      !
1:    XMISC<3> = ''; FKEY = ''
      PRINT @(COL,ROW):LON:SPACE(VLEN):LOFF:@(COL,ROW):LON:
      BORDER = FALSE
      !
      NEW.VAR = VAR; POINTER = 1; HELP.FLG = FALSE
      PAGE.WIDTH = SYSTEM(2); PRT.FLG = TRUE
      !
      EOF = FALSE; MOVE = FALSE
      LOOP UNTIL EOF DO
         IF STARTING.POINTER # '' THEN
            POINTER = STARTING.POINTER
            STARTING.POINTER = ''
         END
         IF LEN(MC) = 1 THEN
            MASK = (NEW.VAR:STR(MC,VLEN))[1,VLEN]
         END ELSE MASK = NEW.VAR:MC[LEN(NEW.VAR)+1,99]
         IF XMISC<7> = 'Y' THEN MASK = XMISC<1,2>; ECHO OFF
         SP = VLEN - LEN(MASK)
         IF PRT.FLG & (NOT(XMISC<14>) ! NOT(XMISC<15>)) THEN
            IF BORDER THEN PRINT @(COL-1,ROW):'[':MASK:']':SPACE(SP): ELSE
               IF NOT(MOVE) THEN
                  PRINT @(COL,ROW):MASK:SPACE(SP):
               END
            END
         END
         IF DTFLG THEN IF POINTER = 3 ! POINTER = 6 THEN
            IF NEW.VAR[POINTER,1] # "/" THEN NEW.VAR = NEW.VAR:"/"
            POINTER = POINTER + 1
         END
         IF TLFLG THEN IF POINTER = 4 ! POINTER = 8 THEN
            IF NEW.VAR[POINTER,1] # "-" THEN NEW.VAR = NEW.VAR:"-"
            POINTER = POINTER + 1
         END
         IF TMFLG THEN IF POINTER = 3 THEN
            IF NEW.VAR[POINTER,1] # ":" THEN NEW.VAR = NEW.VAR:":"
            POINTER = POINTER + 1
         END
         IF ZFLG THEN IF POINTER = 6 THEN
            IF NEW.VAR[POINTER,1] # '-' THEN NEW.VAR = NEW.VAR:'-'
            POINTER = POINTER + 1
         END
         IF SSFLG THEN IF POINTER = 4 ! POINTER = 7 THEN
            IF NEW.VAR[POINTER,1] # "-" THEN NEW.VAR = NEW.VAR:"-"
            POINTER = POINTER + 1
         END
         IF SKFLG THEN IF XMISC<1,2> # '' THEN
            SYMB = XMISC<1,2>[POINTER,1]
            LOCATE SYMB IN SKM.B<1> SETTING SKV THEN
               IF NEW.VAR[POINTER,1] # SYMB THEN NEW.VAR = NEW.VAR:SYMB
               POINTER = POINTER + 1
               PRT.FLG = TRUE
            END
         END
         IF PRT.FLG & (NOT(XMISC<14>) ! NOT(XMISC<15>)) THEN
            PRINT @(COL + (POINTER-1), ROW):
         END
         IF XMISC<9,1> # '' THEN
            SCOL = COL + (POINTER-1); SROW = ROW
            IF XMISC<9,1> = 1 THEN
               CALL TIMECLICK (XMISC<9,2>, XMISC<9,3>, SCOL, SROW)
               IF NOT(BORDER) THEN PRINT @(SCOL,SROW):LON:
            END
         END
         IMISC = XMISC<19>; IMISC<2> = COL; IMISC<3> = ROW; IMISC<4> = XMISC<25>
         CALL @IRP.PGM (VALUE,IMISC)
         PRT.FLG = TRUE; MOVE = FALSE
         IF VALUE = 1 ! VALUE = 2 ! VALUE = 0 THEN
            ECHO OFF
            CALL @IRP.PGM (FKEY,IMISC)
            ECHO ON
            IF VALUE = 0 THEN
               IF FKEY GE 59 & FKEY LE 68 THEN
                  FKEY = FKEY - 58
               END ELSE
                  BEGIN CASE
                     CASE FKEY = 5; FKEY = 11
                     CASE FKEY = 6; FKEY = 12
                     CASE FKEY = 72; VALUE = 11; FKEY = ''
                     CASE FKEY = 80; VALUE = 10; FKEY = ''
                     CASE FKEY = 75; VALUE = 21; FKEY = ''
                     CASE FKEY = 77; VALUE = 6; FKEY = ''
                     CASE FKEY = 82; VALUE = 14; FKEY = ''
                     CASE FKEY = 83; VALUE = 4; FKEY = ''
                     CASE FKEY = 71; VALUE = 30; FKEY = ''
                     CASE FKEY = 79; VALUE = 127; FKEY = ''
                  END CASE
                  IF FKEY # '' THEN GOSUB 500; GO 175
               END
            END ELSE
               IF FKEY GE 97 & FKEY LE 107 THEN
                  FKEY = FKEY - 95; GOSUB 500
                  IF VALUE = 1 & TERM.TYPE<1> = 'WYSE' THEN FKEY = 'S':FKEY
               END ELSE
                  IF TERM.TYPE<1> # 'ADDS' & TERM.TYPE<1> # 'VIEWPOINT' THEN FKEY = FKEY - 63 ELSE
                     FKEY = FKEY - 48
                  END
               END
            END
         END
175:     IF FKEY # '' THEN
            GOSUB 500
            IF FKEY = 33 & TERM.TYPE<1> = 'WYSE' & VALUE = 1 THEN FKEY = 'S1'
            IF (FKEY = 'S1' ! FKEY = 'S2') & TERM.TYPE<1> = 'WYSE' THEN
               IF TERM.TYPE<19> # 'Y' THEN
                  TERM.TYPE<19> = 'Y'
                  WRITE TERM.TYPE ON TERM.F, PORT
                  PRINT VAOFF:
                  PRINT ESC:"w1":CLS:
                  IF FKEY = 'S1' THEN
                     SF1.VALID = TRUE
                     OPEN 'CONTROL' TO F.CONTROL THEN
                        PATH = 'TCL ACCESS'
                        CALL ACCESS.SUB (F.CONTROL, PATH, SF1.VALID, REDISPLAY)
                        IF SF1.VALID & REDISPLAY THEN PRINT CLS:
                     END
                     IF SF1.VALID THEN
400:                    CALL WINDOW (0, 9, 77, 3, '', '')
450:                    PRINT @(2,11):DMON:'Command ?':DMOFF:
                        CALL CIP (12, 11, 65, '', CMND, CESCFLG, '')
                        IF NOT(CESCFLG) THEN
                           IF CMND = '' THEN GO 450
                           PRINT @(0,14):
                           EXECUTE CMND
                           PRINT C23:LON:'End of Command, Hit RETURN...':LOFF:; INPUT NL,1:
                           PRINT CLS:; GO 400
                        END
                     END
                  END ELSE CALL FASTKEY.MENU ('', '', '', '')
                  PRINT CLS:ESC:"w0":LON:
                  TERM.TYPE<19> = 'N'
                  WRITE TERM.TYPE ON TERM.F, PORT
                  PRT.FLG = TRUE
                  FKEY = ''
                  GO 200
               END ELSE PRINT BELL:
            END
            LOCATE FKEY IN XMISC<4> SETTING VM THEN
               XMISC<6> = FKEY; GO 100
            END ELSE GO 1
         END
         PRT.FLG = TRUE
200:     LOCATE VALUE IN OPTIONS SETTING THING THEN
            BEGIN CASE
               CASE VALUE = ESCAPE
                  VALUE = ''
                  CALL @IBP.PGM (INPUT.BUFFER, NBR.OF.CHAR)
                  IF TERM.TYPE<2> = 3151 THEN
                     CALL NAP
                     CALL @IBP.PGM (IB2, '')
                  END ELSE IB2 = 0
                  IF TERM.TYPE<2> = 'VT220' & INPUT.BUFFER THEN
                     ECHO OFF
                     CALL @IRP.PGM (VAL,IMISC)
                     IF VAL = 79 THEN
                        CALL @IRP.PGM (VAL,IMISC)
                        BEGIN CASE
                           CASE VAL = 80 ;FKEY = 1; ECHO ON; GO 175
                           CASE VAL = 81 ;FKEY = 2; ECHO ON; GO 175
                           CASE VAL = 82 ;FKEY = 3; ECHO ON; GO 175
                           CASE VAL = 83 ;FKEY = 4; ECHO ON; GO 175
                        END CASE
                        ECHO ON
                        GO 200
                     END ELSE IF VAL = 91 THEN
                        CALL @IRP.PGM (VAL,IMISC)
                        BEGIN CASE
                           CASE VAL = 65 ;VALUE = UPLN
                           CASE VAL = 66 ;VALUE = DWLN
                           CASE VAL = 67 ;VALUE = RIGHT1
                           CASE VAL = 68 ;VALUE = LEFT1
                           CASE VAL = 77; FKEY = 5
                           CASE VAL = 49
                              CALL @IRP.PGM (VAL,IMISC)
                              BEGIN CASE
                                 CASE VAL = 55; FKEY = 6
                                 CASE VAL = 56; FKEY = 7
                                 CASE VAL = 57; FKEY = 8
                              END CASE
                           CASE VAL = 50
                              CALL @IRP.PGM (VAL,IMISC)
                              BEGIN CASE
                                 CASE VAL = 48; FKEY = 9
                                 CASE VAL = 49; FKEY = 10
                                 CASE VAL = 51; FKEY = 11
                                 CASE VAL = 52; FKEY = 12
                              END CASE
                           CASE 1; FKEY = ''
                        END CASE
                        IF FKEY # '' THEN
                           IF FKEY # 5 THEN
                              CALL @IRP.PGM ('', '')
                           END
                           ECHO ON
                           GO 175
                        END
                        ECHO ON
                        GO 200
                     END
                  END
                  IF TERM.TYPE<2> = 3151 & (INPUT.BUFFER ! IB2) THEN
                     ECHO OFF
                     CALL @IRP.PGM (VAL,IMISC)
                     BEGIN CASE
                        CASE VAL = 68; VALUE = LEFT1
                        CASE VAL = 67; VALUE = RIGHT1
                        CASE VAL = 66; VALUE = DWLN
                        CASE VAL = 65; VALUE = UPLN
                        CASE VAL GE 97 & VAL LE 108
                           FKEY = VAL - 96
                           ECHO ON
                           GO 175
                     END CASE
                     ECHO ON
                     GO 200
                  END
                  IF INPUT.BUFFER THEN
                     ECHO OFF
                     CALL @IRP.PGM (VAL,IMISC)
                     IF VAL = 80 THEN
                        PRINT @(79,19):
                        PRINT ESC:"B"     ;* TURN BLOCK MODE ON
                        PRINT @(79,22):
                        PRINT ESC:"P"     ;* PRINT SCREEN OPTION
                        PRINT @(79,22):
                        PRINT ESC:"C":ESC:"DF"  ;* TURN FDX MODE ON
                        IF POINTER GT 0 THEN POINTER = POINTER - 1 ELSE POINTER = 0
                     END ELSE
                        IF VAL = 75 ! VAL = 74 THEN
                           IF VAL = 75 THEN FKEY = 4 ELSE FKEY = 5
                           GO 175
                        END
                        BEGIN CASE
                           CASE VAL = 81 ! VAL = 113; VALUE = ICHAR
                           CASE VAL = 87; VALUE = DCHAR
                           CASE VAL = 84 ! VAL = 82; VALUE = KILL.EOL
                           CASE VAL = 114; VALUE = CANCEL
                        END CASE
                     END
                     ECHO ON
                     IF VALUE # '' THEN GO 200
                  END ELSE ESCFLG = TRUE; EOF = TRUE
               CASE VALUE = HOME; POINTER = 1
               CASE VALUE = BACK ! VALUE = HOME; POINTER = 1
               CASE VALUE = UPLN ! VALUE = UPLN2
                  XMISC<3,1> = 'Y'; EOF = TRUE
               CASE VALUE = DWLN
                  XMISC<3,2> = 'Y'; EOF = TRUE
               CASE VALUE = CANCEL
                  NEW.VAR = VAR; POINTER = 1
               CASE VALUE = RIGHT1 ! VALUE = RIGHT2 ! VALUE = RIGHT3
                  MOVE = TRUE
                  IF XMISC<20> = 'Y' & POINTER = VLEN THEN
                     POINTER = 'R'; EOF = TRUE
                  END ELSE
                     IF XMISC<2,4> = 'Y' THEN
                        XMISC<3,4> = 'Y'; EOF = TRUE
                     END ELSE
                        IF POINTER LE LEN(NEW.VAR) & POINTER LT VLEN THEN
                           POINTER = POINTER + 1
                        END
                     END
                  END
               CASE VALUE = CR
                  EOF = TRUE
               CASE VALUE = ICHAR ! VALUE = ICHAR2
                  IF XMISC<2,7> = 'Y' THEN
                     XMISC<3,7> = 'Y'; EOF = TRUE
                  END ELSE
                     IF LEN(NEW.VAR)+1 LE VLEN ! XMISC<20> = 'Y' THEN
                        IF POINTER = 1 THEN
                           NEW.VAR = ' ':NEW.VAR
                        END ELSE
                           LEFT.VAL = NEW.VAR[1,POINTER-1]
                           RIGHT.VAL= NEW.VAR[POINTER,99]
                           NEW.VAR  = LEFT.VAL:' ':RIGHT.VAL
                        END
                     END
                     IF XMISC<20> = 'Y' THEN
                        XMISC<21> = 'I'; XMISC<22> = POINTER; EOF = TRUE
                     END
                  END
               CASE VALUE = DCHAR
                  IF XMISC<2,5> = 'Y' THEN
                     XMISC<3,5> = 'Y'; EOF = TRUE
                  END ELSE
                     IF POINTER = 1 THEN
                        NEW.VAR = NEW.VAR[2,99]
                     END ELSE
                        LEFT.VAL = NEW.VAR[1,POINTER-1]
                        RIGHT.VAL= NEW.VAR[POINTER+1,99]
                        NEW.VAR  = LEFT.VAL:RIGHT.VAL
                     END
                     IF XMISC<20> = 'Y' THEN
                        XMISC<21> = 'D'; XMISC<22> = POINTER; EOF = TRUE
                     END
                  END
               CASE VALUE = HELP
                  PRINT C22:"LEFT '<=' or ctrl+B, RIGHT '=>' or ctrl+F,":
                  PRINT ' CANCEL ctrl+X, END OF LINE ctrl+E'
                  PRINT C23:"INSERT ctrl+I, DELETE ctrl+D, KILL THE REST ":
                  PRINT 'ctrl+K, OPTIONS ctrl+O, <ESCAPE>':
                  HELP.FLG = TRUE
               CASE VALUE = SKIP.EOL
                  IF LEN(NEW.VAR) LT VLEN THEN POINTER = LEN(NEW.VAR) + 1 ELSE
                     POINTER = VLEN
                  END
               CASE VALUE = BACK.SPACE
                  MOVE = TRUE
                  IF POINTER = 1 & XMISC<20> = 'Y' THEN
                     XMISC<22> = 'L'; EOF = TRUE
                  END ELSE
                     IF XMISC<2,3> = 'Y' THEN
                        XMISC<3,3> = 'Y'; EOF = TRUE
                     END ELSE
                        IF POINTER # 1 THEN POINTER = POINTER - 1
                        IF DTFLG THEN
                           IF POINTER = 3 ! POINTER = 6 THEN POINTER = POINTER - 1
                        END ELSE IF TMFLG THEN
                           IF POINTER = 3 THEN POINTER = POINTER - 1
                        END ELSE IF ZFLG THEN
                           IF POINTER = 6 THEN POINTER = POINTER - 1
                        END ELSE IF SSFLG THEN
                           IF POINTER = 4 ! POINTER = 7 THEN POINTER = POINTER - 1
                        END ELSE IF TLFLG THEN
                           IF POINTER = 4 ! POINTER = 8 THEN POINTER = POINTER - 1
                        END ELSE IF SKFLG THEN
                           IF XMISC<1,2> # '' THEN
                              SYMB = XMISC<1,2>[POINTER,1]
                              LOCATE SYMB IN SKM.B<1> SETTING SKV THEN
                                 POINTER = POINTER - 1
                              END
                           END
                        END
                        IF TERM.TYPE<2> = 'VT220' THEN
                           NEW.VAR = NEW.VAR[1,POINTER-1]; MOVE = FALSE
                        END
                     END
                  END
               CASE VALUE = KILL.EOL
                  IF XMISC<2,6> = 'Y' THEN
                     XMISC<3,6> = 'Y'; EOF = TRUE
                  END ELSE
                     NEW.VAR = NEW.VAR[1,POINTER-1]
                  END
               CASE 1
                  IF XMISC<2,3> = 'Y' THEN
                     XMISC<3,3> = 'Y'; EOF = TRUE
                  END ELSE
                     IF POINTER GT 1 THEN POINTER = POINTER - 1
                     IF DTFLG AND (POINTER = 3 ! POINTER = 6) THEN
                        POINTER = POINTER - 1
                     END
                     IF TMFLG AND POINTER = 3 THEN POINTER = POINTER - 1
                     IF TLFLG AND (POINTER = 4 ! POINTER = 8) THEN
                        POINTER = POINTER - 1
                     END
                     IF ZFLG AND POINTER = 5 THEN POINTER = POINTER - 1
                     IF SSFLG AND (POINTER = 4 ! POINTER = 7) THEN
                        POINTER = POINTER - 1
                     END
                     IF SKFLG THEN IF XMISC<1,2> # '' THEN
                        SYMB = XMISC<1,2>[POINTER,1]
                        LOCATE SYMB IN SKM.B<1> SETTING SKV THEN
                           IF POINTER # 1 THEN POINTER = POINTER - 1
                        END
                     END
                  END
            END CASE
         END ELSE
            CHAR.OK = FALSE
            IF XMISC<12> # '' THEN
               LOCATE CHAR(VALUE) IN XMISC<12> SETTING BVM THEN
                  NEW.VAR = CHAR(VALUE); BYPASS = TRUE; GO 100
               END
               IF LEN(XMISC<12>) GT 1 THEN
                  LOCATE NEW.VAR:CHAR(VALUE) IN XMISC<12> SETTING BVAL THEN
                     NEW.VAR = NEW.VAR:CHAR(VALUE); BYPASS = TRUE; GO 100
                  END
               END
            END
            IF VALUE GT 31 & VALUE LT 127 THEN CHAR.OK = TRUE
            IF (VALUE LT 32) ! (VALUE GT 126 & VALUE LT 252) THEN NULL ELSE
               CHAR.OK = TRUE
            END
            IF CHAR.OK THEN IF DTFLG ! TLFLG ! SSFLG ! ZFLG THEN
               CV = CHAR(VALUE)
               IF DTFLG THEN
                  IF POINTER = 2 AND LEN(NEW.VAR) = 1 THEN
                     IF CV = ' ' ! CV = '.' ! CV = ':' ! CV = '/' THEN
                        NEW.VAR = 0:NEW.VAR; CHAR.OK = FALSE
                        POINTER = POINTER + 1
                     END
                  END
                  IF POINTER = 5 AND LEN(NEW.VAR) = 4 THEN
                     IF CV = ' ' ! CV = '.' ! CV = ':' ! CV = '/' THEN
                        LVAR = NEW.VAR[1,POINTER-2]
                        RVAR = NEW.VAR[LEN(NEW.VAR),1]
                        NEW.VAR = LVAR:0:RVAR
                        CHAR.OK = FALSE; POINTER = POINTER + 1
                     END
                  END
               END
               LOCATE CV IN INTG.B<1> SETTING A ELSE CHAR.OK = FALSE
            END
            IF CHAR.OK THEN IF TMFLG THEN
               CV = CHAR(VALUE)
               IF POINTER = 2 AND LEN(NEW.VAR) LE 2 THEN
                  IF CV = ' ' ! CV = '.' ! CV = ':' ! CV = '/' THEN
                     NEW.VAR = '0':NEW.VAR; CHAR.OK = FALSE; POINTER = POINTER + 1
                  END
               END
               IF POINTER # 6 AND CHAR.OK THEN
                  LOCATE CV IN INTG.B<1> SETTING A ELSE CHAR.OK = FALSE
               END ELSE IF CV = 'A' ! CV = 'P' ELSE CHAR.OK = FALSE
            END
            IF CHAR.OK THEN
               IF SKFLG AND XMISC<7> # 'Y' THEN
                  PRT.FLG = FALSE
               END
               IF POINTER = 1 & VAR # '' THEN
                  IF TERM.TYPE<2> = 'VT220' ! NUM(VAR) THEN NEW.VAR = ''; PRT.FLG = TRUE
               END
               *L*            IF POINTER = 1 THEN IF VAR # '' & NUM(VAR) THEN NEW.VAR = ''; PRT.FLG = TRUE
               IF POINTER GT VLEN THEN
                  PRINT @(COL + (POINTER-1)):' ':BELL:
               END ELSE
                  VALUE = CHAR(VALUE)
                  IF POINTER = 1 THEN LEFT.VAL = '' ELSE
                     LEFT.VAL = NEW.VAR[1,POINTER-1]
                  END
                  RIGHT.VAL= NEW.VAR[POINTER+1,99]
                  NEW.VAR= LEFT.VAL:VALUE:RIGHT.VAL
                  POINTER = POINTER + 1
               END
            END
         END
         IF POINTER GT VLEN THEN EOF = TRUE
      REPEAT
      IF XMISC<22> = '' THEN XMISC<22> = POINTER
      !
      IF HELP.FLG THEN PRINT C23:C22:
100:  IF ZFLG & NOT(BYPASS) THEN
         IF NEW.VAR # '' THEN
            IF NEW.VAR MATCHES ('5N':"-") THEN
               NEW.VAR = NEW.VAR[1,5]
            END ELSE IF NEW.VAR MATCHES ('5N':"-":'4N') ! NEW.VAR MATCHES '5N' ELSE GO 1
         END
      END
      IF DTFLG & NOT(BYPASS) THEN
         IF NEW.VAR # '' THEN
            IF ICONV(NEW.VAR,'D') THEN
               NEW.VAR = OCONV(ICONV(NEW.VAR,'D'),'D2/')
            END ELSE GO 1
         END
      END
      IF TMFLG & NOT(BYPASS) THEN
         IF NEW.VAR # '' THEN
            IF ICONV(NEW.VAR,'MT') THEN
               NEW.VAR = OCONV(ICONV(NEW.VAR,'MT'),'MTH')
            END ELSE GO 1
         END
      END
      IF TLFLG & NOT(BYPASS) THEN
         IF NEW.VAR # '' THEN
            IF NEW.VAR MATCHES ('3N':"-":'3N':"-":'4N') ELSE GO 1
         END
      END
      IF SSFLG & NOT(BYPASS) THEN
         IF NEW.VAR # '' THEN
            IF NEW.VAR MATCHES ('3N':"-":'2N':"-":'4N') ELSE GO 1
         END
      END
      !
      ! BACK OUT PROCEEDING SPACES
      !
      IF XMISC<23> # 'N' THEN
         EOF = FALSE; LENGTH = LEN(NEW.VAR) + 1
         LOOP UNTIL EOF DO
            LENGTH = LENGTH - 1
            IF NEW.VAR[LENGTH,1] = ' ' THEN
               NEW.VAR = NEW.VAR[1,LENGTH-1]
            END ELSE EOF = TRUE
         REPEAT
      END
      IF XMISC<4> # '' THEN
         ARROWS = FALSE
         IF XMISC<3,1> # '' ! XMISC<3,2> # '' ! XMISC<3,3> # '' ! XMISC<3,4> # '' THEN ARROWS = TRUE
         IF (ARROWS & NOT(XMISC<16>)) ! NOT(ARROWS) THEN
            PRINT LOFF:BTM:
            PRINT BTM:
            IF BTM2 # '' THEN PRINT BTM2:
         END
      END
      IF NOT(XMISC<14>) ! NOT(XMISC<15>) THEN
         IF BORDER THEN PRINT @(COL-1,ROW):SPACE(VLEN+2): ELSE
            SPC = VLEN
            PRINT @(COL,ROW):LOFF:SPACE(SPC):
         END
      END
300:  IF XMISC<7> = 'Y' THEN ECHO ON ELSE PRINT @(COL,ROW):NEW.VAR[1,VLEN]:
      IF STACKON ELSE IF FKEY # '' THEN GOSUB 500
      !
      ! INSTANT MESSAGING
      !
      *L*      INSTANT.MESSAGING = OCONV('COMPANY','TCONTROL;X;102;102')
      *L*      IF INSTANT.MESSAGING = 'Y' & TERM.TYPE<19> # 'Y' & XMISC<19> # 'Y' & TERM.TYPE<1> = 'WYSE' THEN
      *L*        CALL INSTANT.MESSAGE (PORT, '')
      *L*      END
      !
      ! RETURN TO CALLING PROGRAM
      !
      RETURN
      !
      ! CLEAR TYPE AHEAD BUFFER
      !
500:  ECHO OFF
      TYPE.AHEAD.CLEARED = FALSE
      LOOP
         CALL @IBP.PGM (INBUFF, NBR.OF.CHAR)
         IF INBUFF THEN
            IF NBR.OF.CHAR + 0 # 0 THEN
               INPUT CARR.RTN,NBR.OF.CHAR:
            END ELSE
               INPUT CARR.RTN:
            END
         END ELSE TYPE.AHEAD.CLEARED = TRUE
      UNTIL TYPE.AHEAD.CLEARED DO REPEAT
      ECHO ON
      RETURN
