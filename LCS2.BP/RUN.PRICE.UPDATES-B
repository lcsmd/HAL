
     DEFFUN GET.OPTION(CMD.STRING,OPT.NAME)
     ** THIS PROGRAM GATHERS ALL THE INPUTS NEEDED TO PROCESS UPDATES AND THEN IT PASSES DATA TO THE PROCESS.UPDATE ROUTINE ONE AT A TIME
     *  A CROSS REFERENCE FILE TO RELATE UPDATEID'S AND LOGID'S IS UPDATED WITH AN ENTRY THAT ALSO SHOWS THE STATUS OF THE UPDATE, THE PREPARED TEXT FILE LOCATIONS
     * WHICH ARE IN THE ARCHIVE FILE LOCATION. AND ERRORS
     * THIS DATA IS SUBSEQUENTLY READ FROM THE PROCESS ROUTINE BASED ON THE UPDATEID PASSED TO IT. 142508527lod, 3-M20140702, etc)

     **I2.PRICE.UPDATE.LOG
     EQU  UPDATE.TYPE TO XMISC<1>, UPDATEID TO XMISC<2>, USER TO XMISC<3>, UPDATE.NUM TO XMISC<4>
     EQU UPDATE.CODE TO XMISC<5>,  UPDATE.NAME TO XMISC<6>
     EQU NOTE TO XMISC<7>, MAST.EFFDATE TO XMISC<8>, MAST.EXPDATE TO XMISC<9>, UPDATE.MODE TO XMISC<10>
     EQU UPDATE.SCOPE TO XMISC<11>
     EQU FILE.NAME TO XMISC<12,1>, FILE.PATH TO XMISC<12,2>, ITEM.NAME TO XMISC<13>
     EQU DELIM TO XMISC<14>
     EQU COL.LABELS TO XMISC<15>
     EQU UPC.COL TO XMISC<16,1>, CAT.COL TO XMISC<16,2>, EFFDATE.COL TO XMISC<16,3>,COST.COL TO XMISC<16,4>, UOM.COL TO XMISC<16,5>
     EQU HEADER.LABELS TO XMISC<17>
     EQU UPC.FORMAT TO XMISC<18,1>, DATA.START TO XMISC<18,2>
     EQU CUSTOM.SUB TO XMISC<19>
     EQU MAST.UOM TO XMISC<20>
     EQU RUN.COUNT TO XMISC<21>
     EQU V.NUM TO XMISC<22>, V.NAME TO XMISC<23>, V.NEW TO XMISC<24>
     EQU V.UPDATE TO XMISC<25>, V.DELETE TO XMISC<26>, V.ZAP TO XMISC<27>
     EQU V.TOTAL TO XMISC<28>
     EQU T.NEW TO XMISC<29>, T.UPDATE TO XMISC<30>, T.DELETE TO XMISC<31>
     EQU T.ZAPPED TO XMISC<32>, T.TOTAL TO XMISC<33>

     **I2.PRODUCT
     EQU UPC TO LIB.B<99>, MFF_UCC_NUM TO LIB.B<4>, ITEM_NUM TO LIB.B<5>
     EQU PS_PIK TO LIB.B<1>, PNO TO LIB.B<9>, CATNUM TO LIB.B<22>
     EQU PRICEGROUP TO LIB.B<31>, UOM TO LIB.B<37>, EFFDATE TO LIB.B<44>, EXPDATE TO LIB.B<45>
     EQU COST.C1 TO LIB.B<51>, COST.C4 TO LIB.B<53>, COST.C8 TO LIB.B<54>


     *** DETAIL OF NEXT.PRICE.UPDATE
     EQU NEXT.TRADE.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,1>
     EQU NEXT.VENDOR.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,2>
     EQU NEXT.MANUAL.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,3>
     EQU NEXT.REFRESH.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,4>

     *** TS.VEND
     EQU TS.VEND.NAME TO VBUFF<1>
     EQU TS.VEND.ABB TO VBUFF<2>
     EQU TS.MAJOR.PG TO VBUFF<3>
     EQU TS.CUSTOM.PG TO VBUFF<4>
     EQU TS.ITEM.COUNT TO VBUFF<5>
     EQU TS.ACTIVE TO VBUFF<6>
     EQU TS.FULL.LINE TO VBUFF<7>
     EQU TS.PG.SUB TO VBUFF<8>
     EQU TS.SHORT.NAME TO VBUFF<9>
     EQU TS.VM.LIST TO VBUFF<10>

     ***

     LM=1
     DM=1
     CL=1
     GOSUB INITIALIZE:
     GOSUB PARSE.COMMAND:
     IF UPDATE.MODE = "REFRESH" THEN
          LOG.ID = "R":NEXT.REFRESH.UPDATE.LOG
          GOSUB GET.UPDATE.LIST:
          GOSUB WRITE.I2.UPDATE.LOG:
          UPDATE.CT = 1
          GOSUB PROCESS.UPDATE:
          GOSUB UPDATE.CONTROLS:
     END ELSE
          PRICE.NAME.NEW = UPDATE.TYPE2:",NEW"
          PRICE.NAME.ARCHIVE = UPDATE.TYPE2:",ARCHIVE"
          OPEN PRICE.NAME.NEW TO PRICE.NEW ELSE PRINT PRICE.NAME.NEW ; STOP
          OPEN PRICE.NAME.ARCHIVE TO PRICE.ARCHIVE ELSE PRINT PRICE.NAME.ARCHIVE ; STOP
          I2.ARCHIVE.PATH = I2.PATH:UPDATE.TYPE2:"\ARCHIVE\"
          I2.NEW.PATH = I2.PATH:UPDATE.TYPE2:"\NEW\"
          OPEN 'TS.VEND.XREF' TO TS.VEND.XREF ELSE PRINT 'TS.VEND.XREF' ;STOP

          GOSUB GET.UPDATE.LIST:
          UPDATE.CT = DCOUNT(UPDATE.LIST,@FM)
          IF LM THEN PRINT "UPDATE.LIST:":CHANGE(UPDATE.LIST,@FM,",")
          FOR UPDATE.X = 1 TO UPDATE.CT
               GOSUB RESET.UPDATE:
               UPDATEID = UPDATE.LIST<UPDATE.X>
               IF LM THEN PRINT "PROCESS UPDATE :":UPDATEID
               IF UPDATE.TYPE = "T" THEN
                    SEQ = UPDATEID[1,4]
               END
               IF UPDATE.TYPE = "V" THEN
                    SEQ = NEXT.VENDOR.UPDATE.LOG
               END
               IF UPDATE.TYPE = 'M' THEN
                    SEQ = NEXT.MANUAL.UPDATE.LOG
               END
               LOG.ID = UPDATE.TYPE:SEQ
               GOSUB PROCESS.UPDATE:
               GOSUB UPDATE.CONTROLS:
          NEXT UPDATE.X
     END
     GOSUB PRINT.SUMMARY:
     GOSUB PRINT.FINAL.INFO:
     STOP

INITIALIZE:
     IF LM THEN PRINT "*** INITIALIZE"
     *UPDATE.LIST = ""
     *COMMA = ","update.
     EXECUTE "CREATE.TS.VEND.XREF"
     LM=1
     DM=0
     TODAY = OCONV(DATE(),'D4-')
     YEAR=RIGHT(TODAY,4)
     MONTH = FIELD(TODAY,"-",1)
     DAY = FIELD(TODAY,"-",2)
     DEBUG = 0
     ERRORS = ""
     ERROR.CT = 0
     *DONE = 0
     UPDATES.DONE.CT = 0
     FINISHED=FALSE
     UPDATES.CT = 0
     *      ITEM.NAME = ""
     *NEW.FILES.COUNT = 0
     *TRADE.EOF = 0
     FILE.TYPES="ZIP,XLSX,XLS,CSV,TXT,XML"
     FILE.TYPES = CHANGE(FILE.TYPES,",",@VM)
     FILE.TYPE.CT= DCOUNT(FILE.TYPES,@VM)

     XMISC = ""
     UPDATE.MODE = ""
     PRINT @(-1):
     START.TIME = TIME()
     PRINT "START:":OCONV(START.TIME,'MTHS')

     OPEN 'LCS.CONTROL' TO LCS.CONTROL ELSE PRINT 'LCS.CONTROL' ; STOP
     OPEN 'UPDATE.NAME.XREF' TO UPDATE.NAME.XREF ELSE PRINT 'UPDATE.NAME.XREF' ; STOP
     *OPEN "I2.PRODUCT" TO I2.PRODUCT ELSE PRINT "I2.PRODUCT";STOP
     OPEN 'LCS.DOS' TO LCS.DOS ELSE PRINT 'LCS.DOS' ; STOP
     OPEN 'I2.PRICE.UPDATE.LOG' TO I2.PRICE.UPDATE.LOG ELSE PRINT 'I2.PRICE.UPDATE.LOG' ; STOP
     OPEN 'TS.VEND' TO TS.VEND ELSE PRINT 'TS.VEND' ; STOP
     *
     READV I2.PATH FROM LCS.CONTROL,"I2.PATH",1 ELSE I2.PATH = "G:\DATA_ACCOUNTS\PRICE\"
     READV TRADE.ACCOUNT FROM LCS.CONTROL,"TRADE.ACCOUNT",1 ELSE TRADE.ACCOUNT = "08527"
     READV NEXT.PRICE.UPDATE.LOG FROM LCS.CONTROL,"NEXT.PRICE.UPDATE.LOG",1 ELSE PRINT "NEXT.PRICE.UPDATE.LOG ENTRY NOT FOUND" ; STOP


     *update.scope = s,sl,slp (passed as argument)
     *update.class = lod,upd (passed as argument)
     *update.mode = load,refresh,update (based on update class)
     *check command line and options
     CALL USERINFO(CONTROL,ONAME,OPER,ULEVEL,INTLS,'')
     USER = INTLS
     PROMPT ''

     TRUE = 1
     FALSE = 0
     UPDATE.LIST = ""
     RETURN

PARSE.COMMAND:
     ALL.NEW = 'N'
     IF LM THEN PRINT "*** PARSE.COMMAND:"
     CMD.LINE = @SENTENCE
     IF GET.OPTION(CMD.LINE,"?") THEN
          PRINT "RUN.PRICE.UPDATES <T|V|M|L|R> [-LOAD <issue#> LOGID <logid>] [-UPDATE <UPDATEID> | NEW ] [-REFRESH] [-SCOPE scope] [-VENDOR] <ven> [-HEADER] <data-row,upc,cat.num,uom,effdate,cost>"
          * TYPE T - MODE = UPDATE <ID OR NEW>
          * TYPE V - MODE = UPDATE <ID OR NEW>, [optional VENDOR <VEN>] [optional HEADER <header info>]
          * TYPE M - MODE = UPDATE <ID OR NEW>
          * TYPE R - MODE = REFRESH SCOPE <-SCOPE> <DEFAULT IS SLP> [VENDOR <ven>] [LOGID <logid>]
          * TYPE L - MODE = LOAD (requires UPDATEID and logid) [VENDOR <ven>]
          * ACCEPTABLE COMMAND LINES ARE:
          * now:

          * RUN.PRICE.UPDATES T <trade.issue>|<NEXT> RUNS <trade.issue> or the next TRADE.SERVICE.UPDATE -SCOPE <scope>
          * RUN.PRICE.UPDATES V <UPDATEID> RUNS THE VENDOR UPDATE -SCOPE <scope>
          * RUN.PRICE.UPDATES V VENDOR <ven> RUNS THE VENDOR UPDATE for vendor ven -SCOPE <scope>
          * RUN.PRICE.UPDATES V NEW
          * RUN.PRICE.UDPATES L <TRADE.ISSUE> -SCOPE <scope>
          * for future:
          * RUN.PRICE.UPDATES M <UPDATEID> RUNS MANUAL UPDATE -SCOPE <scope>
          * RUN.PRICE.UPDATES R VENDOR <ven> | LOGID >logid>
          PRINT
     END ELSE
          UPDATE.SCOPE = GET.OPTION(CMD.LINE,"SCOPE")
          IF (UPDATE.SCOPE = "SCOPE" OR UPDATE.SCOPE = "") THEN UPDATE.SCOPE = "SLP"
          UPDATE.TYPE = TRIM(FIELD(CMD.LINE," ",2))
          UPDATE.CODE = GET.OPTION(CMD.LINE,"VENDOR")
          IF UPDATE.CODE = "VENDOR" THEN UPDATE.CODE = ""
          LOG.ID = GET.OPTION(CMD.LINE,"LOGID")
          IF UPDATE.TYPE = "" THEN UPDATE.TYPE = "T"
          IF UPDATE.TYPE = "L" THEN
               UPDATE.MODE = "LOAD"
               UPDATE.CLASS = "lod"
               UPDATE.TYPE = "T"
          END
          IF UPDATE.TYPE = "R" THEN
               UPDATE.MODE = "REFRESH"
               UPDATE.TYPE2="REFRESH"
               UPDATE.CODE = "REF"
               FINISHED = TRUE
          END
          IF UPDATE.TYPE = "V" THEN
               UPDATE.MODE = 'UPDATE'
               UPDATE.TYPE2 = 'VENDOR'
               FINISHED = TRUE
          END
          IF UPDATE.TYPE = 'M' THEN
               UPDATE.TYPE2 = 'MANUAL'
               UPDATE.MODE = 'UPDATE'
               FINISHED=TRUE
          END
          IF UPDATE.TYPE = "T" THEN
               UPDATE.TYPE2 = 'TRADESERVICE'
               ALL.NEW = "Y"
               IF UPDATE.MODE = "" THEN
                    UPDATE.MODE = "UPDATE"
                    UPDATE.CLASS = "upd"
               END ELSE
                    FINISHED = TRUE
               END
               TRADE.ISSUE = FIELD(CMD.LINE," ",3)
               IF TRADE.ISSUE = "NEXT" THEN
                    ALL.NEW = "N"
                    TRADE.ISSUE = ""
                    FINISHED = TRUE
               END
          END
     END
     RETURN

RESET.UPDATE:
     FILE.NAME = ""
     ITEM.NAME = ""
     FILE.PATH = ""
     SOURCE.FOUND = FALSE
     RETURN

GET.UPDATE.LIST:
     IF LM THEN PRINT "**** GET.UPDATE.LIST:"
     SELECT.STR = ""
     IF UPDATE.TYPE = "R" THEN
          UPDATE.CODE = "REFRESH"
          UPDATE.NUM = "000000"
          UPDATE.NAME =  "REFRESH PRODUCT FILE"
          EOF = 1
          UPDATEID = "REF":YEAR:MONTH:DAY
          UPDATEID1 = UPDATEID
          UPDATE.LIST = UPDATEID
     END

     IF UPDATE.TYPE = "T" THEN
          UPDATE.CODE = "T-S"
          UPDATE.NUM = "000000"
          UPDATE.NAME = "TRADE SERVICE"
          NEXT.ISSUE = NEXT.TRADE.UPDATE.LOG
          IF UPDATE.MODE = "LOAD" THEN UPDATE.CLASS = "lod" ELSE UPDATE.CLASS = "upd"
          EOF = 0
          LOOP WHILE NOT(EOF) DO
               THIS.ISSUE = NEXT.ISSUE
               UPDATEID = THIS.ISSUE:TRADE.ACCOUNT:UPDATE.CLASS
               ZIP.FILE = UPDATEID:".zip"
               IF LM THEN PRINT "LOOKING FOR ":ZIP.FILE:" IN TRADESERVICE,NEW"
               READV EXISTS FROM PRICE.NEW,ZIP.FILE,0 THEN
                    IF LM THEN PRINT ZIP.FILE:" FOUND"
                    UPDATE.LIST<-1> = UPDATEID
                    GOSUB SET.NEXT.ISSUE:
               END ELSE
                    IF LM THEN PRINT ZIP.FILE:" NOT FOUND IN TRADESERVICE,NEW"
                    EOF = TRUE
               END
          REPEAT
     END
     IF UPDATE.TYPE = 'V' THEN
          IF UPDATEID THEN
               UPDATE.LIST = UPDATEID
               IF LM THEN PRINT "ADDING ":UPDATEID
               ITEM.NAME = UPDATEID
               UPDATEID1 = FIELD(UPDATEID,".",1)
               UPDATE.LIST<-1> = UPDATEID
               *GOSUB PROCESS.ITEM.NAME:          SELECT.STR = "SSELECT VENDOR,NEW"
          END ELSE
               SELECT.STR = "SSELECT VENDOR,NEW"
               IF UPDATE.CODE THEN
                    SELECT.STR := " = ":UPDATE.CODE:"]"
               END ELSE
                    SELECT.STR := " = ":TRIM(CMD.LINE[INDEX(CMD.LINE," ",2)+1,LEN(CMD.LINE)])
               END
               IF LM THEN PRINT SELECT.STR
               EXECUTE SELECT.STR CAPTURING CAP
               IF LM THEN PRINT CAP
               READLIST UPDATE.LIST ELSE UPDATE.LIST = ""
          END
     END

     IF UPDATE.TYPE = "M" THEN
          * CODE TO PROCESS MANUAL UPDATES
          SELECT.STR = 'SSELECT MANUAL,NEW':TRIM(CMD.LINE[INDEX(CMD.LINE," ",2)+1,LEN(CMD.LINE)])
          IF LM THEN PRINT SELECT.STR
          EXECUTE SELECT.STR CAPTURING CAP
          IF LM THEN PRINT CAP
          READLIST UPDATE.LIST ELSE UPDATE.LIST = ""
     END
     RETURN


PROCESS.UPDATE:
     IF CL THEN
          DELETE UPDATE.NAME.XREF, UPDATEID
          DELETE I2.PRICE.UPDATE.LOG,LOG.ID
     END

     READ UPNX FROM UPDATE.NAME.XREF, UPDATEID ELSE
          UPNX = ""
     END
     INS LOG.ID BEFORE UPNX<1,1>
     INS UPDATE.MODE BEFORE UPNX<2,1>
     INS UPDATE.SCOPE BEFORE UPNX<3,1>
     INS "STARTED" BEFORE UPNX<4,1>
     INS DATE() BEFORE UPNX<5,1>
     INS TIME() BEFORE UPNX<6,1>
     WRITE UPNX ON UPDATE.NAME.XREF,UPDATEID

     IF NOT(DELIM) THEN DELIM = "TAB"

     IF DELIM = "TAB" THEN DELIMITER = CHAR(9)
     IF DELIM = "COMMA" THEN DELIMITER = CHAR(ASCII(","))
     IF DELIM = "COLON" THEN DELIMITER = CHAR(ASCII(":"))

     USER = INTLS
     IF UPDATE.MODE = "REFRESH" THEN
          UPDATE.OPTIONS.IN = UPDATE.MODE
          UPDATE.OPTIONS.IN<2> = UPDATE.SCOPE
          OUTARGS = ""
          IF LM THEN PRINT "**** CALLING I2.CREATE.PRODUCT.SOURCE"
          IF DM THEN DEBUG
          CALL I2.CREATE.PRODUCT.SOURCE(LOG.ID,UPDATE.OPTIONS.IN,OUTARGS)

     END
     IF UPDATE.TYPE = "T" THEN
          READV TRADE.COL.LABELS FROM PRICE.ARCHIVE,ITEM.NAME,1 ELSE TRADE.COL.LABELS = ""
          TRADE.COL.LABELS  = CHANGE(TRADE.COL.LABELS,CHAR(9),@VM)
          COL.LABELS = TRADE.COL.LABELS
          TRADE.COL.VALUES = ""
          FOR CX = 1 TO DCOUNT(COL.LABELS,@VM)
               TRADE.COL.VALUES<1,CX> = CX
          NEXT CX
          COL.VALUES = TRADE.COL.VALUES
          GOSUB GET.EFFDATE:
          GOSUB GET.TRADE.STATS:
     END
     IF UPDATE.TYPE = "V" THEN
          READ TSVX FROM TS.VEND.XREF,UPDATE.CODE THEN
               UPDATE.NUM = TSVX<1,1>
               READ VBUFF FROM TS.VEND,UPDATE.NUM ELSE
                    PRINT UPDATE.CODE:" NOT FOUND IN TS.VEND";STOP
               END
               UPDATE.NAME = VBUFF<1>


          END
          IF LM THEN PRINT XMISC
          IF LM THEN PRINT "*** CALLING SUBMIT.FILE ":UPDATEID:" IN ":I2.ARCHIVE.PATH
          CALL SUBMIT.FILE(XMISC,ERRORS)
          IF LM THEN PRINT XMISC;PRINT ERRORS
     END

     IF UPDATE.MODE <> "REFRESH" THEN
          IF NOT(MAST.EFFDATE) AND UPDATE.MODE <> "REFRESH" THEN
               MAST.EFFDATE1 = FIELD(FIELD(UPDATEID,"_",2),".",1)
               MAST.EFF.YEAR = FIELD(MAST.EFFDATE1,"-",1)[1,4]
               MAST.EFF.MON = FIELD(MAST.EFFDATE1,"-",2)[1,2]
               MAST.EFF.DAY = FIELD(MAST.EFFDATE1,"-",3)[1,2]
               MAST.EFFDATE=MAST.EFF.MON:"-":MAST.EFF.DAY:"-":MAST.EFF.YEAR
          END
      END


     IF UPDATE.TYPE = 'T' THEN
         IF NOT(MAST.UOM) THEN MAST.UOM = "E"

          GOSUB WRITE.I2.UPDATE.LOG:

          UPDATE.OPTIONS.IN = UPDATE.MODE
          UPDATE.OPTIONS.IN<2> = UPDATE.SCOPE
          OUTARGS = ""
          IF LM THEN PRINT "**** CALLING I2.CREATE.PRODUCT.SOURCE"
          IF DM THEN DEBUG
          CALL I2.CREATE.PRODUCT.SOURCE(LOG.ID,UPDATE.OPTIONS.IN,OUTARGS)
          GOSUB DELETE.TEXT.FILES:
          GOSUB UPDATE.CONTROLS:
          IF OUTARGS THEN
               MESSAGE = OUTARGS<1>
               ERRORS = OUTARGS<2>
               FINISHED = TRUE
          END ELSE
               UPDATES.CT +=1
          END
     END
     IF LM THEN PRINT "*** FINISHED PROCESS.UPDATE"
     RETURN

GET.TRADE.STATS:
     IF LM THEN PRINT "*** GET.TRADE.STATS"
     READ PD FROM PRICE.ARCHIVE,UPDATEID:"-edfu_upd_rpt.txt" THEN
          PD = CHANGE(PD,CHAR(9),@VM)
          PD = CHANGE(PD,CHAR(13),@FM)
          Y=3
          LOOP
               Y+=1
          WHILE TRIM(PD<Y,1>) # "" DO
               TS.KEY = PD<Y,1>
               TS.NAME = PD<Y,2>
               READ TSBUFF FROM TS.VEND,TS.KEY ELSE TSBUFF = ""
               XMISC<22,Y-3> = TS.KEY
               XMISC<23,Y-3> = TS.NAME
               LOCATE LOG.ID IN TSBUFF<11> BY 'DL' SETTING POS ELSE
                    FOR I = 11 TO 16
                         INS '' BEFORE TSBUFF<I,POS>
                    NEXT I
                    TSBUFF<11,POS> = LOG.ID
               END
               FOR TS.X = 3 TO 7
                    TSBUFF<TS.X+9,POS> = PD<Y,TS.X>
                    XMISC<TS.X+21,Y-3> = PD<Y,TS.X>
               NEXT TS.X
               WRITE TSBUFF ON TS.VEND,TS.KEY
          REPEAT
          T.NEW = PD<Y+1,2>
          T.UPDATE = PD<Y+2,2>
          T.DELETE = PD<Y+3,2>
          T.ZAPPED = PD<Y+4,2>
          T.TOTAL = PD<Y+5,2>
     END
     RETURN

WRITE.I2.UPDATE.LOG:
     IF LM THEN PRINT "*** WRITE.I2.UPDATE.LOG"
     IF NOT(UPDATEID1) THEN UPDATEID1 = FIELD(UPDATEID,".",1) ELSE UPDATEID1 = UPDATEID
     UPDATEID = UPDATEID1
     RUN.COUNT += 1
     IF LM THEN PRINT "WRITING TO I2.PRICE.UPDATE.LOG"
     IF LM THEN PRINT LOG.ID:":":XMISC
     WRITE XMISC ON I2.PRICE.UPDATE.LOG,LOG.ID
     IF DM THEN DEBUG
     RETURN

FIND.SOURCE.FILE:
     IF LM THEN PRINT "** FIND.SOURCE.FILE"
     PRICE.FILE = PRICE.NEW
     PRICE.PATH = I2.NEW.PATH
     GOSUB SEARCH.FOR.SOURCE:
     IF NOT(SOURCE.FOUND) THEN
          PRICE.FILE = PRICE.ARCHIVE
          PRICE.PATH = I2.ARCHIVE.PATH
          GOSUB SEARCH.FOR.SOURCE:
     END
     RETURN

SEARCH.FOR.SOURCE:
     IF LM THEN PRINT "*** SEARCH FOR SOURCE"
     IF UPDATE.TYPE = 'T' THEN
          ZIP.FILE = UPDATEID:".zip"
          SOURCE.FILE.PATH = PRICE.PATH
          ITEM.NAME = ZIP.FILE
          SOURCE.ITEM=ITEM.NAME
          TARGET.FILE.PATH = I2.ARCHIVE.PATH
          TARGET.FILE = PRICE.ARCHIVE
          READV EXISTS FROM PRICE.FILE,SOURCE.ITEM,0 THEN
               GOSUB UNZIP.ZIP:
               GOSUB RENAME.FILES:
               ITEM.NAME = UPDATEID:"-pricesvc.txt"
               FILE.NAME = PRICE.NAME.ARCHIVE
               FILE.PATH = I2.ARCHIVE.PATH
               SOURCE.ITEM = ITEM.NAME
          END
     END ELSE
          LOOP
               ITEM.NAME = UPDATEID
               FILE.TYPE = FIELD(UPDATEID,".",2)
               IF LM THEN PRINT "   *** CHECK FOR ":ITEM.NAME:" IN ":PRICE.PATH
               READV EXISTS FROM PRICE.FILE,ITEM.NAME,0 THEN
                    SOURCE.ITEM = ITEM.NAME
                    SOURCE.FILE.PATH = PRICE.PATH
                    IF FILE.TYPE = 'ZIP' THEN
                         IF LM THEN PRINT "FOUND ":SOURCE.ITEM:" IN ":SOURCE.FILE.PATH
                         TARGET.FILE.PATH = PRICE.PATH
                         TARGET.FILE = PRICE.FILE
                         GOSUB UNZIP.ZIP:
                    END ELSE
                         IF (FILE.TYPE='XLS' OR FILE.TYPE = 'XLSX') THEN
                              GOSUB CONVERT.XLS:
                         END ELSE
                              IF (FILE.TYPE = 'CSV' OR FILE.TYPE = 'TXT') THEN
                                   ITEM.NAME = UPDATEID
                                   GOSUB MOVE.CSV.ARCHIVE:
                                   FILE.NAME = PRICE.NAME.ARCHIVE
                                   FILE.PATH = I2.ARCHIVE.PATH
                                   SOURCE.FOUND=TRUE
                              END
                         END
                    END
               END ELSE
                    PRINT ITEM.NAME:" NOT FOUND IN ":PRICE.PATH
                    SOURCE.FOUND=TRUE
               END
          UNTIL SOURCE.FOUND
          REPEAT
     END
     RETURN

MOVE.CSV.ARCHIVE:
     IF LM THEN PRINT "*** MOVE.CSV.ARCHIVE"
     IF SOURCE.FILE.PATH <> I2.ARCHIVE.PATH THEN
          UP.ITEM.NAME = UPCASE(ITEM.NAME)
          DOS.CMD = "MOVE ":SOURCE.FILE.PATH:ITEM.NAME:' ':I2.ARCHIVE.PATH:UP.ITEM.NAME
          ITEM.NAME = UP.ITEM.NAME
          IF LM THEN PRINT DOS.CMD
          EXECUTE "DOS /C ":DOS.CMD CAPTURING CAP
          *         DEBUG
          IF LM THEN PRINT CAP
          SOURCE.FILE.PATH = I2.ARCHIVE.PATH
     END
     RETURN

GET.EFFDATE:
     IF LM THEN PRINT "*** GET.EFFDATE"
     READ MEDIABUFF FROM PRICE.ARCHIVE,UPDATEID:"-media.tbl" THEN
          MAST.EFFDATE = ICONV(FIELD(MEDIABUFF<2>,CHAR(9),4),"D4/")
          PRINT "EFFECTIVE DATE:":OCONV(MAST.EFFDATE,"D4/")
     END ELSE PRINT "CANNOT READ FROM MEDIA.TBL" ; DEBUG
     RETURN

DELETE.TEXT.FILES:
     IF LM THEN PRINT "*** DELETE.TEXT.FILES"
     READ LAST.TRADE.UPDATE FROM LCS.CONTROL,"LAST.TRADE.UPDATE" THEN
          DELETE PRICE.ARCHIVE,UPDATEID:"-zone.txt"
          DELETE PRICE.ARCHIVE,UPDATEID:"-mstrzone.txt"
          DELETE PRICE.ARCHIVE,UPDATEID:"-edfu_upd_rpt.txt"
          DELETE PRICE.ARCHIVE,UPDATEID:"-pricesvc.txt"
          DELETE PRICE.ARCHIVE,UPDATEID:"-media.tbl"
          DELETE PRICE.ARCHIVE,UPDATEID:"-comm.txt"
          DELETE PRICE.ARCHIVE,UPDATEID:UPDATE.CLASS
     END
     RETURN

UNZIP.ZIP:
     IF LM THEN PRINT "*** UNZIP.ZIP"
     SOURCE.FOUND = TRUE
     IF LM THEN PRINT "UNZIPPING ":SOURCE.ITEM:" IN ":SOURCE.FILE.PATH
     DOS.STRING = 'DOS /C C:\7ZIP\7Z x ':SOURCE.FILE.PATH:SOURCE.ITEM:' -y -o':TARGET.FILE.PATH
     IF LM THEN PRINT DOS.STRING
     EXECUTE DOS.STRING CAPTURING CAP
     *         DEBUG
     IF LM THEN PRINT CAP
     IF INDEX(CAP,"Error",1) THEN
          EOF = TRUE
          LOG.ID = ""
          PRINT SOURCE.ITEM:" NOT FOUND IN ":SOURCE.FILE.PATH
     END ELSE
          SOURCE.FILE.PATH = TARGET.FILE.PATH
          PRICE.FILE = TARGET.FILE
     END
     RETURN

CONVERT.XLS:
     IF LM THEN PRINT "*** CONVERT.XLS"
     **CODE TO CONVERT .XLS TO .TXT
     IF LM=1 THEN PRINT "CONVERTING ":SOURCE.ITEM:" IN ":SOURCE.FILE.PATH
     DOS.STRING = 'DOS /C C:\CXLS ':SOURCE.FILE.PATH:SOURCE.ITEM:' -y -o':I2.ARCHIVE.PATH
     IF LM THEN PRINT DOS.STRING
     EXECUTE DOS.STRING CAPTURING CAP
     *         DEBUG
     IF LM THEN PRINT CAP
     IF INDEX(CAP,"Error",1) THEN
          EOF = TRUE
          LOG.ID = ""
          PRINT SOURCE.ITEM:" NOT FOUND IN ":SOURCE.FILE.PATH
     END ELSE
          SOURCE.FILE.PATH = I2.ARCHIVE.PATH
          PRICE.FILE = PRICE.ARCHIVE
     END
     RETURN

RENAME.FILES:
     IF LM THEN PRINT "*** RENAME.FILES"
     IF LM THEN PRINT "RENAMING FILES IN ARCHIVE"
     FILE.PARTS = "zone.txt,mstrzone.txt,edfu_upd_rpt.txt,pricesvc.txt,comm.txt,media.tbl"
     FILE.PARTS = CHANGE(FILE.PARTS,",",@VM)
     FOR PART.X = 1 TO 6
          FILE.PART = FILE.PARTS<1,PART.X>
          IF LM THEN PRINT "RENAMING TO ":SOURCE.FILE.PATH:FILE.PART:
          READV EXISTS FROM PRICE.ARCHIVE,UPDATEID:"-":FILE.PART,0 THEN
               IF LM THEN PRINT "  ":UPDATEID:"-":FILE.PART:" ALREADY EXISTS"
          END ELSE
               DOS.CMD = "RENAME ":SOURCE.FILE.PATH:FILE.PART:" ":UPDATEID:"-":FILE.PART
               IF LM THEN PRINT DOS.CMD
               EXECUTE "DOS /C ":DOS.CMD CAPTURING CAP
               IF LM THEN PRINT CAP
          END
     NEXT PART.X
     SOURCE.ITEM=UPDATEID:"-pricesvc.txt"
     RETURN

     *MOVE.ZIP.ARCHIVE:
     *IF LM THEN PRINT "MOVING ":ZIP.FILE:" FROM NEW TO ARCHIVE"
     *DOS.CMD = "MOVE /Y ":I2.SOURCE.PATH:ZIP.FILE:" ":I2.ARCHIVE.PATH
     *EXECUTE "DOS /C ":DOS.CMD CAPTURING CAP
     *IF INDEX(CAP,"cannot find",1) THEN
     *DONE=1
     *OUTARGS = CAP
     *PRINT OUTARGS
     *STOP
     *END
     *RETURN

UPDATE.CONTROLS:
     IF LM THEN PRINT "**** UPDATE.CONTROLS"
     IF UPDATE.TYPE = 'T' THEN
          NEXT.TRADE.UPDATE.LOG = NEXT.ISSUE
     END ELSE
          IF UPDATE.TYPE = "V" THEN
               NEXT.VENDOR.UPDATE.LOG += 1
          END ELSE
               IF UPDATE.TYPE = "M" THEN
                    NEXT.MANUAL.UPDATE.LOG += 1
               END ELSE
                    IF UPDATE.MODE = "REFRESH" THEN
                         NEXT.REFRESH.UPDATE.LOG += 1
                    END
               END
          END
     END
     WRITE NEXT.PRICE.UPDATE.LOG ON LCS.CONTROL,"NEXT.PRICE.UPDATE.LOG"
     RETURN

SET.NEXT.ISSUE:
     IF THIS.ISSUE[3,2] = "52" THEN
          NEW.ISSUE = ("00":THIS.ISSUE[1,2]+1) "R#2":"01"
     END ELSE
          NEXT.YEAR = THIS.ISSUE[1,2]
          NEXT.WEEK=THIS.ISSUE[3,2]+1
          NEXT.WEEK=("00":NEXT.WEEK) "R#2"
          NEXT.ISSUE =NEXT.YEAR:NEXT.WEEK
     END
     RETURN

PRINT.SUMMARY:
     PRINT
     PRINT "UPDATE ID:":UPDATEID
     PRINT "ITEM.NAME:":ITEM.NAME
     PRINT "FILE.NAME:":FILE.NAME
     PRINT "UPDATE.MODE:":UPDATE.MODE
     PRINT "UPDATE PROCESSED WITH LOG.ID ":LOG.ID
     RETURN


PRINT.FINAL.INFO:
     PRINT
     PRINT ERRORS:" ERRORS WERE FOUND."
     PRINT UPDATES.CT:" UPDATES WERE SUCCESSFULLY PROCESSED"
     OUTARGS = UPDATES.CT
     PRINT
     RETURN
     *
     *
PROCESS.ITEM.NAME:
     READ LOG.ID FROM UPDATE.NAME.XREF,ITEM.NAME ELSE LOG.ID = ''
     IF LOG.ID THEN READ XMISC FROM I2.PRICE.UPDATE.LOG,LOG.ID ELSE
          LOG.ID = ""
          XMISC = ""
     END
     RETURN



