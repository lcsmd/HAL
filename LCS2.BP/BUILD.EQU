      PROGRAM BUILD.EQU
      EQU IND TO "      "
      FILE.NAME = UPCASE(FIELD(@SENTENCE," ",2))
      PRINT "FN:":FILE.NAME
      OPEN "DICT",FILE.NAME TO D.FILE.NAME ELSE PRINT FILE.NAME;STOP
      READ INCLUDE.B FROM D.FILE.NAME,"$INCLUDE" THEN
         INCLUDE.B = UPCASE(INCLUDE.B)
         INCLUDE.B = TRIMS(INCLUDE.B)
         FILE.EQU.NAME = INCLUDE.B<2>
         FILE.ITEM = INCLUDE.B<3>
         ABB.DYN = INCLUDE.B<4>
         COPYRIGHT = INCLUDE.B<5>
         CONV.CODE = INCLUDE.B<7>
         DM.MODE = INCLUDE.B<8>
         ABB.MAT = INCLUDE.B<11>
         DYNAMIC.MODE = ""
         MATRIX.MODE = ""
         IF INDEX(DM.MODE,"D",1) THEN DYNAMIC.MODE = "Y"
         IF INDEX(DM.MODE,"M",1) THEN MATRIX.MODE = "Y"

         OPEN FILE.EQU.NAME TO F.FILE.EQU.NAME ELSE PRINT "FILE NOT FOUND ":FILE.EQU.NAME;STOP
         SELECT.STR = 'SSELECT DICT ':FILE.NAME:' WITH F1 = D AND WITH F2 > 0 BY LOC.R'
         PRINT SELECT.STR
         EXECUTE SELECT.STR
         READLIST DICT.IDS THEN
            DICT.CT = DCOUNT(DICT.IDS,@FM)
            DICT.IDS = TRIMS(DICT.IDS)
            DICT.IDS = UPCASE(DICT.IDS)
            CONVERT "#" TO "" IN DICT.IDS
            CONVERT "-" TO "_" IN DICT.IDS
            CONVERT " " TO "_" IN DICT.IDS
            IF DYNAMIC.MODE THEN
               MODE='DYNAMIC'
               FILE.ITEM = INCLUDE.B<3>
               GOSUB CREATE.EQU.FILE:
            END
            IF MATRIX.MODE THEN
               FILE.ITEM = INCLUDE.B<3>:".MAT"
               MODE = 'MATRIX'
               GOSUB CREATE.EQU.FILE:
            END
         END
      END ELSE
         PRINT "$INCLUDE ENTRY NOT FOUND FOR DICTIONARY FOR FILE ":FILE.NAME
      END
      STOP

CREATE.EQU.FILE:
      BUFF = ""
      BUFF<1> = " **** ":FILE.NAME:" - START OF ":MODE:" EQU section"
      LINE.X = 1
      LAST.LOC = ""
      IF MODE = "MATRIX" THEN
         FILE.ITEM:=".MAT"
         BUFF<2> = "MAT ":ABB.MAT:".M(":DICT.CT:")"
         BUFF<3> = ""
         LINE.X+=2
      END
      FOR DICT.X = 1 TO DICT.CT
         DICT.KEY = DICT.IDS<DICT.X>
         READ DBUFF FROM D.FILE.NAME,DICT.KEY THEN
            PRINT DICT.KEY
            LOC = DBUFF<2>
            IF LOC THEN
               IF LOC <> LAST.LOC THEN
                  LINE.X = LINE.X + 1
                  ATT.NAME = DICT.KEY
                  BUFF<LINE.X> = IND:"EQU "
                  IF MODE = "DYNAMIC" THEN
                     BUFF<LINE.X>:=ABB.DYN:ATT.NAME:" TO ":ABB.DYN:"B<":LOC:">"
                     BUFF<LINE.X>:=", ":ABB.DYN:ATT.NAME:".ATT TO ":LOC
                  END
                  IF MODE = "MATRIX" THEN
                     BUFF<LINE.X>:=ABB.MAT:ATT.NAME:" TO ":ABB.MAT:"M(":LOC:")"
                     BUFF<LINE.X>:=", ":ABB.DYN:ATT.NAME:".ATT TO ":LOC
                  END
                  IF (CONV.CODE AND DBUFF<3>) THEN
                     IF MODE = "DYNAMIC" THEN ABB = ABB.DYN
                     IF MODE = "MATRIX" THEN ABB = ABB.MAT
                     CONV.NAME = ABB:ATT.NAME:".CNV"
                     GOSUB CHECK.COMMA:
                     BUFF<LINE.X>:=CONV.NAME:' TO "':DBUFF<3>:'"'
                  END
               END
            END
         END
         PRINT BUFF<LINE.X>
      NEXT DICT.X
      LINE.X = LINE.X + 1
      BUFF<LINE.X> = "****":UPCASE(FILE.NAME):" - END of ":MODE:" EQU section"
      BUFF<LINE.X+1> = "*** COPYRIGHT ":COPYRIGHT:" ":OCONV(DATE(),'D4-'):" ":OCONV(TIME(),'MTS')
      BUFF<LINE.X+2> = ""
      DELETE F.FILE.EQU.NAME,FILE.ITEM
      WRITE BUFF ON F.FILE.EQU.NAME,FILE.ITEM
      PRINT "Creating entry in ":FILE.EQU.NAME:" named ":FILE.ITEM:" for file ":FILE.NAME
      PRINT
      FOR SHOW.X = 1 TO LINE.X
         PRINT BUFF<SHOW.X>
      NEXT SHOW.X
      RETURN

CHECK.COMMA:
      IF BUFF<LINE.X> <> IND:"EQU " THEN
         BUFF<LINE.X>:=", "
      END
      RETURN

      END






