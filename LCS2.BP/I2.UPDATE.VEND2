SUBROUTINE I2.UPDATE.VEND(NEXT.UPDATE,UPDATE.OPTIONS.IN,OUTARGS)
PRINT @(-1):
DEFFUN EXTRACT.FIELD(BUFF,LABEL)
DEFFUN CONVERT.ROLLUP.GROUP(PRICE.GROUP,F.PRICE.GROUP)
DEFFUN LS.DATE(DATE.TEXT)
DEFFUN CONVERT.FROM.CSV(A)
INCLUDE UTLSBP ASSIGN.VAR
LM=0                             ; * SET LIVE MONITORING 0=OFF 1=ON
DM=0

**I2.PRICE.UPDATE.LOG
EQU UPDATE.TYPE.P TO XMISC<1>, UPDATEID TO XMISC<2>, USER TO XMISC<3>, VENDOR.NUM TO XMISC<4>
EQU VENDOR.CODE TO XMISC<5>,  VENDOR.NAME TO XMISC<6>
EQU NOTE TO XMISC<7>, MAST.EFFDATE TO XMISC<8>, MAST.EXPDATE TO XMISC<9>, UPDATE.MODE TO XMISC<10>
EQU UPDATE.SCOPE TO XMISC<11>
EQU FILE.NAME TO XMISC<12,1>, FILE.PATH TO XMISC<12,2>, ITEM.NAME TO XMISC<13>
EQU DELIM TO XMISC<14>
EQU COL.LABELS TO XMISC<15>
EQU COL.VALS TO XMISC<16>
EQU UPC.COL TO XMISC<16,1>, CAT.COL TO XMISC<16,2>,COST.COL TO XMISC<16,3>, UOM.COL TO XMISC<16,4>, EFFDATE.COL TO XMISC<16,5>,EXPDATE.COL TO XMISC<16,6>,PG.COL TO XMISC<16,7>
EQU HEADER.LABELS TO XMISC<17>
EQU HEADER.VALS TO XMISC<18>
EQU UPC.FORMAT TO XMISC<18,1>, DATA.START TO XMISC<18,2>
EQU CUSTOM.SUB TO XMISC<19>
EQU MAST.UOM TO XMISC<20>
EQU RUN.COUNT TO XMISC<21>
EQU V.NUM TO XMISC<22>, V.NAME TO XMISC<23>, V.NEW TO XMISC<24>
EQU V.UPDATE TO XMISC<25>, V.DELETE TO XMISC<26>, V.ZAP TO XMISC<27>
EQU V.TOTAL TO XMISC<28>
EQU T.NEW TO XMISC<29>, T.UPDATE TO XMISC<30>, T.DELETE TO XMISC<31>
EQU T.ZAPPED TO XMISC<32>, T.TOTAL TO XMISC<33>

*** DETAIL OF NEXT.PRICE.UPDATE
EQU NEXT.TRADE.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,1>
EQU NEXT.VENDOR.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,2>
EQU NEXT.MANUAL.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,3>
EQU NEXT.REFRESH.UPDATE.LOG TO NEXT.PRICE.UPDATE.LOG<1,4>

**** CUSTOM.PRICES - START OF EQU section
EQU CUS.UP.DATE.H TO 1, CUS.UP.DATE TO CUS.B<1>
EQU CUS.SOURCE.NUM.H TO 7, CUS.SOURCE.NUM TO CUS.B<7>
EQU CUS.SOURCE.NAME.H TO 8, CUS.SOURCE.NAME TO CUS.B<8>
EQU CUS.C4.COST.DT.H TO 10, CUS.C4.COST.DT TO CUS.B<12>
EQU CUS.TYPE.H TO 2, CUS.TYPE TO CUS.B<2>
EQU CUS.EFF.DATE.H TO 3, CUS.EFF.DATE TO CUS.B<3>
EQU CUS.EXP.DATE.H TO 4, CUS.EXP.DATE TO CUS.B<4>
EQU CUS.SP.COST.H TO 5, CUS.SP.COST TO CUS.B<5>
EQU CUS.SP.UOM.H TO 6, CUS.SP.UOM TO CUS.B<6>
EQU CUS.C4.COST.H TO 9, CUS.C4.COST TO CUS.B<9>
EQU CUS.UPDATE.TYPE.H TO 10, CUS.UPDATE.TYPE TO CUS.B<10>
EQU CUS.UPDATE.DATE.H TO 18, CUS.UPDATE.DATE TO CUS.B<18>
EQU CUS.APPLY.DATE.H TO 19, CUS.APPLY.DATE TO CUS.B<19>
*EQU CUS.MFGUCC.H TO 14, CUS.MFGUCC TO CUS.B<14>
*EQU CUS.ITEM_NUM.H TO 15, CUS.ITEM_NUM TO CUS.B<15>
*EQU CUS.CATNUM.H TO 16, CUS.CATNUM TO CUS.B<16>
*EQU CUS.LOG.ID.H TO 17, CUS.LOG.ID TO CUS.B<17>
****CUSTOM.PRICES - END of EQU section

**** I2.PRODUCT.VEND***
EQU UPC TO PF.B2<1>, CATNUM TO PF.B2<2>, COST TO PF.B2<3>, UOM TO PF.B2<4>
EQU PG TO PF.B2<5>, EFF.DATE TO PF.B2<6>, EXP.DATE TO PF.B2<7>, I2.PIK TO PF.B2<8>

OPEN 'CONTROL' TO F.CONTROL ELSE PRINT 'CONTROL' ; STOP
OPEN 'TRADESERVICE,ARCHIVE' TO F.I2.ARCHIVE ELSE PRINT 'TRADESERVICE,ARCHIVE';STOP
OPEN 'LCS.CONTROL' TO LCS.CONTROL ELSE PRINT 'LCS.CONTROL' ; STOP
READV APPLY.CUSTOM.COSTS.TRADE FROM LCS.CONTROL,"APPLY.CUSTOM.COSTS.TRADE",1 ELSE APPLY.CUSTOM.COSTS.TRADE = "N"
READ I2.VEND.MAP1 FROM LCS.CONTROL,'I2.VEND.MAP1' ELSE I2.VEND.MAP1 = ""
READ I2.VEND.MAP2 FROM LCS.CONTROL,'I2.VEND.MAP2' ELSE I2.VEND.MAP2 = ""
I2.VEND.MAP.CT1 = DCOUNT(I2.VEND.MAP1,@FM)
I2.VEND.MAP.CT2 = DCOUNT(I2.VEND.MAP2,@FM)

OPEN 'I2.PRICE.UPDATE.LOG' TO F.I2.PRICE.UPDATE.LOG ELSE PRINT 'I2.PRICE.UPDATE.LOG' ; STOP
LOG.ID = NEXT.UPDATE
READ NEXT.PRICE.UPDATE.LOG FROM LCS.CONTROL,"NEXT.PRICE.UPDATE.LOG" ELSE NEXT.PRICE.UDPATE.LOG = ""
READ XMISC FROM F.I2.PRICE.UPDATE.LOG,LOG.ID ELSE XMISC = ''
UPDATE.TYPE = UPDATE.TYPE.P
UPDATE.MODE = UPDATE.OPTIONS.IN<1>
UPDATE.SCOPE = UPDATE.OPTIONS.IN<2>

REFRESH = 0
IF LM THEN PRINT "READING LOG.ID:":LOG.ID:"   ":XMISC

ARC.FILE.NAME = FILE.NAME

IF LM THEN
	PRINT "***** STARTING I2.CREATE.PRODUCT.SOURCE"
	PRINT "NEXT.UPDATE:":LOG.ID
	PRINT "SCOPE:":UPDATE.SCOPE
	PRINT "MODE:":UPDATE.MODE
	PRINT "UPDATE.TYPE:":UPDATE.TYPE
END
UPDATE.SOURCE = COUNT(UPDATE.SCOPE,"S")
UPDATE.LIB = COUNT(UPDATE.SCOPE,"L")
UPDATE.PRODUCT = COUNT(UPDATE.SCOPE,"P")
IF UPDATE.MODE = "REFRESH" THEN UPDATE.REFRESH = 1

*    XMISC = ""

GOSUB GET.HEADER.INFO:

IF DELIM = "" THEN
	DELIM = "COMMA"
	DELIMITER = ","
	DEBUG.MODE = 0
END

X.ITEM = DATA.START
TEST.LINES = 0
LOG.CT=0
ANS = ""
F=""

PROC.LOG.MODE = 1
GOSUB INIT.VARIABLES:
IF UPDATE.TYPE = 'V' THEN
	PF.CUSTOM.COST.ATT = 53
	PF.CUSTOM.COST.UOM.ATT = 72
	POS = 2
	I2.FILE.NAME = 'I2.PRODUCT.VEND'
END ELSE
	IF UPDATE.TYPE = "M" THEN
		PF.CUSTOM.COST.ATT = 54
		PF.CUSTOM.COST.UOM.ATT = 73
		POS = 3
		I2.FILE.NAME = 'I2.PRODUCT.MAN'
	END
END
GOSUB OPEN.FILES:

IF UPDATE.SOURCE THEN
	IF DM THEN DEBUG
	GOSUB WRITE.UPDATE.LOG.ENTRY:
END

MAIN:
IF LM THEN PRINT "GETTING HEADER INFO"
I2PSVC.PATH = FILE.PATH:ITEM.NAME
PRINT ARC.FILE.NAME:",":ITEM.NAME

OPEN ARC.FILE.NAME TO F.ARC.FILE.NAME ELSE PRINT ARC.FILE.NAME;STOP
READ PF.BALL FROM F.ARC.FILE.NAME,ITEM.NAME THEN
	ITEM.X.CT = DCOUNT(PF.BALL,@FM)
	ITEM.X =DATA.START
END
GOSUB SHOW.HEADER:
EOF = FALSE
TOTAL.COUNT = 0
CUS.B = ""
GOSUB DISPLAY.PROGRESS:
LOOP
	IF TEST.LINES THEN
		IF TOTAL.COUNT > TEST.LINES THEN EOF = TRUE
	END
	PF.B2 = ""
	PNO=''
	UPC = ""
	CATNUM = ""
	PF.B = ""
	LIB.B = ""
	I2.PIK = ""
	PF.UPC=""
	LOOKUP.CT = 0
	PROD.B = ""
	ORIG.B = ""

	ERROR.STATUS = 0

UNTIL EOF DO
	TOTAL.COUNT = TOTAL.COUNT + 1
	LINE.NUM = ("000000000":TOTAL.COUNT) "R#10"
	PRICE.SOURCE.LINE.ID = LOG.ID:":":LINE.NUM
	IF UPDATE.SOURCE THEN
		GOSUB UPDATE.SOURCE:
		IF PF.B2<6> = "" THEN PF.B2<6> = MAST.EFFDATE
		IF NOT(EOF) THEN
			IF UPDATE.LIB THEN GOSUB UPDATE.LIB
		END
		CALL CUSTOM.PRICE.UPDATE(F.CUSTOM.PRICES,UPDATEID,PRICE.SOURCE.LINE.ID,PF.B2,LIB.B)
		GOSUB I2.UPDATE.KEYS:
	END
	IF NOT(MOD(TOTAL.COUNT,100)) THEN GOSUB DISPLAY.PROGRESS:
REPEAT
GOSUB WRITE.UPDATE.LOG.ENTRY:
FINISHED:
PRINT ; PRINT
RETURN

I2.UPDATE.KEYS:
IF I2.PIK THEN
	READ KEYS FROM I2.KEYS,I2.PIK ELSE KEYS = ""
	IF UPDATE.TYPE = 'V' THEN POS = 2 ELSE IF UPDATE.TYPE  ='M' THEN POS = 3
	LAST.KEY = KEYS<POS>
	IF LAST.KEY > "" THEN
		IF PRICE.SOURCE.LINE.ID > LAST.KEY THEN
			GOSUB MOVE.TO.ARC:
			GOSUB UPDATE.CURRENT.KEY:
		END
	END ELSE
		GOSUB UPDATE.CURRENT.KEY:
	END
END
RETURN

UPDATE.CURRENT.KEY:
KEYS<POS> = PRICE.SOURCE.LINE.ID
KEYS<11+POS> = TODAY
KEYS<11> = LOG.ID
IF CATNUM THEN
	READ CATBUFF.O FROM F.CATVNO,CATNUM ELSE CATBUFF.O = ""
	CATBUFF = CATBUFF.O
	CATBUFF<1> = I2.PIK
	CATBUFF<2> = PNO
	IF UPC THEN CATBUFF<3> = UPC
	CATKEY = CATNUM
	IF CATBUFF.O <> CATBUFF THEN WRITE CATBUFF ON F.CATVNO,CATKEY
END
WRITE KEYS ON I2.KEYS,I2.PIK
RETURN

LOOKUP.PROD:
LOOKUP.CT += 1
PNO.T = ""
PROD.B.T = ""
UPC.T=""
I2.PIK.T = ""
IF I2.PIK THEN
	PNO.T = "!":I2.PIK
	READ PROD.B.T FROM F.PRODUCT,PNO.T ELSE
		READV PNO.T FROM F.PSVNO,I2.PIK,9 ELSE
			SELECT.STR = "SELECT PRODUCT WITH PRICE.SRVC# = ":I2.PIK
			EXECUTE SELECT.STR CAPTURING CAP
			IF FIELD(CAP<2>," ",1) <> "0" THEN
				READLIST PNOS THEN
					PNO.T = PNOS<1,1>
				END
			END
		END
	END
END ELSE
	IF UPC THEN
		IF LEN(UPC) = 5 THEN UPC=VENDOR.NUM:UPC
		READV I2.PIK.T FROM F.UPC.PSVNO.XREF,UPC,1 ELSE
			UPC = UPC[1,11]
			READV I2.PIK.T FROM F.UPC.PSVNO.XREF,UPC,1 ELSE
				UPC = "0":UPC
				READV I2.PIK.T FROM F.UPC.PSVNO.XREF,UPC,1 ELSE
					UPC=UPC[1,11]
					READV I2.PIK.T FROM F.UPC.PSVNO.XREF,UPC,1 ELSE
						READV PNO.T FROM F.I2.PRODUCT,I2.PIK,9 ELSE
							READ PNO.T FROM F.PSVNO,UPC ELSE
								SELECT.STR = "SELECT PRODUCT WITH UPC = ":UPC
								EXECUTE SELECT.STR CAPTURING CAP
								IF FIELD(CAP<2>," ",1) <> "0" THEN
									READLIST PNOS THEN
										PNO.T = PNOS<1,1>
									END
								END
							END
						END
					END
				END
			END
		END
	END ELSE
		IF CATNUM THEN
			READ CATNO.B FROM F.CATVNO,CATNUM ELSE
				READ CATNO.B FROM F.CATVNO,VEND.CODE:CATNUM ELSE CATNO.B = ""
			END
			IF CATNO.B<1> THEN I2.PIK = CATNO.B<1>
			IF CATNO.B<2> THEN PNO = CATNO.B<2>
			IF CATNO.B<3> THEN UPC = CATNO.B<3>
		END
	END
END
IF I2.PIK.T THEN I2.PIK = I2.PIK.T
IF UPC.T THEN UPC = UPC.T
IF PNO.T THEN PNO=PNO.T
*END
*IF (LOOKUP.CT AND I2.PIK = "") THEN GOSUB LOOKUP.PROD:
*IF I2.PIK = "" AND DM THEN DEBUG
RETURN


SHOW.HEADER:
TITLE = "LOG.ID: ":LOG.ID:"  VENDOR:":VENDOR.CODE:"  ":VENDOR.NAME:"  VENDOR#:":VENDOR.NUM "R#6 ":"MODE:":UPDATE.MODE
TITLE2 = "UPDATE.TYPE:":UPDATE.TYPE:" USER: ":USER:"  UPDATEID: ":UPDATEID:"  EFFDATE:":OCONV(MAST.EFFDATE,"D2-"):" SCP:":UPDATE.SCOPE
TITLE3 = ""
*IF NOTE THEN TITLE3 := "   NOTE:":NOTE
PROC.LOG = "RAN INTERPRET.XMISC: XMISC = ":XMISC:" UPDATE.TYPE:":FILE.NAME:" MAST.EFFDATE:":OCONV(MAST.EFFDATE,"D4/")
GOSUB WRITE.PROC.LOG
PROC.LOG = TITLE ; GOSUB WRITE.PROC.LOG
PROC.LOG = TITLE2 ; GOSUB WRITE.PROC.LOG
PROC.LOG = TITLE3 ; GOSUB WRITE.PROC.LOG

PRINT @(-1): ; PRINT TITLE ; PRINT TITLE2 ; PRINT TITLE3
PRINT @(1,15):"SOURCE" "R#9":"  : ":"LIBRARY" "R#9":"      ":"PRODUCT" "R#9":

PROC.LOG = "FILE.NAME":"|":"CUSTOM.COST.TYPE":"|":"PF.CUST.COST.ATT":"|":"PF.CUSTOM.COST":"|":"CUSTOM.COST.ATT":"|":"CUSTOM.COST":"|":"PF.CUSTOM.COST.UOM|":"CUSTOM.COST.UOM" ; GOSUB WRITE.PROC.LOG
PROC.LOG = FILE.NAME:"|":CUSTOM.COST.TYPE:"|":PF.CUSTOM.COST.ATT:"|":PF.CUSTOM.COST:"|":CUSTOM.COST.ATT:"|":LIB.B<CUSTOM.COST.ATT>:"|":PF.CUSTOM.COST.UOM.ATT:"|":CUSTOM.COST.UOM ; GOSUB WRITE.PROC.LOG
PROC.LOG = LIB.B ; GOSUB WRITE.PROC.LOG
RETURN

UPDATE.SOURCE:
UPDATE.SOURCE.CT1 += 1
IF LM THEN PRINT "**** UPDATING SOURCE: (UPDATE TYPE:":UPDATE.TYPE:"  ":UPDATE.SOURCE.CT1
PF.B = PF.BALL<ITEM.X>
PF.B2 = ""
ITEM.X += 1
IF ITEM.X > ITEM.X.CT THEN EOF = 1
IF LM THEN PRINT "PF.B:":PF.B
PROC.LOG = "READ SOURCE FILE:":LINE.NUM ; GOSUB WRITE.PROC.LOG:
IF PF.B <> "" THEN
	PF.B = CONVERT.FROM.CSV(PF.B)
	PF.B = TRIM(PF.B,"$","A")
	PF.B = TRIM(PF.B,",","A")
	PF.B = TRIM(PF.B,'"',"A")
	PF.B = TRIM(PF.B,"-","B")
	
	FOR ATT.X = 1 TO COL.LABEL.CT
		COL.ATT = COL.VALS<1,ATT.X>
		IF COL.ATT THEN PF.B2<ATT.X> = PF.B<COL.ATT> ELSE PF.B2<ATT.X> = ""
	NEXT ATT.X
	
	IF UPC.FORMAT THEN
		UPC.P = UPC
		GOSUB GET.UPC:
		IF UPC.P THEN UPC = UPC.P
	END
	GOSUB LOOKUP.PROD:
	READ OPF.B FROM F.I2.PRODUCT.SOURCE,PRICE.SOURCE.LINE.ID ELSE OPF.B = ""
	IF (UPC OR CATNUM OR I2.PIK) THEN
		IF OPF.B <> PF.B2 THEN
			IF PF.B2<1> THEN WRITE PF.B2 ON F.I2.PRODUCT.SOURCE,PRICE.SOURCE.LINE.ID
			UPDATE.SOURCE.CT2 += 1
		END
	END
END
	RETURN

UPDATE.LIB:
	UPDATE.LIB.CT1 += 1
	READ LIB.B FROM F.I2.PRODUCT,I2.PIK ELSE LIB.B = ""
	ALIB.B = LIB.B
	IF COST THEN LIB.B<53>=COST
	IF UOM THEN LIB.B<72>=UOM
	IF LIB.B <> ALIB.B THEN
		IF LIB.B<1> THEN WRITE LIB.B ON F.I2.PRODUCT,I2.PIK
		UPDATE.LIB.CT2 += 1
	END
	RETURN



GET.UPC:
	IF UPC.FORMAT THEN GOSUB RUN.FORMULA:

	UPC.L = LEN(UPC.P)
	IF LEN(UPC.P) = 5 THEN UPC.P = VENDOR.CODE:UPC.P
	IF UPC.L = 14 THEN
		UPC.P=UPC.P[3,11]
	END
	IF UPC.L = 12 THEN UPC.P = UPC.P[1,11]
	IF UPC.L = 10 THEN UPC.P = "0":UPC.P
	UPC.L = LEN(UPC.P)
	IF UPC.L <> 11 THEN
		UPC.P = ("00000000000":UPC.P) "R#11"
		IF LM THEN PRINT "UPC3:":UPC.P
		UPC.P = RIGHT(UPC.P,11)
		IF LM THEN PRINT "UPC4:":PF.UPC
	END
	UPC.P = UPC.P[1,11]
	IF LEN(UPC.P) = 11 THEN
		MFF_UCC_NUM = UPC.P[1,6]
		ITEM_NUM = UPC.P[7,5]
	END
	RETURN


RUN.FORMULA:
	IF UPC.FORMAT = "6:5" THEN
		UPC.P = VENDOR.CODE:ITEM_NUM
	END
	RETURN



PARSE.MANUAL.DATA:
	RETURN


INIT.VARIABLES:
	NOW = DATE()
	EFFDATE = ""
	HEADER = ""
	TODAY = NOW
	OTODAY = OCONV(TODAY,"D4-")
	MONTH = ("00":FIELD(OTODAY,"-",1)) "R#2"
	DAY = ("00":FIELD(OTODAY,"-",2)) "R#2"
	YEAR = FIELD(OTODAY,"-",3)
	DATE.STAMP = YEAR:MONTH:DAY
	BEFORE.NOW = NOW
	START.TIME = TIME()
	PROC.LOG = ""
	PRICE.SOURCE.LINE.ID =0
	UPDATE.LIB.CT1 = 0
	UPDATE.LIB.CT2 = 0
	UPDATE.PRODUCT.CT1 = 0
	UPDATE.PRODUCT.CT2 = 0
	UPDATE.SOURCE.CT1 = 0
	UPDATE.SOURCE.CT2 = 0
	F = ""
	MASTER.CUSTOM.COST.UOM = ""
	PF.HEAD.ITEMS = ""
	PF.HEAD = ""
	PF.HEAD.HEADER = ""
	NOTE = ""
	UPC.OPTIONS = ""
	ZERO.DISC.CT = 0
	MAST.EXPDATE = ""
	UPC11 = ""
	TOTAL.COMPLETED.DATE = ""
	PF.CUSTOM.COST = 0
	PF.CUSTOM.COST.ATT=0
	PF.CUSTOM.COST.UOM = ""
	CUSTOM.COST = 0
	CUSTOM.COST.ATT = 0
	CUSTOM.COST.UOM.ATT = 0
	CUSTOM.COST.UOM = ""
	CUSTOM.COST.DATE.ATT = 0
	CUSTOM.COST.DATE = ""
	I2.LOG.FILE.NAME = LOG.ID:"-":DATE.STAMP
	OPENSEQ "I2.LOGS",I2.LOG.FILE.NAME TO IPUTL ELSE CREATE IPUTL ELSE PRINT "UNABLE TO CREATE I2.PRODUCT.PRICE.UPDATE.LOG.":LOG.ID:DATE.STAMP ; STOP
	IF LM THEN PRINT "STARTING INIT.VARIABLES"
	IF NOT(REFRESH) THEN
		FILE.EXT = FIELD(ITEM.NAME,".",2)
		IF UPCASE(FILE.EXT) = "CSV" THEN DELIM = "COMMA"
		IF UPCASE(FILE.EXT) = 'TSV' THEN DELIM = "TAB"
		IF UPCASE(FILE.EXT) = 'TXT' THEN DELIM = 'TAB'
		IF DELIM = "TAB" THEN DELIMITER = CHAR(9)
		IF DELIM = "COMMA" THEN DELIMITER = ","
		IF LEN(DELIM) = 1 THEN DELIMITER=DELIM
		PROC.LOG = "DELIM:":DELIM ; GOSUB WRITE.PROC.LOG
	END

	LIB.B = ""
	PROC.LOG = "CHECKPOINT 1 UPDATE:":UPDATEID:"  PATH:":FILE.NAME:"  RUN AT ":OCONV(DATE(),'D4/'):" ":OCONV(TIME(),"MHS") ; GOSUB WRITE.PROC.LOG

	PF.C1.ATT = 0

	IF LM THEN PRINT "LOG.ID:":LOG.ID
	PNO=""
	PRCLMS = 'C1':IVM:'C2':IVM:'C3':IVM:'C4':IVM:'C5':IVM:'C6':IVM:'C7':IVM:'L1':IVM:'L2':IVM:'L3':IVM:'L4'
	PRCLMS<2> = 3:IVM:5:IVM:19:IVM:62:IVM:120:IVM:122:IVM:124:IVM:13:IVM:64:IVM:66:IVM:126

	IF UPDATE.TYPE ="V" THEN
		CUSTOM.COST.TYPE = "V"
		CUSTOM.COST.ATT=53
		CUSTOM.COST.UOM.ATT=72
		IF MAST.EFFDATE = "" THEN MAST.EFFDATE = LS.DATE(UPDATEID[5,19])
		MAST.EFFDATE = ICONV(MAST.EFFDATE,"D")
	END ELSE
		IF UPDATE.TYPE = "M" THEN
			CUSTOM.COST.TYPE = "M"
			CUSTOM.COST.ATT=54
			CUSTOM.COST.UOM.ATT=73
*        CUSTOM.COST.DATE.ATT = 112
		END
	END
	IF LM THEN PRINT @(20,20):"  UPDATE.SOURCE.CT: ":UPDATE.SOURCE.CT1
	CATNUM.ATT = 23
	UPC.ATT = 99
	EFFDATE.ATT = 44
	MAST.EXPDATE.ATT = 45
	PRICE.SOURCE.LINE.ID = 0
	PNO.ATT = 9
	PS.PIK.ATT = 1

	PREV.VEND = '' ; PREV.COMM = ''

	UPDATE.DATE = TODAY
	TOTAL.COUNT = 0
	ERROR.COUNT = 0
	UPDATE.LIB.CT2 = 0
	UPDATE.PRODUCT.CT2 = 0
	ZERO.DISC.CT = 0

	I2KEY = ''
	EOF = FALSE
	VENDOR = ""
	RETURN

GET.HEADER.INFO:
	HEAD.LABEL.CT = DCOUNT(HEADER.LABELS,@VM)
	FOR LABEL.X = 1 TO HEAD.LABEL.CT
		DATA.LABEL =  HEADER.LABELS<1,LABEL.X>
		DATA.VALUE = HEADER.VALS<1,LABEL.X>
		GOSUB PARSE.HEADER.LABELS:
	NEXT LABEL.X

	COL.LABEL.CT = DCOUNT(COL.LABELS,@VM)
	FOR LABEL.X = 1 TO COL.LABEL.CT
		DATA.LABEL =  COL.LABELS<1,LABEL.X>
		DATA.VALUE = COL.VALS<1,LABEL.X>
		GOSUB PARSE.HEADER.LABELS:
	NEXT LABEL.X
	IF MAST.EFFDATE THEN
		MAST.EFFDATE = ICONV(MAST.EFFDATE,"D")
	END
	IF NOT(MAST.EFFDATE) THEN MAST.EFFDATE = DATE()
	RETURN

PARSE.HEADER.LABELS:
*IF DATA.LABEL =  "UPDATE.TYPE" THEN   UPDATE.TYPE = DATA.VALUE
*IF DATA.LABEL =   "UPDATE.ID" THEN   UPDATE.ID = DATA.VALUE
	IF DATA.LABEL =   "USER" THEN   USER = DATA.VALUE
	IF DATA.LABEL =   "VEND.NUM" THEN   VEND.NUM = DATA.VALUE
	IF DATA.LABEL =   "VEND.CODE" THEN   VEND.CODE = DATA.VALUE
*IF DATA.LABEL =   "VEND.NAME" THEN   VEND.NAME = DATA.VALUE
	IF DATA.LABEL =   "NOTE" THEN   NOTE = DATA.VALUE
	IF DATA.LABEL =   "MAST.EFFDATE" THEN   MAST.EFFDATE = DATA.VALUE
	IF DATA.LABEL =   "MAST.EXPDATE" THEN   MAST.EXPDATE = DATA.VALUE
*IF DATA.LABEL =   "UPDATE.MODE" THEN   UPDATE.MODE = DATA.VALUE
*IF DATA.LABEL =   "UPDATE.SCOPE" THEN   UPDATE.SCOPE = DATA.VALUE
*IF DATA.LABEL =   "FILE.NAME" THEN   FILE.NAME = DATA.VALUE
*IF DATA.LABEL =   "CUSTOM.SUB" THEN   CUSTOM.SUB = DATA.VALUE
	IF DATA.LABEL =   "MAST.UOM" THEN   MAST.UOM = DATA.VALUE
*IF DATA.LABEL =   "ITEM.NAME" THEN   ITEM.NAME = DATA.VALUE
	IF DATA.LABEL =   "DELIM" THEN   DELIM = DATA.VALUE

	IF DATA.LABEL =   "UPC.COL" THEN   UPC.COL = DATA.VALUE
	IF DATA.LABEL =   "DATA.START" THEN   DATA.START = DATA.VALUE
	IF DATA.LABEL =   "CATNUM.COL" THEN   CAT.COL = DATA.VALUE
	IF DATA.LABEL =   "EFFDATE.COL" THEN   EXPDATE.COL = DATA.VALUE
	IF DATA.LABEL =   "EXPDATE.COL" THEN   EFFDATE.COL = DATA.VALUE
	IF DATA.LABEL =   "COST.COL" THEN   COST.COL = DATA.VALUE
	IF DATA.LABEL =   "UOM.COL" THEN   UOM.COL = DATA.VALUE
	IF DATA.LABEL =   "UPC.FORMAT" THEN   UPC.FORMAT = DATA.VALUE
	IF DATA.LABEL =   "PG.COL" THEN   PG.COL = DATA.VALUE

	RETURN

OPEN.FILES:
	IF LM THEN PRINT "OPENING FILES"
	OPEN 'I2.PRODUCT' TO F.I2.PRODUCT ELSE F = F:', I2.PRODUCT'

	OPEN 'PSVNO' TO F.PSVNO ELSE F = F:', PSVNO'
	OPEN 'CATVNO' TO F.CATVNO ELSE F = F:', CATVNO'
	OPEN 'PRODUCT' TO F.PRODUCT ELSE F = F:', PRODUCT'
	OPEN 'PROD.FIND' TO F.PROD.FIND ELSE F = F:', PROD.FIND'
	OPEN 'PROD.LINE' TO F.PROD.LINE ELSE F = F:', PROD.LINE'
	OPEN 'PRICE.ROLLUP' TO F.PRICE.ROLLUP ELSE F = F:', PRICE.ROLLUP'
	OPEN 'PRICE.GROUP' TO F.PRICE.GROUP ELSE F = F:', PRICE.GROUP'
	OPEN 'UPC.PSVNO.XREF' TO F.UPC.PSVNO.XREF ELSE F = F:', UPC.PSVNO.XREF'
	OPEN 'UOM' TO F.UOM ELSE F = F:', UOM'
	OPEN I2.FILE.NAME TO F.I2.PRODUCT.SOURCE ELSE PRINT I2.FILE.NAME ; STOP
	OPEN I2.FILE.NAME:".ARC" TO F.I2.PRODUCT.SOURCE.ARC ELSE PRINT I2.FILE.NAME:".ARC";STOP
	OPEN 'I2.PRODUCT.ARCHIVE' TO F.I2.PRODUCT.ARCHIVE ELSE F = F:', I2.PRODUCT.ARCHIVE'
	OPEN 'UPC.PRODUCT.XREF' TO F.UPC.PRODUCT.XREF ELSE F = F:", UPC.PRODUCT.XREF"
	OPEN 'CUSTOM.PRICES' TO F.CUSTOM.PRICES ELSE F = F:", CUSTOM.PRICES"
	OPEN 'PRICE.TRANS' TO PRICE.TRANS ELSE PRINT 'PRICE.TRANS' ; STOP
	PROC.LOG = "OPEN.FILES DONE WITH F=":F
	OPEN 'DICT','I2.PRICE.UPDATE.LOG' TO D.I2PUL ELSE PRINT "DICT,I2.PRICE.UPDATE.LOG";STOP
	OPEN 'DICT','I2.PRODUCT' TO D.I2.PRODUCT ELSE PRINT 'DICT,I2.PRODUCT';STOP
	OPEN 'I2.KEYS' TO I2.KEYS ELSE PRINT 'I2.KEYS';STOP
	IF NOT(REFRESH) THEN
		OPEN ARC.FILE.NAME TO F.UP ELSE PRINT ARC.FILE.NAME ; DEBUG
	END

	GOSUB CHECK.ERRORS:

	READ I2.HEADER FROM LCS.CONTROL, "I2.HEADER" ELSE I2.HEADER = ''

	READ OPT.B FROM F.CONTROL, 'I2.OPTIONS' ELSE OPT.B = ''
	READ NEVER.UPDATE.DESC.I2 FROM F.CONTROL,'NEVER.UPDATE.DESC.I2' ELSE NEVER.UPDATE.DESC.I2 = ''
	READ I2FT.B FROM F.CONTROL, 'I2.FIELD.TEMPLATE' ELSE I2FT.B = ''
	READ VEND.B FROM F.CONTROL, 'I2.VEND' ELSE VEND.B = ''
	READ UPD.UOM FROM F.CONTROL,"I2.UPD.UOM" ELSE UPD.UOM = "N"
	READ COMP.B FROM F.CONTROL, 'COMPANY' ELSE COMP.B = ''
	READ C4.OVERWRITABLE FROM F.CONTROL, 'C4.OVERWRITABLE' ELSE C4.OVERWRITABLE = 'N'
	GUI.SYSTEM = COMP.B<247>
	GOSUB CHECK.ERRORS

	RETURN



CHECK.EOM:
	FOR PRICE.INDEX = 46 TO 54
		IF NOT(NUM(LIB.B<PRICE.INDEX>)) THEN LIB.B<PRICE.INDEX> = 0
	NEXT PRICE.INDEX

	PROC.LOG = "CHECK.EOM:" ; GOSUB WRITE.PROC.LOG
	MD=10000
	CMD = 10000
	UOM = LIB.B<39>
	PUOM = 'EA'
	CUOM = LIB.B<CUSTOM.COST.UOM.ATT>
	IF CUOM = "EA" THEN CUOM = 'E'
	IF UOM = "EA" THEN UOM = 'E'
	BEGIN CASE
		CASE CUOM = 'E' ; CMD = 10000
		CASE CUOM = 'C' ; CMD = 100
		CASE CUOM = 'M' ; CMD = 10
	END CASE

	BEGIN CASE
		CASE UOM = 'E' ; MD = 10000 ; PUOM = 'EA'
		CASE UOM = 'C' ; MD = 100 ; PUOM = 'C'
		CASE UOM = 'M' ; MD = 10 ; PUOM = 'M'
	END CASE
	PROC.LOG = "CUSTOM.COST.UOM.ATT:":CUSTOM.COST.UOM.ATT:" "
	PROC.LOG := "UOM: ":UOM:" PUOM:":PUOM:" CUOM:":CUOM:" CMD:":CMD:" MD:":MD ; GOSUB WRITE.PROC.LOG

	RETURN


WRITE.UPDATE.LOG.ENTRY:
	RUN.COUNT += 1

	WRITE XMISC ON F.I2.PRICE.UPDATE.LOG,LOG.ID

	RETURN


CHECK.ERRORS:
	IF F # '' THEN
		PROC.LOG = F
		GOSUB WRITE.PROC.LOG
		PRINT FILE.ERRORS:F:BCK: ; INPUT NL
		IF NL = 'D' THEN DEBUG
		IF NL = 'E' THEN GOTO FINISHED:
	END
	RETURN

WRITE.PROC.LOG:
*IF LM THEN PRINT PROC.LOG
	IF PROC.LOG.MODE THEN
		LOG.CT += 1
		CONVERT @FM TO "|" IN PROC.LOG
		BEFORE.NOW = NOW
		NOW = DATE()
		DELTA = NOW - BEFORE.NOW
		LOG.ENTRY = NOW:":":DELTA:" ":LOG.CT:"|":UPDATEID:"-":FILE.NAME:"|":PROC.LOG
		PROC.LOG = ""
*IF LM THEN PRINT LOG.ENTRY
		WRITESEQ LOG.ENTRY ON IPUTL ELSE PRINT 'UNABLE TO WRITE TO PROCESS LOG':IPUTL ; STOP
		BEFORE.NOW = NOW
		IF INDEX(PROC.LOG,"FATAL",1) THEN
			ERROR.STATUS = 1
			ERROR.COUNT = ERROR.COUNT + 1
			WRITESEQ "*** FATAL ERROR ENCOUNTERED.  UPDATE FILE ":UPDATEID:" SKIPPED." ON IPUTL ELSE PRINT 'UNABLE TO WRITE TO PROCESS LOG':IPUTL
*DEBUG
		END
	END
	RETURN



DISPLAY.PROGRESS:
	NOW.TIME = TIME()
	ITEMS.DONE=UPDATE.SOURCE.CT1
	IF UPDATE.TYPE='R' THEN ITEMS.DONE=UPDATE.PRODUCT.CT1
	TIME.ELAPSED = NOW.TIME-START.TIME
	PRINT @(1,16):OCONV(UPDATE.SOURCE.CT1,'MD0,') "R#9":"    ":OCONV(UPDATE.LIB.CT1,'MD0,') "R#9":"      ":OCONV(UPDATE.PRODUCT.CT1,'MD0,') "R#9":
	PRINT @(1,17):OCONV(UPDATE.SOURCE.CT2,'MD0,') "R#9":"  : ":OCONV(UPDATE.LIB.CT2,'MD0,') "R#9":"      ":OCONV(UPDATE.PRODUCT.CT2,'MD0,') "R#9":
	IF T.TOTAL THEN PRINT @(1,19):"TOTAL ITEMS:":OCONV(T.TOTAL,"MD0,") "R#9   ":
	IF T.TOTAL > 0 THEN
		DONE = OCONV(((ITEMS.DONE/T.TOTAL)*1000),'MD1')
		IF ITEMS.DONE THEN TIM.EXPECTED = (TIME.ELAPSED*T.TOTAL)/ITEMS.DONE
		TIM.REM = (TIM.EXPECTED-TIME.ELAPSED)
		PRINT DONE:" %    ":"ELAPSED: ":OCONV(TIME.ELAPSED,'MD0,'):" SEC   REMAINING: ":TIM.REM
		SEC.REM = TIM.REM
		HOURS.REM = INT(SEC.REM/3600)
		SEC.REM = SEC.REM - HOURS.REM*3600
		MIN.REM = INT(SEC.REM/60)
		SEC.REM = SEC.REM - MIN.REM*60
		SEC.REM = INT(SEC.REM)
		PRINT "REMAINING TIME: HOURS:":HOURS.REM:"  MIN:":MIN.REM:" SEC:":SEC.REM
	END
	RETURN

MOVE.TO.ARC:
	READ PF.B3 FROM F.I2.PRODUCT.SOURCE,LAST.KEY THEN
		WRITE PF.B3 ON F.I2.PRODUCT.SOURCE.ARC,LAST.KEY
		DELETE F.I2.PRODUCT.SOURCE,LAST.KEY
	END
	RETURN


